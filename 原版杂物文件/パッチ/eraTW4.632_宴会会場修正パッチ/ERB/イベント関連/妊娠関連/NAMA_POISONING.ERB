@CHK_NAMA_POISONING(ARG)
#FUNCTION
LOCAL = 0

;愛情経験系より膣射中毒などの方がモノを言う
;色々閾値がキツい感じ
;口上用イベントは、一応EVENT31に設定。
;1：OK
;2：NG
;3：は？？？（真顔）



SIF TALENT:ARG:恋人
	LOCAL += 100
SIF TALENT:ARG:風騷
	LOCAL += 300
SIF TALENT:ARG:自己愛 == 1
	LOCAL += 50
SIF TALENT:ARG:倒錯的
	LOCAL += 100
SIF TALENT:ARG:Ｖ感度 == 1
	LOCAL += 50
SIF TALENT:ARG:子宮口性感
	LOCAL += 100
SIF TALENT:ARG:淫壺
	LOCAL += 150

LOCAL:3 += GETPALAMLV(PALAM:欲情,15) * (10 + TALENT:ARG:快感応答 * 2)
LOCAL:3 += GETPALAMLV(PALAM:恭順,15) * (10 - TALENT:ARG:態度 * 2)
IF TALENT:ARG:受虐狂
	LOCAL:3 += GETPALAMLV(PALAM:屈服,15) * 2
ELSE
	LOCAL:3 += GETPALAMLV(PALAM:屈服,15)
ENDIF

LOCAL:5 += ABL:ARG:従順 * 5
LOCAL:5 += ABL:ARG:欲望 * (10 + TALENT:ARG:快感応答 * 2)
LOCAL:5 += ABL:ARG:奉仕精神 * 5
LOCAL:5 += ABL:ARG:Ｖ感覚 * 10
LOCAL:5 += ABL:ARG:受虐属性 * 5
LOCAL:5 += ABL:ARG:精液中毒 * 7
LOCAL:5 += ABL:ARG:膣射中毒 * 20
LOCAL:5 += ABL:ARG:膣射中毒 * 15
LOCAL:5 += MARK:ARG:快楽刻印 * 10
LOCAL:5 += MARK:ARG:苦痛刻印 * 10
LOCAL:5 += MARK:ARG:時姦刻印 * 10


LOCAL = LOCAL:3 * LOCAL:5 / 500


RETURNF LOCAL


@CHK_NAMA_REASON(ARG,ARGS)
#FUNCTION
#DIM 理性判定値
VARSET LOCAL


理性判定値 = 800 + MARK:ARG:反発刻印 * 500

SIF ESTRUS_CYCLE(ARG) == 150
	理性判定値 += 1000

IF TCVAR:ARG:発情
	理性判定値 -= 200
ELSEIF TCVAR:ARG:媚薬
	理性判定値 -= 50
ENDIF

IF TALENT:ARG:傲嬌
	IF TALENT:ARG:恋慕
		理性判定値 = 理性判定値 / 2
	ELSE
		理性判定値 = 理性判定値 * 3 / 2
	ENDIF
ENDIF

;信頼度
IF CFLAG:ARG:信頼度 < 100
	理性判定値 *= 5
ELSEIF CFLAG:ARG:信頼度 < 300
	理性判定値 *= 5 / 2
ELSEIF CFLAG:ARG:信頼度 < 500
	理性判定値 *= 5 / 3
ELSEIF CFLAG:ARG:信頼度 < 1000
	理性判定値 *= 5 / 4
ENDIF

;好感度
SELECTCASE CFLAG:ARG:好感度
CASE 0 TO 2000
	理性判定値 *= 10
CASE 2000 TO 3999
	理性判定値 *= 8
CASE 4000 TO 5999
	理性判定値 *= 6
CASE 6000 TO 7999
	理性判定値 *= 4
CASE 8000 TO 9999
	理性判定値 *= 2
CASEELSE
;	理性判定値 *= 1
ENDSELECT

;カウンターだとここで素質処理しておく
IF ARGS == "COUNTER"
	SIF TALENT:ARG:抵抗
		理性判定値 += 300
	IF TALENT:MASTER:謎之魅力
		理性判定値 = 理性判定値 * 9 / 10
	ENDIF
	IF TALENT:ARG:自尊心 == 1
		理性判定値 = 理性判定値 * 7 / 5
	ENDIF
	IF OTOKOGIRAI(ARG)
		理性判定値 = 理性判定値 * 3 / 2
	ENDIF
	IF TALENT:ARG:倒錯的
		理性判定値 = 理性判定値 * 9 / 10
	ENDIF


	IF ABL:ARG:Ｖ感覚 > 10
		理性判定値 = 理性判定値 * 9 / 10
		SIF TALENT:ARG:Ｖ感度 == 1
			理性判定値 = 理性判定値 * 4 / 5
		SIF TALENT:ARG:子宮口性感
			理性判定値 = 理性判定値 * 4 / 5
		SIF TALENT:ARG:淫壺
			理性判定値 = 理性判定値 / 2
	ENDIF

	IF TALENT:ARG:傲嬌
		IF TALENT:ARG:恋慕
			理性判定値 = 理性判定値 * 2 / 3
		ELSE
			理性判定値 = 理性判定値 * 3 / 2
		ENDIF
	ENDIF
ENDIF


理性判定値 = MAX(理性判定値,0)
RETURNF 理性判定値

@NAMA_POISONING(ARG)
#DIM 理性判定値
#DIM 欲望判定値
#DIM 好意表示
VARSET LOCAL


PRINTFORMW %CALLNAME:ARG%の胸中

欲望判定値 = CHK_NAMA_POISONING(ARG) 
CALL SHOW_NAMAPOWER(欲望判定値)

WAIT
理性判定値 = CHK_NAMA_REASON(ARG)

CALL SHOW_REASONWALL(理性判定値)
IF TALENT:MASTER:謎之魅力
	CALL COLORMESSAGE(@"%CALLNAME:ARG%は、%CALLNAME:MASTER%の謎之魅力に理性がぐらつくのを感じた……",C_PINK,1,1)
	理性判定値 = 理性判定値 * 9 / 10
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:自尊心 == -1 || TALENT:ARG:献身的 == 1
	理性判定値  = 理性判定値 * 9 / 10
	CALL COLORMESSAGE(@"%CALLNAME:ARG%は、ここまで頼みこまれたらと思ってしまった……",C_PINK,1,1)
ELSEIF TALENT:ARG:自尊心 == 1
	理性判定値 = 理性判定値 * 7 / 5
	CALL COLORMESSAGE(@"気位の高い%CALLNAME:ARG%は、自分勝手な言動に腹を立てた……",C_PINK,1,1)
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF OTOKOGIRAI(ARG)
	理性判定値 = 理性判定値 * 3 / 2
	IF TALENT:ARG:性別嗜好 == 3
		CALL COLORMESSAGE(@"%CALLNAME:ARG%は他者一般に対して忌避感情を持っている……",C_PINK,1,1)
	ELSEIF TALENT:ARG:性別嗜好 == 1
		CALL COLORMESSAGE(@"%CALLNAME:ARG%は男性一般に対して忌避感情を持っている……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:倒錯的
	CALL COLORMESSAGE(@"%CALLNAME:ARG%は、後先を考えない性交に倒錯的な魅力を感じた……",C_PINK,1,1)
	理性判定値 = 理性判定値 * 9 / 10
	CALL SHOW_REASONWALL(理性判定値)
ENDIF


IF ABL:ARG:Ｖ感覚 > 10
	CALL COLORMESSAGE(@"%CALLNAME:ARG%の子宮が、例えようもなく疼いた……！",C_PINK,1,1)
	理性判定値 = 理性判定値 * 9 / 10
	SIF TALENT:ARG:Ｖ感度 == 1
		理性判定値 = 理性判定値 * 4 / 5
	SIF TALENT:ARG:子宮口性感
		理性判定値 = 理性判定値 * 4 / 5
	SIF TALENT:ARG:淫壺
		理性判定値 = 理性判定値 / 2
	CALL SHOW_REASONWALL(理性判定値)
ENDIF


IF TALENT:ARG:傲嬌
	IF TALENT:ARG:恋慕
		理性判定値 = 理性判定値 * 2 / 3
		CALL COLORMESSAGE(@"%CALLNAME:ARG%はデレた！",C_PINK,1,1)
	ELSE
		理性判定値 = 理性判定値 * 3 / 2
		CALL COLORMESSAGE(@"%CALLNAME:ARG%は反感を抱いた……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定値)
ENDIF

WAIT
IF CHK_NAMA_POISONING(ARG) > 理性判定値
	PRINTFORMW %CALLNAME:MASTER%からの問いに、%CALLNAME:ARG%の瞳が欲情に溶ける
	PRINTFORMW %CALLNAME:ARG%は、こくりと頷いた……
	CFLAG:ARG:子育て中解禁 = 1
	;一応EVENT31に設定。
	CALL KOJO_MESSAGE_SEND("EVENT",31,TARGET,1,0)
ELSE
	PRINTFORML	%CALLNAME:ARG%委婉地拒绝了
	CALL KOJO_MESSAGE_SEND("EVENT",31,TARGET,2,0)
	PRINTL [0]相手の意志を尊重する
	PRINTL [1]もうひと押し
	$INPUT2_LOOP
	INPUT
	IF RESULT == 0
		TFLAG:16 =2
		TFLAG:17 = TARGET
	ELSEIF RESULT == 1
	;口のうまさと相手次第では粘ればどうにかなる
		LOCAL = (4 - TALENT:ARG:応答 - TALENT:ARG:胆量 - TALENT:ARG:態度) * (ABL:MASTER:話術技能 + 2 * CFLAG:ARG:指出缺点)
		LOCAL:2 = 理性判定値 * (100 - LOCAL) / 200
		CALL SHOW_REASONWALL(LOCAL:2)
		IF CHK_NAMA_POISONING(ARG) > LOCAL:2
			PRINTFORMW %CALLNAME:MASTER%は舌先三寸でどうにか%CALLNAME:ARG%の欲望を引き出した
			PRINTFORMW %CALLNAME:ARG%は、こくりと頷いた……
			CFLAG:ARG:子育て中解禁 = 1
			CALL KOJO_MESSAGE_SEND("EVENT",31,TARGET,1,0)
		ELSE
			CALL KOJO_MESSAGE_SEND("EVENT",31,TARGET,3,0)
			PRINTFORMW %CALLNAME:ARG%に思いっきり怒られた…
			CFLAG:ARG:指出缺点 += 1
			CALL ENDUFUFU(ARG)
			CALL GET_ANGRY(ARG)
			MARK:ARG:反発刻印 = MIN(MARK:ARG:反発刻印 + 1, 3)
			TFLAG:106 = 0
			TFLAG:16 = 2
		ENDIF
	ELSE
		GOTO INPUT2_LOOP
	ENDIF
ENDIF

@SHOW_NAMAPOWER(欲望判定値, UNIT = 100)
#DIM 欲望判定値
#DIM 欲望表示
#DIM UNIT

PRINTFORM 生ハメしたさ:
欲望表示 = MAX(欲望判定値 / UNIT,0)
LOCALS:1 = %GETFONT()%
SETFONT "Times New Roman"
LOCAL = GETCOLOR()
PRINTFORM %UNICODE(0x2764) * MAX((欲望表示 / 10000 -1),0)%
RESETCOLOR
SETCOLOR 255,51,153
SIF 欲望表示 >=10000
	PRINTFORM %UNICODE(0x2764) * (9999)%%UNICODE(0x2764)%

PRINTFORM %UNICODE(0x2764) * (欲望表示 % 10000)%
SETCOLOR LOCAL

SIF 欲望判定値 % UNIT > (UNIT / 2)
	PRINTFORML %UNICODE(0x2764)%
SETFONT LOCALS:1
PRINTW