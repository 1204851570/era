@ENKAI_SETTING
;------------------------------------------------------------------------------------------------------
;宴会の開催判定
;------------------------------------------------------------------------------------------------------
;1ターンで飲む酒の量は経過時間 * 10 通常は30分なので300、宴会規模1200なら2時間程度の宴会となる
;主催者は宴会の準備と片づけの仕事をしなければならない
;一日に複数の宴会を開ける仕様ではないので注意（人が分散しちゃって寂しいし…）
;------------------------------------------------------------------------------------------------------
PRINTL  

;PRINTFORMW 宴会開催フラグ = {FLAG:宴会開催フラグ}

;開催判定は宴会フラグが立っていない状態でしか行われない（バグ防止）
IF FLAG:宴会開催FLAG == 0
	;メイン主催者が出禁になっているなら開催されない
	;宴会種別は98以上には設定しないこと
	;2 幻想郷年忘れ会　メイン主催者：なし　大晦日の一週間前に１００％発生する
	IF DAY:2 == 4 && DAY:3 >= 25 && DAY:3 <= 30
		;宴会名
		LOCALS = 幻想郷歳忘れ会
		;宴会種別
		FLAG:宴会開催FLAG = 2
		;宴会規模
		FLAG:31 = 6000
		;宴会会場
		FLAG:32 = 9
		;開始時間
		FLAG:33 = 900
		;開始日
		FLAG:34 = 31 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 0
		PRINTL  
		PRINTFORMW 忘年会のシーズンがやってきた
	;月見酒
	ELSEIF DAY:2 == 3 && DAY:3 >= 1 && DAY:3 < 15
		;宴会名
		LOCALS = 赏月酒
		;宴会種別
		FLAG:宴会開催FLAG = 3
		;宴会規模
		FLAG:31 = 2000 + RAND:1000
		;会場
		FLAG:32 = 409
		;開始時間
		FLAG:33 = 1140
		;開始日
		FLAG:34 = 15 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		IF MAIN_MAP == 4
			PRINTFORMW 永琳から赏月酒に誘われた
		ELSE
			PRINTFORMW 永遠亭から赏月酒に招待された
		ENDIF
	;収穫祭
	ELSEIF DAY:2 == 3 && DAY:3 >= 16 && DAY:3 < 27
		;宴会種別
		FLAG:宴会開催FLAG = 4
		;宴会規模
		FLAG:31 = 2000 + RAND:3000
		;会場
		FLAG:32 = 202
		;開始時間
		FLAG:33 = 540
		;開始日
		FLAG:34 = 27 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 0
		PRINTL  
		PRINTFORMW 今年も収穫祭の季節がやってきた
	; レミリアの気紛れなパーティ　メイン主催者：レミリア　自宅が紅魔館　土曜日に５０％で開催予告、実施日は日曜日
	ELSEIF CFLAG:16:出禁 == 0 && DAY%7 == 6 && MAIN_MAP == 3 && RAND:50 <= 49
		;宴会名
		LOCALS = 蕾米莉亜の気紛れなパーティ
		;宴会種別
		FLAG:宴会開催FLAG = 6
		;宴会規模
		FLAG:31 = 1500 + RAND:2500
		;宴会会場
		FLAG:32 = 324
		;開始時間
		FLAG:33 = 1140
		;開始日
		FLAG:34 = 1
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		PRINTFORMW %CALLNAME:16%はパーティをしたくなったようだ
	;SAMPLE 霊夢の気紛れ酒盛り　メイン主催者：霊夢　土曜日に５０％で開催予告、実施日は日曜日
	ELSEIF CFLAG:1:出禁 == 0 && DAY%7 == 6 && RAND:100 <= 49
		;宴会名
		LOCALS = 霊夢の気紛れ酒盛り
		;宴会種別
		FLAG:宴会開催FLAG = 1
		;宴会規模
		FLAG:31 = 1000 + RAND:1500
		;宴会会場
		IF RAND:5 <= 3
			FLAG:32 = 11
		ELSEIF RAND:4 == 0
			FLAG:32 = 2
		ELSEIF RAND:4 == 0
			FLAG:32 = 20
		ELSEIF RAND:4 == 0
			FLAG:32 = 21
		ELSE
			FLAG:32 = 218
		ENDIF
		;開始時間
		FLAG:33 = 1360 - (FLAG:31 / 10)
		;開始日
		FLAG:34 = 1
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		PRINTFORMW %CALLNAME:1%は宴会したくなったようだ
;	ELSEIF DAY:3 >= 8 && DAY:3 <= 10
;		;宴会名
;		LOCALS = 夜通し読経ライブ
;		;宴会種別
;		FLAG:宴会開催フラグ = 5
;		;宴会規模
;		FLAG:31 = 2000 + RAND:30 * 40
;		;宴会会場
;		FLAG:32 = 104
;		;開始時間
;		FLAG:33 = 1440 + 240 - (FLAG:31 /10) ;朝４時まで宴会する
;		;開始日
;		FLAG:34 = 10 - DAY:3
;		;現在の参加人数をリセット
;		FLAG:35 = 0
;		;雨天中止
;		FLAG:36 = 0
;		PRINTL  
;		PRINTFORMW 夜通し読経ライブの時期が近づいてきた
	ENDIF
ENDIF
CALL SET_ENKAI_LOCATIONDISPLAY
SIF FLAG:宴会開催FLAG
	CALL 宴会概要
PRINTL  

@ENKAI_ENTRY
#DIM DYNAMIC HOST_LIST  , 人物数量上限;主催者参加
#DIM DYNAMIC GUEST_LIST , 人物数量上限;客参加
#DIM DYNAMIC NG_LIST    , 人物数量上限;来ない
;---   宴会の参加者決定   -----------------------------------------------------------------------------
;CFLAG:宴会参加 0=不参加 1=参加 2=主催者 3=脱落者 4=飲まないけど参加
;------------------------------------------------------------------------------------------------------
;フラグ初期化
CVARSET CFLAG, GETNUM(CFLAG, "宴会参加"), 0, 1
SELECTCASE FLAG:宴会開催FLAG
	;2 幻想郷歳忘れ会
	CASE 2
		;1:霊夢, 2:るーこと, 11:魔理沙
		HOST_LIST  = 1, 2, 11
		;10:萃香
		GUEST_LIST = 10
		;正邪は参加しない
		NG_LIST    = 97
		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:宴会参加
				SIF CFLAG:LOCAL:宴会参加 < 0
					CFLAG:LOCAL:宴会参加 = 0
				CONTINUE
			ENDIF
			;他のキャラはランダム参加
			SIF RAND:5 == 0
				CFLAG:LOCAL:宴会参加 = 1
		NEXT
	;1 霊夢の気紛れ酒盛り
	CASE 1
		SELECTCASE FLAG:宴会差し入れ
			CASE 51
				FLAG:31 += RAND:1000
			CASE 52
				FLAG:31 += RAND:2000
		ENDSELECT
		;1:霊夢, 2:るーこと, 11:魔理沙
		HOST_LIST  = 1, 2, 11
		;10:萃香
		GUEST_LIST = 10
		;正邪は参加しない
		NG_LIST    = 97
		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
		IF FLAG:31 >= 2500
		;規模が大きいと参加確率大
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:宴会参加
					SIF CFLAG:LOCAL:宴会参加 < 0
						CFLAG:LOCAL:宴会参加 = 0
					CONTINUE
				ENDIF
				;他のキャラはランダム参加
				SIF RAND:7 == 0
					CFLAG:LOCAL:宴会参加 = 1
			NEXT
		ELSE
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:宴会参加
					SIF CFLAG:LOCAL:宴会参加 < 0
						CFLAG:LOCAL:宴会参加 = 0
					CONTINUE
				ENDIF
				;他のキャラはランダム参加
				SIF RAND:14 == 0
					CFLAG:LOCAL:宴会参加 = 1
			NEXT
		ENDIF
	;月見酒
	CASE 3
		;52:うどん, 53てゐ, 72:えーりん　75:メディスンも手伝う
		HOST_LIST  = 52, 53, 72, 75
		;62:ぐや
		GUEST_LIST = 62
		;正邪は参加しない
		NG_LIST    = 97
		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:宴会参加
				SIF CFLAG:LOCAL:宴会参加 < 0
					CFLAG:LOCAL:宴会参加 = 0
				CONTINUE
			ENDIF
			;他のキャラはランダム参加だが、竹林方面に住んでると確率が上がる
			IF CFLAG:LOCAL:自宅位置 / 100 == 4 && !RAND:3
				CFLAG:LOCAL:宴会参加 = 1
			ELSEIF !RAND:11
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
		NEXT
	;収穫祭
	CASE 4
		;HOST_LISTは住居依存分岐なので無しで
		;秋姉妹は必ず来る
		GUEST_LIST = 77, 78
		;正邪は参加しない
		NG_LIST    = 97
		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:宴会参加
				SIF CFLAG:LOCAL:宴会参加 < 0
					CFLAG:LOCAL:宴会参加 = 0
				CONTINUE
			ENDIF
			;人里在住のキャラは働く
			IF CFLAG:LOCAL:自宅位置 / 100 == 2
				CFLAG:LOCAL:宴会参加 = 2
			ELSEIF !RAND:12
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
		NEXT
	;読経ライブ
;	CASE 5
;		;命蓮寺メンバーは働く
;		HOST_LIST = 39, 40, 41, 55, 85, 86, 87, 88
;		;GUEST_LISTなし
;		;博麗神社組はこない
;		;正邪もこない
;		NG_LIST   = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 97
;		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
;		FOR LOCAL, 1, CHARANUM
;			IF CFLAG:LOCAL:宴会参加
;				SIF CFLAG:LOCAL:宴会参加 < 0
;					CFLAG:LOCAL:宴会参加 = 0
;				CONTINUE
;			ENDIF
;			;他は酒を飲まない
;			SIF RAND:15 == 0
;				CFLAG:LOCAL:宴会参加 = 9
;		NEXT
	;6 レミリアの気紛れなパーティ
	CASE 6
		;15:咲夜, 58:美鈴, 59:小悪魔は働く
		HOST_LIST  = 15, 58, 59
		;16:レミリア, 50:フランは働かない
		GUEST_LIST = 16, 50
		;54:パチュリー, 97:正邪は参加しない
		NG_LIST    = 54, 97
		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
		IF FLAG:31 >= 2500
		;規模が大きいと参加確率大
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:宴会参加
					SIF CFLAG:LOCAL:宴会参加 < 0
						CFLAG:LOCAL:宴会参加 = 0
					CONTINUE
				ENDIF
				;霧の湖方面に住んでると確率が上がる
				SIF CFLAG:LOCAL:自宅位置 / 100 == 3 && !RAND:3
					CFLAG:LOCAL:宴会参加 = 1
				;他のキャラはランダム参加
				SIF RAND:7 == 0
					CFLAG:LOCAL:宴会参加 = 1
			NEXT
		ELSE
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:宴会参加
					SIF CFLAG:LOCAL:宴会参加 < 0
						CFLAG:LOCAL:宴会参加 = 0
					CONTINUE
				ENDIF
				;霧の湖方面に住んでると確率が上がる
				SIF CFLAG:LOCAL:自宅位置 / 100 == 3 && !RAND:3
					CFLAG:LOCAL:宴会参加 = 1
				;他のキャラはランダム参加
				SIF RAND:11 == 0
					CFLAG:LOCAL:宴会参加 = 1
			NEXT
		ENDIF
ENDSELECT


;リストを利用した宴会キャラのプリセット関数
@ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
#DIM REF HOST_LIST  ;主催者参加
#DIM REF GUEST_LIST ;客参加
#DIM REF NG_LIST    ;来ない
#DIM L_NUM
#DIM L_ID
L_NUM = FINDELEMENT(HOST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(HOST_LIST:L_ID):宴会参加 = 2
	NEXT
ENDIF
L_NUM = FINDELEMENT(GUEST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(GUEST_LIST:L_ID):宴会参加 = 1
	NEXT
ENDIF
;設定したことを明確化するために-1にします
L_NUM = FINDELEMENT(NG_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(NG_LIST:L_ID):宴会参加 = -1
	NEXT
ENDIF



@ENKAI_WORK(ARG)
;宴会の行動内容を入力する
;NOSAMES:すべての引数が異なると1, 1つでも一致していると0を返す
IF FLAG:宴会開催FLAG > 0 && (NOSAMES(CFLAG:ARG:職種, 48, 49, 50, 51) || CFLAG:ARG:行動 == 0)
	;宴会の時間になったら準備仕事の量に応じて主催者が動き始める
	IF TIME >= FLAG:開始時間 - (FLAG:宴会規模 / 30) - 15 && TIME < FLAG:33 && CFLAG:ARG:宴会参加 == 2
		CALL ENKAI_PREPARE(ARG)
		RETURN 1
	;遊びに来るだけの奴は時間になったら遊びに来る
	ELSEIF DAY == FLAG:34 && TIME >= FLAG:33 && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	;とっくに始まってる場合
	ELSEIF FLAG:宴会開催FLAG >= 98 && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	ENDIF
ELSEIF FLAG:宴会開催FLAG > 0
	RETURN 1
ENDIF
RETURN 0


@ENKAI_PREPARE(ARG)
;主催者の準備仕事
IF CFLAG:ARG:職種 != 宴会準備
	MAXBASE:ARG:工作量 = FLAG:31 / 3
	;最大値、2400
	SIF MAXBASE:ARG:工作量 >= 2400
		MAXBASE:ARG:工作量 = 2400
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	CFLAG:ARG:職種 = 宴会準備
	CFLAG:ARG:職場 = TFLAG:宴会場
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行動 = 5
ENDIF
RETURN 1

@ENKAI_START(ARG)
;それぞれが飲むペースを変えるので、宴の規模に応じて仕事量が変化
IF CFLAG:ARG:職種 != ENKAI_SANKA && TCVAR:ARG:烂醉 == 0
	MAXBASE:ARG:工作量 = FLAG:31
	;最大値、通常7200(半日)・酒呑み神なら制限なし
	SIF MAXBASE:ARG:工作量 >= 7200 && TALENT:ARG:酒耐性 != 3
		MAXBASE:ARG:工作量 = 7200
	MAXBASE:ARG:工作量 += RAND:500
	IF TALENT:ARG:酒耐性 == -1
		TIMES MAXBASE:ARG:工作量, 0.75
	ELSEIF TALENT:ARG:酒耐性 == 1
		TIMES MAXBASE:ARG:工作量, 1.25
	ELSEIF TALENT:ARG:酒耐性 == 2
		TIMES MAXBASE:ARG:工作量, 1.5
	ELSEIF TALENT:ARG:酒耐性 == 3
		TIMES MAXBASE:ARG:工作量, 2.0
	ENDIF
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	;仕事内容・宴会に参加
	CFLAG:ARG:職種 = ENKAI_SANKA
	CFLAG:ARG:職場 = TFLAG:宴会場
	CFLAG:ARG:現在位置 = TFLAG:宴会場
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行動 = 5
	IF CFLAG:ARG:宴会参加 == 2
		;主催者は片付け予定をセット(仕事量は最大1200)
		CFLAG:ARG:片付け工作量 = MIN(FLAG:宴会規模 / 6, 1200)
		CFLAG:ARG:片付け場所 = FLAG:宴会会場
	ENDIF
ENDIF

RETURN 1


@ENKAI_KATAZUKE(ARG)
;主催者の片づけ仕事
MAXBASE:ARG:工作量 = CFLAG:ARG:片付け工作量
BASE:ARG:工作量 = MAXBASE:ARG:工作量
;仕事内容・宴会の片づけ
CFLAG:ARG:職種 = 50
CFLAG:ARG:職場 = GET_MAP_REPLACEMENT(CFLAG:ARG:片付け場所)
TCVAR:ARG:没办法帮忙 = 0
CFLAG:ARG:行動 = 5
RETURN 1


@ENKAI_DRUNK(ARG)
;酔いつぶれるラインを超えていたら戻る
IF BASE:ARG:酒気 > (MAXBASE:ARG:酒気 / 10) * 8
	RETURN 1
ENDIF

;酒を飲まないなら酔わない 
IF TALENT:ARG:酒耐性 == -2 || CFLAG:ARG:妊娠自覚状態 == 1
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:宴会参加 = 4
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG%はもう十分楽しんだようです
	ENDIF
ELSE
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:宴会参加 = 3
;	PRINTFORMW %CALLNAME:ARG%はほろ酔い気分のようです
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG%はもう十分楽しんだようです
	ENDIF
ENDIF
RETURN 1


@ENKAI_STOP
;悪天候などによる中止
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:職種 = 0
	MAXBASE:LOCAL:工作量 = 0
	BASE:LOCAL:工作量 = 0
	CFLAG:LOCAL:宴会参加 = 0
	CFLAG:LOCAL:行動 = 0
NEXT
FLAG:宴会開催FLAG = 0
FLAG:31 = 0
FLAG:32 = 0
TFLAG:宴会場 = 0
FLAG:33 = 0
FLAG:34 = 0
FLAG:35 = 0
FLAG:36 = 0
FLAG:宴会差し入れ = 0
;宴会会場表示 = 
RETURN 0

@宴会予定表示

PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}から%宴会場表示()%で%宴会名表示()%


@SET_ENKAI_LOCATIONDISPLAY
宴会会場表示 = 

@宴会場表示
#FUNCTIONS
LOCALS = 宴会場表示エラー
IF FLAG:宴会開催FLAG
	IF IN_HOME(FLAG:32)
		LOCALS = %NAME_FROM_PLACE(FLAG:32)%
	ELSE
		LOCALS = %GET_MAPNAME(FLAG:32/100)%の%GET_PLACENAME(FLAG:32)%
	ENDIF
ENDIF
RETURNF LOCALS

@宴会名表示
#FUNCTIONS
SELECTCASE FLAG:宴会開催FLAG
	CASE 1
		LOCALS = 霊夢の気紛れ酒盛り
	CASE 2
		LOCALS = 幻想郷歳忘れ会
	CASE 3
		LOCALS = 赏月酒
	CASE 4
		LOCALS = 収穫祭
	CASE 5
		LOCALS = 夜通し読経ライブ
	CASE 6
		LOCALS = 蕾米莉亜の気紛れなパーティ
ENDSELECT
RETURNF LOCALS

@宴会概要
IF FLAG:34 == 1
	PRINT 明日、
ELSE
	PRINTFORM {FLAG:34}日後、
ENDIF

PRINTFORML {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}から%宴会場表示()%にて、「%宴会名表示()%」を開くそうだ

PRINT 話の様子だと
IF FLAG:31 >= 5000
	PRINTW ……これは長丁場になりそうだ
ELSEIF FLAG:31 >= 2500
	PRINTW 、佳肴をたっぷり用意しているらしい
ELSEIF FLAG:31 >= 1500
	PRINTW 、親睦を深めるには丁度よさそうだ
ELSE
	PRINTW 、稍微した集まりを予定しているようだ
ENDIF
IF FLAG:34 == 1
	PRINTFORMW 急な催しではあるが、参加してみようか……
ELSE
	PRINTFORMW 参加するなら予定を空けておこう……
ENDIF
SIF FLAG:36 > 0
	PRINTFORMW なお、雨天中止とのことだ……
PRINTL  
FLAG:34 += DAY


@宴会参加人数再計算
;宴会の参加人数をセット
FLAG:参加人数 = 0
FOR LOCAL,0,CHARANUM
	IF (CFLAG:LOCAL:宴会参加 == 1 || CFLAG:LOCAL:宴会参加 == 2) && !TCVAR:LOCAL:烂醉
		SIF CFLAG:LOCAL:現在位置 == TFLAG:宴会場
			FLAG:参加人数 ++
	ENDIF
NEXT
