;オプション設定処理
@OPTION
PRINTL 設定項目を選択してください
PRINTL [0] - 文本設定
PRINTL [1] - 指令过滤器
PRINT [2] - 消耗品の自動購入 
	PRINTFORML \@ FLAG:1001 ? ON # OFF \@
PRINT [3] - 汚れ度表示 
	PRINTFORML \@ FLAG:1002 ? OFF # ON \@
;PRINT [4] - 時間停止中の信頼度低下
;	PRINTFORML \@ FLAG:1011 ? ON # OFF \@
PRINT [5] - 移動時停下脚步の有無 
	SIF FLAG:停下脚步 == 0
		PRINTL ON (無条件で止步)
	SIF FLAG:停下脚步 == 1
		PRINTL ON (口上が存在する場合のみ)
	SIF FLAG:停下脚步 == 2
		PRINTL OFF (立ち止まらない)
PRINT [6] - 能力を比較して被逆推なくなる判定 
	PRINTFORML \@ FLAG:推倒 ? ON # OFF \@
PRINT [7] - 自動時止め移動 
	IF FLAG:瞬間移動 == 1
		PRINTL ON(外出先の散步時のみ)
	ELSEIF FLAG:瞬間移動 == 2
		PRINTL ON(拠点内の移動時のみ)
	ELSEIF FLAG:瞬間移動 == 3
		PRINTL ON(外出先/拠点内両方)
	ELSE
		PRINTL OFF
	ENDIF
PRINT [8] - 住宿事件 
	PRINTFORML \@ FLAG:住宿事件 ? OFF # ON \@
PRINTL [10] - 登場人物的切换
PRINT [11] - 警告牌使用 
	PRINTFORML \@ FLAG:警告牌使用 ? ON # OFF \@
PRINT [12] - 一部キャラの長筒襪着用 
	PRINTFORML \@ FLAG:長筒襪着用 ? ON # OFF \@
PRINT [13] - 宴会の発生 
	PRINTFORML \@ FLAG:宴会的有無 ? ON # OFF \@
PRINT [14] - 好感度０のキャラは来訪しないモード 
	SIF FLAG:交流来訪模式 == 0
		PRINTL ON (来訪する)
	SIF FLAG:交流来訪模式 == 1
		PRINTL ON (好感度0は来訪しない)
	SIF FLAG:交流来訪模式 == 2
		PRINTL OFF (来訪しない)
PRINT [15] - 妊娠の有無※基本OFF。ONにする場合は何かあったら自己責任で　　
	PRINTFORML \@ FLAG:妊娠开关 ? ON # OFF \@
PRINT [16] - 脱衣選択 
SELECTCASE FLAG:脱衣選択
	CASE 0
		PRINTL [毎回選択]
	CASE 1
		PRINTL [全裸]
	CASE 2
		PRINTL [半脱]
ENDSELECT
PRINT [17] - 時間停止脱衣時パンツ処理 
SELECTCASE FLAG:パンツ泥棒
	CASE 0
		PRINTL [毎回選択]
	CASE 1
		PRINTL [常にくすねる]
	CASE 2
		PRINTL [常に脱がすだけ]
ENDSELECT
PRINT [18] - 口上色設定 
	PRINTFORML \@ FLAG:口上色 ? ON # OFF \@
PRINTL [19] - 胸部関連Look表示設定
PRINT [20] - 能力表示のボタン表記 
	PRINTFORML \@ FLAG:能力表記 ? [数字表記] # [記号表記] \@
PRINT [21] - counterの地の文を素質で変化させる 
	PRINTFORML \@ FLAG:counter地文控制 ? ON # OFF \@
PRINT [22] - エッチな下着を追加する 
	PRINTFORML \@ FLAG:セクシーパンツ追加 ? ON # OFF \@
PRINT [23] - 炮友系陥落から恋慕系陥落への変化
	PRINTFORML \@ FLAG:陥落路線変化 ? する # しない \@
PRINT [24] - 相手から告白
	PRINTFORML \@ FLAG:告白禁止 ? してこない # する \@
PRINT [25] - ターン毎のABLUP省略 
	PRINTFORML \@ FLAG:ABLUP省略 ? ON # OFF \@
PRINTFORML [26]\@ FLAG:画像表示 ? ┬ # %" "%- \@ AAの表示を画像に変更する \@ FLAG:画像表示 ? ON # OFF \@
SIF FLAG:画像表示
	PRINTFORML [27]└ 画像詳細設定
PRINT [28] - イベントによる無知喪失 
	PRINTFORML \@ FLAG:無知自動喪失禁止 ? しない # する \@
PRINT [29] - 你への好意・理性之壁を短縮表示 
	PRINTFORML \@ FLAG:好意理性短縮 ? ON # OFF \@
PRINT [30] - 兒童の描写 
	PRINTFORML \@ FLAG:兒童描写 ? ON # OFF \@
PRINT [31] - 兒童の性別
SELECTCASE FLAG:兒童の性別
	CASE 0
		PRINTL [ランダム]
	CASE 1
		PRINTL [女の子]
	CASE 2
		PRINTL [男の子]
ENDSELECT
PRINTL [32] - 簡易過濾的總設定
PRINTL [33] - 料理操作的設定
SIF DAY > 120
	PRINTL [99] 败者・食尘
PRINTL [100]-返回
$INPUT_LOOP
INPUT
IF RESULT == 0
	CALL OPTION_TEXT_SETTING
ELSEIF RESULT == 1
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 2
	FLAG:1001 = !FLAG:1001
ELSEIF RESULT == 3
	CALL OPTION_HOUSE_INFO
ELSEIF RESULT == 5
	IF FLAG:停下脚步 == 0
		FLAG:停下脚步 = 1
	ELSEIF FLAG:停下脚步 == 1
		FLAG:停下脚步 = 2
	ELSEIF FLAG:停下脚步 == 2
		FLAG:停下脚步 = 0
	ENDIF
ELSEIF RESULT == 6
	FLAG:推倒 = !FLAG:推倒
ELSEIF RESULT == 7
	FLAG:瞬間移動 ++
	SIF FLAG:瞬間移動 >= 4
		FLAG:瞬間移動 = 0
ELSEIF RESULT == 8
	FLAG:住宿事件 = !FLAG:住宿事件
ELSEIF RESULT == 16
	IF FLAG:脱衣選択 < 2
		FLAG:脱衣選択 ++
	ELSE
		FLAG:脱衣選択 = 0
	ENDIF
ELSEIF RESULT == 17
	IF FLAG:パンツ泥棒 < 2
		FLAG:パンツ泥棒 ++
	ELSE
		FLAG:パンツ泥棒 = 0
	ENDIF
ELSEIF RESULT == 10
	CALL 登場人物的切换
ELSEIF RESULT == 11
	FLAG:警告牌使用 = !FLAG:警告牌使用
ELSEIF RESULT == 12
	FLAG:長筒襪着用 = !FLAG:長筒襪着用
ELSEIF RESULT == 13
	FLAG:宴会的有無 = !FLAG:宴会的有無
ELSEIF RESULT == 14
	IF FLAG:交流来訪模式 == 0
		FLAG:交流来訪模式 = 1
	ELSEIF FLAG:交流来訪模式 == 1
		FLAG:交流来訪模式 = 2
	ELSEIF FLAG:交流来訪模式 == 2
		FLAG:交流来訪模式 = 0
	ENDIF
ELSEIF RESULT == 15
	FLAG:妊娠开关 = !FLAG:妊娠开关
ELSEIF RESULT == 18
	FLAG:口上色 = !FLAG:口上色
ELSEIF RESULT == 19
	CALL 胸部関連Look表示設定
ELSEIF RESULT == 20
	FLAG:能力表記 = !FLAG:能力表記
ELSEIF RESULT == 21
	FLAG:counter地文控制 = !FLAG:counter地文控制
ELSEIF RESULT == 22
	FLAG:セクシーパンツ追加 = !FLAG:セクシーパンツ追加
	IF FLAG:セクシーパンツ追加 == 0
		PRINTFORMW 幻想郷に訪れたエッチな下着ブームが終息しました
		PRINTL 
		PRINTFORML 所持しているエッチな下着を削除しますか？
		PRINTFORML 削除対象：超短胖次、兜襠布、Gストリング、透明胖次、開放式胖次、創口貼、襯褲（開襠）
		PRINTL 
		PRINTL [0] もう要らない
		PRINTL [1] 大事に持っとく
		INPUT
		IF RESULT == 0
			CALL DELETE_SEXYPANTS
		ELSE
			RESTART
		ENDIF
	ELSE
		PRINTL 
		PRINTFORMW 幻想郷にエッチな下着が流通し始めました
		PRINTL 
	ENDIF
ELSEIF RESULT == 23
	FLAG:陥落路線変化 = !FLAG:陥落路線変化
ELSEIF RESULT == 24
	FLAG:告白禁止 = !FLAG:告白禁止
ELSEIF RESULT == 25
	FLAG:ABLUP省略 = !FLAG:ABLUP省略
ELSEIF RESULT == 26
	FLAG:画像表示 = !FLAG:画像表示
ELSEIF RESULT == 27 && FLAG:画像表示
	CALL OPTION_画像表示詳細設定
ELSEIF RESULT == 28
	FLAG:無知自動喪失禁止 = !FLAG:無知自動喪失禁止
ELSEIF RESULT == 29
	FLAG:好意理性短縮 = !FLAG:好意理性短縮
ELSEIF RESULT == 30
	FLAG:兒童描写 = !FLAG:兒童描写
ELSEIF RESULT == 31
	IF FLAG:兒童の性別 < 2
		FLAG:兒童の性別 ++
	ELSE
		FLAG:兒童の性別 = 0
	ENDIF
ELSEIF RESULT == 32
	CALL 簡易过滤器一括設定
ELSEIF RESULT == 33
	CALL SET_OPTION_COOKING
ELSEIF RESULT == 99
	IF DAY > 120
		PRINTFORML 時間を吹き飛ばして一日目に戻りますか？
		PRINTL 
		PRINTL [0]「スイッチ」を押させるなーーッ
		PRINTL [1] いいや！限界だ押すね！今だッ！
		INPUT
		IF RESULT == 1
			CALL 強行开始新的游戏
		ELSE
;			PRINTW 「スイッチ」を押すのは止められなかったが、幸運にもオプション選択までしか戻らなかった。
			RESTART
		ENDIF
	ENDIF
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART
;テキスト関連
@OPTION_TEXT_SETTING
PRINTL 文本の設定をしてください
PRINT 現在的設定：
IF FLAG:6 == 1 && FLAG:7 == 2
	PRINTL 【口上文本（常に表示）＋情景文本】
ELSEIF FLAG:6 == 1 && FLAG:7 == -1
	PRINTL 【情景文本のみ】
ELSEIF FLAG:6 == 0 && FLAG:7 == -1
	PRINTL 【文本を表示しない】
ENDIF
PRINTL 　
PRINTL [0] - 口上文本＋情景文本
PRINTL [1] - 情景文本のみ
PRINTL [2] - 文本を表示しない
PRINTL [100]-返回
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0
	PRINTL 【口上文本＋情景文本】に設定しました
	FLAG:6 = 1
	FLAG:7 = 2
ELSEIF RESULT == 1
	PRINTL 【情景文本のみ】に設定しました
	FLAG:6 = 1
	FLAG:7 = -1
ELSEIF RESULT == 2
	PRINTL 【文本を表示しない】に設定しました
	FLAG:6 = 0
	FLAG:7 = -1
ELSE
	GOTO INPUT_LOOP
ENDIF



@OPTION_HOUSE_INFO
PRINTL 垃圾山を見なかった事にできます
PRINTL [0] 情報表示ON [1] - OFF
INPUT
IF RESULT == 0
	FLAG:1002 = 0
ELSE
	FLAG:1002 = 1
ENDIF

;@OPTION_TIME
;PRINTL 時間停止中の信頼度低下をオンにします
;PRINTL [0] OFF [1] - ON
;INPUT
;IF RESULT == 0
;	
;ELSE
;	
;ENDIF



@登場人物的切换
PRINTL 　指定したキャラを登場不会うにします。（"*"は口上のあるキャラ）
DRAWLINE
FOR LOCAL,1,CHARANUM
	SIF TALENT:LOCAL:路人
		CONTINUE
	SIF CFLAG:LOCAL:出禁
		SETCOLOR C_D_RED
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	PRINTFORMLC [{LOCAL, 3}] \@ RESULT ? * # \@%CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTFORML [999] 設定終了　出禁キャラ：{FLAG:出禁人数}人　[998]リセット
$LOOP
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM
	IF CFLAG:RESULT:出禁 && FLAG:扮演 != RESULT
		CFLAG:RESULT:出禁 = 0
		SIF CFLAG:RESULT:現在位置 == CFLAG:RESULT:初始位置 && CFLAG:RESULT:睡眠
			LOCK:(CFLAG:RESULT:初始位置) = 1
		FLAG:出禁人数 --
	ELSEIF TALENT:RESULT:路人
		PRINTFORMW このキャラは出禁解除できません
		GOTO LOOP
	ELSEIF FLAG:扮演 == RESULT
		IF CFLAG:(FLAG:扮演):出禁 == 1
			PRINTFORML 現在そのキャラになりきっているため、%CALLNAME:RESULT%が二人いることになってしまいますが…
			PRINTL それでも登場させますか？
			PRINTL [0]はい [1]いいえ
			INPUT
			IF RESULT == 0
				CFLAG:(FLAG:扮演):出禁 = 0
				SIF CFLAG:(FLAG:扮演):現在位置 == CFLAG:(FLAG:扮演):初始位置 && CFLAG:(FLAG:扮演):睡眠
					LOCK:(CFLAG:(FLAG:扮演):初始位置) = 1
				FLAG:出禁人数 --
				PRINTFORMW %CALLNAME:(FLAG:扮演)%を登場させます
			ELSE
				GOTO LOOP
			ENDIF
		ELSE
			CFLAG:(FLAG:扮演):出禁 = 1
			SIF LOCK:(CFLAG:(FLAG:扮演):初始位置) && CFLAG:(FLAG:扮演):初始位置 < 100
				LOCK:(CFLAG:(FLAG:扮演):初始位置) = 0
			FLAG:出禁人数 ++
		ENDIF
	ELSE
		CFLAG:RESULT:出禁 = 1
		SIF LOCK:(CFLAG:RESULT:初始位置) && CFLAG:RESULT:初始位置 < 100
			LOCK:(CFLAG:RESULT:初始位置) = 0
		FLAG:出禁人数 ++
	ENDIF
	RESTART
ELSEIF RESULT == 999
	PRINTW 設定終了しました。
ELSEIF RESULT == 998
	FOR LOCAL,1,CHARANUM
		SIF FLAG:扮演 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:路人
			CONTINUE
		CFLAG:LOCAL:出禁 = 0
	NEXT
	FLAG:出禁人数 = 0
	RESTART
ELSE
	GOTO LOOP
ENDIF

@胸部関連Look表示設定
PRINTL 文本の設定をしてください
PRINT 現在的設定：
IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳首 == 1
	PRINTL 【Look時描写胸部固定】
	PRINTL （毎回変更せず固定します）
ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳首 == 0
	PRINTL 【Look時描写乳房固定】
	PRINTL （毎回乳首描写が変わり、乳房描写は固定します）
ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳首 == 1
	PRINTL 【Look時描写乳首固定】
	PRINTL （毎回乳房描写が変わり、乳首描写は固定します）
ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳首 == 0
	PRINTL 【Look時描写胸部ランダム】
	PRINTL （毎回胸部描写が変わります）
ENDIF
PRINTL 　
PRINTL [0] - Look時描写胸部関連固定
PRINTL [1] - Look時描写乳房固定
PRINTL [2] - Look時描写乳首固定
PRINTL [3] - Look時描写胸部関連ランダム
PRINTL [999]-返回
$INPUT_LOOP
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 0
	PRINTW 【Look時描写胸部固定】に設定しました
	FLAG:Look表示用乳房 = 1
	FLAG:Look表示用乳首 = 1
ELSEIF RESULT == 1
	PRINTW 【Look時描写乳房固定】に設定しました
	FLAG:Look表示用乳房 = 1
	FLAG:Look表示用乳首 = 0
ELSEIF RESULT == 2
	PRINTW 【Look時描写乳首固定】に設定しました
	FLAG:Look表示用乳房 = 0
	FLAG:Look表示用乳首 = 1
ELSEIF RESULT == 3
	PRINTW 【Look時描写胸部ランダム】に設定しました
	FLAG:Look表示用乳房 = 0
	FLAG:Look表示用乳首 = 0
ELSE
	GOTO INPUT_LOOP
ENDIF



;簡易フィルタ制御関数
;仕様上の問題として、設定項目が増えすぎると設計をしなおす必要がある
@簡易过滤器一括設定
#DIM OP_CFLAG_Ｖ過濾器
#DIM OP_CFLAG_Ａ過濾器
#DIM OP_CFLAG_ＳＭ過濾器
#DIM OP_CFLAG_奉仕過濾器
#DIM OP_CFLAG_性騷擾過濾器
#DIM OP_SETTING
IF !LOCAL
	OP_CFLAG_Ｖ過濾器       = GETNUM(CFLAG, "Ｖ過濾器")
	OP_CFLAG_Ａ過濾器       = GETNUM(CFLAG, "Ａ過濾器")
	OP_CFLAG_ＳＭ過濾器     = GETNUM(CFLAG, "ＳＭ過濾器")
	OP_CFLAG_奉仕過濾器     = GETNUM(CFLAG, "奉仕過濾器")
	OP_CFLAG_性騷擾過濾器 = GETNUM(CFLAG, "性騷擾過濾器")
	LOCAL ++
ENDIF
PRINTFORML ～簡易过滤器設定～
PRINTFORML ※個別の指令过滤器はこれとは別に設定されています
PRINTFORML 
PRINTFORML [29] 全キャラの全ての簡易过滤器オフ
PRINTFORML [28] 全角色の[Ｖ]を開放
PRINTFORML [27] 全角色の[Ａ]を開放
PRINTFORML [26] 全角色の[ＳＭ]を開放
PRINTFORML [25] 全角色の[奉仕]を開放
PRINTFORML [24] 全角色の[性騷擾]を開放
PRINTFORML [23] [家事]を開放
PRINTFORML 
PRINTFORML [19] 全キャラの全ての簡易过滤器オン
PRINTFORML [18] 全角色の[Ｖ]を制限
PRINTFORML [17] 全角色の[Ａ]を制限
PRINTFORML [16] 全角色の[ＳＭ]を制限
PRINTFORML [15] 全角色の[奉仕]を制限
PRINTFORML [14] 全角色の[性騷擾]を制限
PRINTFORML [13] [家事]を制限
PRINTFORML 
PRINTFORML [99] 設定終了
DO
	INPUT
	OP_SETTING = (RESULT / 10) - 1
	;20で1、10で0
	SELECTCASE RESULT
	CASE 29, 19
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全キャラの全ての簡易过滤器を＜オフ＞にしました
		ELSE
			PRINTFORMW 全キャラの全ての簡易过滤器を＜オン＞にしました
		ENDIF
	CASE 28, 18
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色の[Ｖ]指令を＜開放＞します
		ELSE
			PRINTFORMW 全角色の[Ｖ]指令を＜制限＞します
		ENDIF
	CASE 27, 17
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色の[Ａ]指令を＜開放＞します
		ELSE
			PRINTFORMW 全角色の[Ａ]指令を＜制限＞します
		ENDIF
	CASE 26, 16
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色の[ＳＭ]指令を＜開放＞します
		ELSE
			PRINTFORMW 全角色の[ＳＭ]指令を＜制限＞します
		ENDIF
	CASE 25, 15
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色の[奉仕]指令を＜開放＞します
		ELSE
			PRINTFORMW 全角色の[奉仕]指令を＜制限＞します
		ENDIF
	CASE 24, 14
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色の[性騷擾]指令を＜開放＞します
		ELSE
			PRINTFORMW 全角色の[性騷擾]指令を＜制限＞します
		ENDIF
	CASE 23, 13
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 家事指令を＜開放＞します
		ELSE
			PRINTFORMW 家事指令を＜制限＞します
		ENDIF
	CASE 99
		PRINTFORMW 簡易过滤器の設定を終了します
		RETURN
	CASEELSE
		PRINTFORML 無効な入力です
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;---------------------------------------------------------------
;	画像詳細設定
;	設定が増えたので新設
;---------------------------------------------------------------
@OPTION_画像表示詳細設定
PRINTL
PRINTFORML [*]┬ 現在的\@ FLAG:立絵種類 ? 顔絵 # 立絵 \@の表示人数 {FLAG:画像表示}人
PRINTFORML [1]├ 増やす
PRINTFORML [2]└ 減らす
PRINTFORML [3] - キャラ画像のタイプ \@ FLAG:立絵種類 ? [顔] # [立絵] \@
PRINTFORML [4]\@ FLAG:挿絵表示 ? ┬ # %" "%- \@ 挿絵を表示する \@ FLAG:挿絵表示 ? ON # OFF \@
IF FLAG:挿絵表示
	PRINT [5]└ 射精画像の種類
	SELECTCASE FLAG:射精画像種類
		CASE 0
			PRINTL  [断面＋効果音]
		CASE 1
			PRINTL  [断面のみ]
		CASE 2
			PRINTL  [効果音のみ]
	ENDSELECT
ENDIF
PRINTFORML [6]\@ FLAG:アニメーション ? ┬ # %" "%- \@アニメーション設定 \@ FLAG:アニメーション ? 繰り返し回数 {FLAG:アニメーション}回 # OFF \@
SIF FLAG:アニメーション
	;PRINTFORML [7]└アニメ個別設定 挿入：\@FLAG:挿入アニメフィルタ & アニメフィルタ_挿入 ? [×]# [〇]\@　絶頂：\@FLAG:挿入アニメフィルタ & アニメフィルタ_絶頂 ? [×]# [〇]\@　射精：\@FLAG:挿入アニメフィルタ & アニメフィルタ_射精 ? [×]# [〇]\@　溢れ：\@FLAG:挿入アニメフィルタ & アニメフィルタ_溢れ ? [×]# [〇]\@
	PRINTFORML [7]└挿入中は毎ターンアニメ表示：\@FLAG:挿入アニメ过滤器 & アニメ过滤器_挿入 ? [×]# [〇]\@
PRINTFORML [8] - \@ FLAG:立絵種類 ? 顔絵 # 立絵 \@の表示位置 [\@ FLAG:画像表示位置 ? 左寄せ# 中央寄せ \@]
PRINTFORML [9] - 画像表示に枠をつける \@ FLAG:画像枠表示 ? ON # OFF \@
PRINTFORML [999] - 返回
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 1
	FLAG:画像表示++
	SIF FLAG:画像表示 > 10
		FLAG:画像表示 = 10
ELSEIF RESULT == 2
	FLAG:画像表示--
	SIF FLAG:画像表示 < 1
		FLAG:画像表示 = 1
ELSEIF RESULT == 3
	FLAG:立絵種類 = !FLAG:立絵種類
ELSEIF RESULT == 4
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 5 && FLAG:挿絵表示
	FLAG:射精画像種類++
	SIF FLAG:射精画像種類 >= 3
		FLAG:射精画像種類 = 0
ELSEIF RESULT == 6
	FLAG:アニメーション++
	SIF FLAG:アニメーション >= 6
		FLAG:アニメーション = 0
ELSEIF RESULT == 7
	INVERTBIT FLAG:挿入アニメ过滤器, 0
	;CALL ASK_M("挿入表示切替",1,"絶頂表示切替",1,"射精表示切替",1,"射精表示切替",1,"戻る",1)
	;SIF RESULT == 4
	;	RESTART
	;INVERTBIT FLAG:挿入アニメフィルタ, RESULT
	RESTART
ELSEIF RESULT == 8
	FLAG:画像表示位置 = !FLAG:画像表示位置
ELSEIF RESULT == 9
	FLAG:画像枠表示 = !FLAG:画像枠表示
ENDIF
RESTART



;料理オプション設定関数
;0:手動決定,1:異物不可,2:薬物不可
@SET_OPTION_COOKING
#DIM CONST OP_BIT_手動決定 = 0
#DIM CONST OP_BIT_異物不可 = 1
#DIM CONST OP_BIT_薬物不可 = 2
#DIM CONST OP_BIT_採点オン = 3
PRINTFORML 料理関連のオプションを設定します
PRINTFORML 現在的設定
IF GET_OPTION_COOKING("料理内容手動決定")
	PRINTFORML 　料理内容：手動で決定
ELSE
	PRINTFORML 　料理内容：自動で決定
ENDIF
IF GET_OPTION_COOKING("異物混入不可")
	PRINTFORML 　異物混入：しない
ELSE
	PRINTFORML 　異物混入：する
ENDIF
IF GET_OPTION_COOKING("薬物混入不可")
	PRINTFORML 　薬物混入：しない
ELSE
	PRINTFORML 　薬物混入：する
ENDIF
SELECTCASE GET_OPTION_COOKING("自動味付け設定")
CASE 0
	PRINTFORML 　自動味付：しない
CASE 10
	PRINTFORML 　自動味付：自動で普通の味
CASE 11
	PRINTFORML 　自動味付：自動で浓郁
CASE 12
	PRINTFORML 　自動味付：自動で薄味
ENDSELECT
IF GET_OPTION_COOKING("採点機能オフ")
	PRINTFORML 　採点機能：オフ
ELSE
	PRINTFORML 　採点機能：オン
ENDIF
DO
	PRINTFORML [9] 料理内容の設定変更
	PRINTFORML [8] 異物混入の設定変更
	PRINTFORML [7] 薬物混入の設定変更
	PRINTFORML [6] 自動味付の設定変更
	PRINTFORML [5] 採点機能の設定変更
	PRINTFORML 
	PRINTFORML 
	PRINTFORML [0] 決定
	INPUT
	SELECTCASE RESULT
	CASE 9
		INVERTBIT FLAG:料理オプション, OP_BIT_手動決定
	CASE 8
		INVERTBIT FLAG:料理オプション, OP_BIT_異物不可
	CASE 7
		INVERTBIT FLAG:料理オプション, OP_BIT_薬物不可
	CASE 6
		PRINTFORML 自動味付け機能の設定
		PRINTFORML [12] 自動味付け＜薄味＞
		PRINTFORML [11] 自動味付け＜浓郁＞
		PRINTFORML [10] 自動味付け＜普通の味＞
		PRINTFORML [ 0] 自動味付けを使わない
		DO
			INPUT
			SELECTCASE RESULT
			CASE 10, 11, 12
				CLEARBIT FLAG:料理オプション, 10, 11, 12
				SETBIT   FLAG:料理オプション, RESULT
			CASE 0
				CLEARBIT FLAG:料理オプション, 10, 11, 12
			CASEELSE
			PRINTFORML 無効な入力です
			CONTINUE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 5
		INVERTBIT FLAG:料理オプション, OP_BIT_採点オン
	CASE 0
		RETURN 
	CASEELSE
		PRINTFORML 無効な入力です
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART


;料理オプションの内容取得関数
@GET_OPTION_COOKING(TYPE)
#FUNCTION
#DIMS DYNAMIC TYPE
#DIM CONST OP_BIT_手動決定 = 0
#DIM CONST OP_BIT_異物混入不可 = 1
#DIM CONST OP_BIT_薬物混入不可 = 2
#DIM CONST OP_BIT_採点機能オン = 3
#DIM CONST OP_BIT_自動味付け_普通 = 10
#DIM CONST OP_BIT_自動味付け_濃い = 11
#DIM CONST OP_BIT_自動味付け_单薄的 = 12
SELECTCASE TYPE
CASE "料理内容手動決定"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_手動決定)
		RETURNF 1
CASE "料理内容自動決定"
	RETURNF !GET_OPTION_COOKING("料理内容手動決定")

CASE "異物混入不可"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_異物混入不可)
		RETURNF 1
CASE "異物混入可能"
	RETURNF !GET_OPTION_COOKING("異物混入不可")

CASE "薬物混入不可"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_薬物混入不可)
		RETURNF 1
CASE "薬物混入可能"
	RETURNF !GET_OPTION_COOKING("薬物混入不可")
CASE "自動味付け設定"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_普通)
		RETURNF OP_BIT_自動味付け_普通
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_濃い)
		RETURNF OP_BIT_自動味付け_濃い
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_单薄的)
		RETURNF OP_BIT_自動味付け_单薄的
CASE "採点機能オン"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_採点機能オン)
		RETURNF 1
CASE "採点機能オフ"
	RETURNF !GET_OPTION_COOKING("採点機能オン")
CASEELSE
	THROW 不明なTYPE%TYPE%
ENDSELECT
RETURNF 0



@SIMATTYAUOJISAN
;TW414_移動パッチにて右クリックやマクロによるスキップもしまっちゃうように修正
;PRINTでのウェイトをなくすかわりにFORCEWAITでスキップ解除するように
;
FORCEWAIT
PRINT 「画面上に表示されていない数字を手打ちする子はここかな？」
FORCEWAIT
PRINT しまっちゃうおじさんが現れた！
FORCEWAIT
PRINT 「さあ、画面上に表示されていない数字を手打ちする子のお金は、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM しまっちゃうおじさんは%CALLNAME:MASTER%の有り金をしまってしまった
MONEY = 0
FORCEWAIT
PRINT 「画面上に表示されていない数字を手打ちする子の道具も、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%の道具はしまわれてしまった
FOR LOCAL,0,500
	ITEM:LOCAL = 0
NEXT
FORCEWAIT
PRINT 「画面上に表示されていない数字を手打ちする子の技巧も、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%の技巧はしまわれてしまった
FORCEWAIT
FOR LOCAL,0,100
	ABL:0:LOCAL = 0
NEXT
PRINT 「画面上に表示されていない数字を手打ちする子の経験も、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%の経験はしまわれてしまった
FORCEWAIT
FOR LOCAL,0,100
	EXP:0:LOCAL = 0
NEXT
;せっかくだしコレクションもしまっちゃいましょう（eraTW4.15パンツ対策パッチにて追加）
;
PRINT 「画面上に表示されていない数字を手打ちする子のパンツ收藏も、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%が丹精込めて収集した珠玉のパンツ收藏はしまわれてしまった
FORCEWAIT
FLAG:700 = 0
FOR LOCAL,1,CHARANUM
	FOR LOCAL:1,0,MAXPANTS
		CFLAG:LOCAL:(LOCAL:1 + パンツ管理) = 0
	NEXT
NEXT
PRINT 「画面上に表示されていない数字を手打ちする子の信頼も、どんどんしまっちゃうからね」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%の少女たちからの信頼はしまわれてしまった
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:信頼度 = 0
NEXT
FORCEWAIT
;
;
;PRINT 「画面上に表示されていない数字を手打ちする子の基礎能力も、どんどんしまっちゃうからね」
;FORCEWAIT
;PRINTFORM %CALLNAME:MASTER%の基礎能力はしまわれてしまった
;FOR LOCAL,0,9
;	MAXBASE:0:LOCAL = 99
;	BASE:0:LOCAL = min(MAXBASE:0:LOCAL,BASE:0:LOCAL)
;NEXT
;さすがにやり過ぎは良くないと思ったのでクリック連打で許してみた
;作者様へ　勝手に当てたパッチですので不愉快でしたらこの部分だけパッチ前に戻してくださって構いません。
;
;
FORCEWAIT
PRINTW もうだめだあ～～～～


;セクシーパンツ削除
@DELETE_SEXYPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,20,26
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT

;バイブパンツ削除
@DELETE_VIBEPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,27,30
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT


