;-------------------------------------------------
;酒虫の様子を見る
;日常系コマンド、レベル1
;-------------------------------------------------
@COM450
#DIM 醸造量
;FLAG:酒虫 水の種類、50超えると酒のアイテム番号

VARSET LOCAL

PRINTFORML %CALLNAME:MASTER%は酒虫の入っている瓶を覗き込んだ
PRINTFORML 瓶は%STR:(7000 + FLAG:酒虫)%で満たされている…
DRAWLINE
PRINTFORML [0] 什麼也不做
IF TFLAG:打水
	PRINTFORML [1] %STR:(TFLAG:打水 + 7000)%と入れ替える
ELSE
	PRINTPLAIN [1] 替えの水も無しにくみ出すわけにはいかない
	PRINTL 
ENDIF
SIF ITEM:濁酒 >= 5 && FLAG:酒虫 == 1 && DAY:10 < 3
	PRINTFORML [2] 濁酒を足す（濁酒を5消費）
;それ以上手を加えても中身が変化しない場合は表示させないように
SIF BASE:MASTER:8 >= 2400 && (FLAG:酒虫 < 51 || FLAG:酒虫 == 51)
	PRINTFORML [3] 時間操作酒造（TSP2400消費）
INPUT
IF RESULT == 1 && TFLAG:打水
	PRINTFORML %CALLNAME:MASTER%は%STR:(7000 + FLAG:酒虫)%を汲み出して%STR:(TFLAG:打水 + 7000)%と入れ替えた
	;酒ができてた場合
	IF FLAG:酒虫 > 50
		醸造量 = 醸造できた量(FLAG:酒虫)
		PRINTFORML %ITEMNAME:(FLAG:酒虫)%を{醸造量}個手に入れた
		ITEM:(FLAG:酒虫) += 醸造量
	ENDIF
	DAY:10 = 0
	FLAG:酒虫 = TFLAG:打水
	TFLAG:打水 = 0
ELSEIF RESULT == 2 && ITEM:濁酒 >= 5 && FLAG:酒虫 == 1 && DAY:10 < 3
	PRINTFORML %CALLNAME:MASTER%は瓶に濁酒を注ぎ込んだ
	PRINTFORML これで少し早くできるはずだ…
	DAY:10 += 3
	ITEM:濁酒-= 5
;それ以上手を加えても中身が変化しない場合は表示させないように
ELSEIF RESULT == 3 && BASE:MASTER:8 >= 2400 && (FLAG:酒虫 < 51 || FLAG:酒虫 == 51)
	PRINTFORML %CALLNAME:MASTER%は時間停止能力を応用して強引に瓶の中の時を進めた
	DAY:10 += 1
	BASE:MASTER:8 -= 2400
ELSE
	RETURN -1
ENDIF
TIME += 5
RETURN 1


;酒虫の様子を見る
@COM_ABLE450
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(450)
	RETURN RESULT
;台所
SIF CFLAG:MASTER:現在位置 != 拠点_酒虫
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1
