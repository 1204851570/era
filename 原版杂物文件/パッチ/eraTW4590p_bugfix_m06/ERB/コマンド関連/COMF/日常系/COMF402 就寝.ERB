;-------------------------------------------------
;就寝
;日常系コマンド、レベル1
;-------------------------------------------------
@COM402
#DIM 労働量
#DIM 収益
IF TARGET > 0
	CFLAG:同行 = 0
	CFLAG:陪睡中 = 0
	CFLAG:MASTER:同行 = 0
	CFLAG:MASTER:陪睡中 = 0
ENDIF

IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:311
	CALL SET_SLEEP(1,MASTER,0)
ELSE
	CALL SET_SLEEP(1,TARGET,1)
	SIF CFLAG:TARGET:衰弱
		CFLAG:TARGET:衰弱 = 0
ENDIF

CALL SET_SNEAK(0,TARGET,1)
;CALL SET_TOGETHER(0,TARGET,1)

IF FLAG:内職 && !CFLAG:陪睡中 && BASE:MASTER:気力 > 300 && BASE:MASTER:TSP > 0
	PRINTFORML %CALLNAME:MASTER%は寝る前に残ったTSPを使って内職をした
	労働量 = MIN(BASE:MASTER:気力, BASE:MASTER:TSP /2)
	収益 = 労働量 * (5 + RAND:5) / 10
	PRINTFORMW \\{収益}稼いだ
ENDIF

IF 兒童の睡眠時間(TARGET) && CFLAG:出産経過日 >= CHILD_よちよち && FLAG:兒童描写 && !INRANGE(TIME, 540, 840)
	PRINTFORMDL 家族三人で川の字になって寝る事にした…
	CALL KOJO_MESSAGE_SEND("EVENT", 0, TARGET, 0, 0, "CHILD_RAISING_OYASUMI")
	TFLAG:193 = 1
	RESETCOLOR
ENDIF

CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
BEGIN AFTERTRAIN

RETURN 1

;就寝
@COM_ABLE402
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(402)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室設定
SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:311 && CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:陪睡中 == 0
	RETURN 0
;起床後12時間以上経過してないとダメ（デバッグ中）
IF FLAG:1005 == 0
SIF NEMUKE() < 720
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
ENDIF
RETURN 1