;-------------------------------------------------
;告白する
;日常系コマンド、レベル2
;-------------------------------------------------
@COM352
LOCAL = 0
IF FLAG:甲斐性無
	PRINTFORMW %CALLNAME:MASTER%にはそんな胆量はない
	RETURN -1
ENDIF
;告白対象確認
PRINTFORML %CALLNAME:TARGET%に告白します。【恋人】は1人しか取得出来ません。
PRINTW よろしいですか？
PRINTL [0] - やめる
PRINTL [1] - 告白。
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > 1
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	RETURN -1
ENDIF

;パラメータにより成功基礎値判定


IF  CFLAG:合意判定 > 300 + TALENT:難以越過的底線 * 20
	TFLAG:193 = 1
ELSE
	TFLAG:193 = 0
ENDIF
IF TFLAG:193 == 1
	SOURCE:歓楽 = 1000
	SOURCE:受動 = 500 + 100 * ABL:従順
	SOURCE:征服 = 500 + 100 * ABL:施虐属性
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE352
;停止中は不可
SIF FLAG:70
	RETURN 0
;妄想中は告白できない
SIF TFLAG:妄想中 == 1
	RETURN 0
;告白する実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(352)
	RETURN RESULT
;二人きりでないとだめ
SIF GET_TARGETNUM() > 1
	RETURN 0
;馴れ合い強度2
SIF TCVAR:MASTER:亲热 < 2
	RETURN 0
;すでに恋人が居る場合はだめ
SIF GETBIT(CFLAG:MASTER:既成事実 , 2)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;添い寝中
SIF CFLAG:陪睡中
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1
