@DAIRY_CONDITION_EV10
#DIM YOME
#DIM CHILDNUM
SIF !CFLAG:MASTER:兒童人数
	RETURN -1
SIF RAND:20 > 0
	RETURN -1
YOME = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	IF CFLAG:LOCAL:兒童人数 - CFLAG:LOCAL:育児人数 > 0
		YOME = LOCAL
		CHILDNUM = RAND:(CFLAG:LOCAL:兒童人数 - CFLAG:LOCAL:育児人数) + 1
		BREAK
	ENDIF
NEXT
IF YOME
	CALL DAIRY_EV10(YOME, CHILDNUM)
	RETURN 1
ELSE
	RETURN -1
ENDIF


@DAIRY_EV10(ARG, ARG:1)
;PRINTFORMW %CALLNAME:ARG%との子供の%CHILDNAME:ARG:(ARG:1)%から手紙が来た
PRINTFORMW %CHILDNAME:ARG:(ARG:1)%から手紙が来た
LOCAL:1 = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:好感度 > CFLAG:ARG:好感度
		LOCAL:1 ++
NEXT

TARGET = ARG
;口上専用の手紙イベント分岐（なければ通常イベント）
IF !RAND:2
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_口上手紙(ARG:1)
	CATCH
		GOTO TEGAMI
	ENDCATCH
ELSE
	$TEGAMI
	IF LOCAL:1 >= 3
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_共通手紙(ARG:1, "すけこまし")
		CATCH
			PRINTFORMW もっと%CALLNAME:ARG%を大事にしろと書いてあった……
		ENDCATCH
	ELSEIF !RAND:5
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_共通手紙(ARG:1, "金の無心")
		CATCH
			PRINTFORMW 金の無心だ……
		ENDCATCH
		PRINTFORMW %CALLNAME:MASTER%は\@ MONEY > 30000? ￥30000# 有り金全部\@送ることにした
		MONEY = MAX(MONEY - 30000,0)
	ELSE
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_共通手紙(ARG:1, "仕送り")
		CATCH
			PRINTFORMW %CALLNAME:ARG%と%CALLNAME:MASTER%を気遣う言葉と仕送り\\10000が入っていた
		ENDCATCH
		MONEY += 10000
	ENDIF
ENDIF
TARGET = TARGET:1
RETURN 1