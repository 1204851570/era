;-------------------------------------------------
;女の子を物色
;日常系コマンド、レベル1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;生成するMOB娘の種類

IF FLAG:约会的对象
	PRINTFORMW 身边已有一位非常可爱的女孩
	RETURN -1
ENDIF

$BUSSYOKU
IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %CALLNAME:MASTER%物色着周围是否有可爱的女孩
PRINTFORMW ……那个女孩怎么様？
SETBIT TFLAG:一日一回, 9
SELECTCASE CAN_NANPA()
	CASE 1
		YUKIZURI_RACE '= TEXTR("町娘/人妻")
	CASE 2
		YUKIZURI_RACE = 妖精
	CASE 3
		YUKIZURI_RACE = 女仆妖精
	CASE 4
		YUKIZURI_RACE = 因幡
	CASE 5
		YUKIZURI_RACE = 僵尸妖精
	CASE 6
		YUKIZURI_RACE '= TEXTR("狸猫妖怪/化け狐/化け猫")
	CASE 7
		YUKIZURI_RACE = 河童
	CASE 8
		YUKIZURI_RACE = 白狼天狗
	CASE 9
		IF !RAND:10
			YUKIZURI_RACE = 鬼
		ELSE
			YUKIZURI_RACE = 僵尸妖精
		ENDIF
	CASE 10
		YUKIZURI_RACE = 玉兎
ENDSELECT



;PRINTFORMW %YUKIZURI_RACE%
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1
$CHOICE
CALL CHARA_STATE(RANDOM_CHARANUM)
IF CSTR:RANDOM_CHARANUM:処女喪失履歴 != ""
	DRAWLINE
	CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
ENDIF
CALL ASK_M("很中意",1,"寻找别的女孩",1,"寻找之前的女孩", TFLAG:直前路人子Map == CFLAG:MASTER:現在位置 /100,"寻找以前认识的女孩",1,"物色中止",1)
IF RESULT == 4
	CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:着替え実行 = 0
	CFLAG:RANDOM_CHARANUM:出禁 = 2
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:直前路人子Map = CFLAG:MASTER:現在位置 /100
	SAVECHARA "TEMP_MOB", "刚刚遇到的女孩子", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL
	IF RESULT == -1
		;念のためまっさらにしておく
		CALL 路人子画像リセット(RANDOM_CHARANUM)
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		RETURN -1
	ENDIF
	CFLAG:RANDOM_CHARANUM:出禁 = 0
ELSEIF RESULT == 2
	CALL 路人子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:直前路人子Map = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:現在位置 = CFLAG:MASTER:現在位置
IF CFLAG:MASTER:現在位置 / 100 == MAIN_MAP
	CFLAG:RANDOM_CHARANUM:约会中 = CFLAG:MASTER:現在位置 / 100
	CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
ELSE
	CFLAG:RANDOM_CHARANUM:遭遇位置 = CFLAG:MASTER:現在位置
	CFLAG:RANDOM_CHARANUM:拠点来訪 = 0
ENDIF
CFLAG:RANDOM_CHARANUM:延迟 = 30
TARGET = RANDOM_CHARANUM
RETURN 1


@COM_ABLE444
;一括管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA()
	RETURN 0
SIF !INRANGE(TIME,540,1140)
	RETURN 0
;SIF !CFLAG:RANDOM_CHARANUM:出禁
;	RETURN 0
RETURN 1



@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		SIF MOBGIRL_PLACE:LOCAL == CFLAG:MASTER:現在位置 / 100
			SETCOLOR  C_P_GREEN
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[没有]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[放弃]", 99

@LOAD_MOBGIRL
CALL PRINT_SAVED_GIRL_LIST
INPUT
LOCAL = RESULT
SIF !INRANGE(LOCAL, 0, MAXMOB -1)
	RETURN -1
IF MOBGIRL_PLACE:LOCAL != CFLAG:MASTER:現在位置 / 100
	PRINTFORML {MOBGIRL_PLACE:LOCAL}
	PRINTFORMW 不在这里吧
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(LOCAL))
	CALL 路人子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(LOCAL)
	PRINTFORML 发现了%CALLNAME:RANDOM_CHARANUM%
	IF CSTR:RANDOM_CHARANUM:路人子画像文字列_服_通常 == ""
		PRINTFORML 未設定外表。请选择类型。
		PRINTBUTTON "[町娘] ", "町娘"
		PRINTBUTTON "[人妻] ", "人妻"
		PRINTBUTTON "[妖精] ", "妖精"
		PRINTBUTTON "[女仆妖精] ", "女仆妖精"
		PRINTBUTTON "[僵尸妖精] ", "僵尸妖精"
		PRINTBUTTON "[河童] ", "河童"
		PRINTBUTTON "[白狼天狗] ", "白狼天狗"
		PRINTBUTTON "[随机] ", "随机"
		INPUTS
		CALL SET_MOB_IMAGE_STR(RANDOM_CHARANUM, RESULTS)
	ENDIF
	IF FLAG:70	;時間停止中は反応なし

	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && (TALENT:RANDOM_CHARANUM:炮友 || TALENT:RANDOM_CHARANUM:思慕)
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は悔しげな、それでいて期待するような表情でもじもじしている…
	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && !TALENT:RANDOM_CHARANUM:恋慕 && !TALENT:RANDOM_CHARANUM:思慕 && !TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW 发现了%CALLNAME:MASTER%的%CALLNAME:RANDOM_CHARANUM%带着逞强的表情
	ELSEIF TALENT:RANDOM_CHARANUM:恋慕
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は开心地こちらに駆け寄ってきた
	ELSEIF TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%はこちらにやって来るなり、腕にしなだれかかってきた…
	ELSEIF ABL:RANDOM_CHARANUM:親密 >= 5
		PRINTFORMW 发现了%CALLNAME:MASTER%的%CALLNAME:RANDOM_CHARANUM%挥了挥手
	ENDIF
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML %MOBNAME%的事忘掉,取而代之的是%CALLNAME:RANDOM_CHARANUM%的事吗？
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %CALLNAME:MASTER%薄情地而干脆地忘掉了%MOBNAME%
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:經常去的地方
TALENT:RANDOM_CHARANUM:路人 = 2
CFLAG:RANDOM_CHARANUM:路人子セーブスロット = CHOSEN_MOB
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:路人子プロフィール１, RANDOM_CHARANUM
PRINTFORMW 记住了%CALLNAME:RANDOM_CHARANUM%