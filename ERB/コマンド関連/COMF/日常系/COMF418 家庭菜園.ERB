;-------------------------------------------------
;家庭菜園
;日常系コマンド、レベル1
;-------------------------------------------------
@COM418
LOCAL:1 = 1 + FLAG:畑拡張
FOR LOCAL,0, LOCAL:1
	CALL SHOW_PLANT(LOCAL)
NEXT
PRINTL [999] 算了
$INPUT_LOOP
INPUT
IF INRANGE(RESULT / 100, 0, LOCAL:1)
	CALL FARMING(RESULT / 100, RESULT % 100)
ELSE
	TIME += 5
	RETURN 0
ENDIF


@FARMING(ARG, 行動番号)
#DIM 行動番号
LOCALS = %HATAKE_NAME:ARG%
IF 行動番号 == 0
	IF PLANT_TYPE:ARG > 0
		PRINTFORML 已經種植了%LOCALS%
		TIME += 5
	ELSE
		PRINTL 要种什么呢？
		CALL INPLANT(ARG)
		TIME += 10
	ENDIF
ELSEIF 行動番号 == 1
	IF !PLANT_DRY:ARG
		PRINTL 今天已经浇够水了
	ELSEIF !TFLAG:打水
		PRINTL 還沒有打水
	ELSE
		PLANT_DRY:ARG = 0
		PRINTFORML 对%LOCALS%浇了水
		SIF ITEM:三粒の天滴
			FLAG:天滴坤神 = 1
		TIME += 5
	ENDIF
ELSEIF 行動番号 == 2
	SIF PLANT_DAYS:ARG > 9
		PLANT_GROW:ARG += 2
	SIF PLANT_FOOD:ARG
		PLANT_GROW:ARG ++
	SIF FLAG:天滴坤神 && RAND:2 == 1
		PLANT_GROW:ARG += 2
	;大豊作…天滴とこやし補正でないと到達不能
	IF PLANT_GROW:ARG > 12
		PRINTL 大豊作だ！
		IF PLANT_FOOD:ARG
			LOCAL = 14 + RAND:6
		ELSE
			LOCAL = 14
		ENDIF
	ELSEIF PLANT_GROW:ARG > 10
		PRINTL 丰收了！
		IF PLANT_FOOD:ARG
			LOCAL = 10 + RAND:4
		ELSE
			LOCAL = 10
		ENDIF
	ELSEIF PLANT_GROW:ARG > 7
		IF PLANT_FOOD:ARG
			LOCAL = 5 + RAND:3
		ELSE
			LOCAL = 5
		ENDIF
	ELSE
		LOCAL = 3
	ENDIF
	PRINTFORML 収穫了{LOCAL}個%LOCALS%
	IF PLANT_TYPE:0 == 190
		ITEM:0 += LOCAL
	ELSE
		ITEM:(200 + PLANT_TYPE:ARG) += LOCAL
	ENDIF
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	TIME += 20
ELSEIF 行動番号 == 3
	IF PLANT_DRY:ARG <= 4 && PLANT_GROW:ARG <= 10
		PRINTFORML %LOCALS%を引っこ抜きますか？
		CALL ASK_YN
		IF RESULT
			TIME += 5
			RETURN 0
		ENDIF
	ENDIF
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	PRINTFORML 拔掉了%LOCALS%
	TIME += 10
;畑が拡張されたら全体にします
ELSEIF 行動番号 == 4 && ITEM:簡易泳池 && GETBIT (FLAG:泳池, 2)
	IF !PLANT_DRY:ARG
		PRINTL 今天已经浇够水了
	ELSE
		PLANT_DRY:ARG = 0
		PRINTFORML 对%LOCALS%浇了水
		SIF ITEM:三粒の天滴
			FLAG:天滴坤神 = 1
		TIME += 5
	ENDIF
ELSEIF 行動番号 == 5 && ITEM:肥料
	IF PLANT_FOOD:ARG
		PRINTL 肥料のやりすぎもよくない
	ELSE
		PLANT_FOOD:ARG = 1
		PRINTFORML %LOCALS%に肥料をやった
		TIME += 20
		ITEM:肥料 --
	ENDIF
ELSE
	TIME += 5
	RETURN 0
ENDIF

@INPLANT(ARG)
FOR LOCAL,151,195	;こやし以降を除外
	SIF ITEM:LOCAL
		PRINTFORML [{LOCAL}]%ITEMNAME:(LOCAL)%
NEXT
PRINTL [999] 算了
$INPUT_LOOP
INPUT
IF RESULT >= 151 && RESULT <= 199 && ITEM:RESULT
	PRINTFORML 在畑{ARG}里种上了%ITEMNAME:(RESULT)%
	ITEM:RESULT -= 1
	PLANT_TYPE:ARG = RESULT
	PLANT_DRY:ARG = 1
	;三粒の天滴を持っている場合、植えてすぐに施肥、水撒き完了状態に
	IF ITEM:三粒の天滴
		PLANT_DRY:ARG = 0
		PLANT_FOOD:ARG = 1
		FLAG:天滴坤神 = 1
	ENDIF
	IF PLANT_TYPE:ARG >= 190
		HATAKE_NAME:ARG = %ITEMNAME:(PLANT_TYPE:ARG - 190)%
	ELSE
		HATAKE_NAME:ARG = %STR:(3500 + PLANT_TYPE:ARG)%
	ENDIF
ELSE
	RETURN
ENDIF

@SHOW_PLANT(ARG)
PRINTFORM 畑{ARG}：
IF PLANT_TYPE:ARG > 0
	SETCOLOR 0x00FF00
	PRINTFORM %HATAKE_NAME:ARG%栽培中
	IF PLANT_DRY:ARG > 4 || PLANT_GROW:ARG > 10
		SETCOLOR 0x994C00
		PRINT （枯）
	ELSE
		SELECTCASE PLANT_GROW:ARG
			CASE 10
				SETCOLOR 0xFF0000
				PRINT （熟）
			CASE 8,9
				SETCOLOR 0xFF8000
				PRINT （実）
			CASE 6,7
				SETCOLOR 0xFFFF00
				PRINT （花）
			CASE 4,5
				PRINT （葉）
			CASEELSE
				PRINT （苗）
		ENDSELECT 
	ENDIF
	IF !PLANT_DRY:ARG
		SETCOLOR C_AQUA
		PRINT 　撒水完畢
	ENDIF
	IF PLANT_FOOD:ARG
		SETCOLOR C_CREAM
		IF ITEM:三粒の天滴
			PRINT 　坤神の恵みを享けている
		ELSE
			PRINT 　施肥済み
		ENDIF
	ENDIF
	RESETCOLOR
	PRINTL
	;水撒き
	IF PLANT_DRY:ARG
		LOCAL = ARG * 100 + 1
		LOCALS = [%TOSTR(LOCAL, "000")%] - 灑水
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;収穫
	IF PLANT_GROW:ARG > 7 && !(PLANT_DRY:ARG > 4 || PLANT_GROW:ARG > 10)
		LOCAL = ARG * 100 + 2
		LOCALS = [%TOSTR(LOCAL, "000")%] - 収穫
		SIF PLANT_GROW:ARG == 10
			SETCOLOR 0xFF0000
		PRINTBUTTONLC LOCALS, LOCAL
		RESETCOLOR
	ELSE
		PRINTLC 　
	ENDIF
	;整地
	LOCAL = ARG * 100 + 3
	LOCALS = [%TOSTR(LOCAL, "000")%] - 抜出
	PRINTBUTTONLC LOCALS, ARG * 100 + 03
	;スプリンクラー
	IF ITEM:簡易泳池 && GETBIT (FLAG:泳池, 2) && PLANT_DRY:ARG
		LOCAL = ARG * 100 + 4
		LOCALS = [%TOSTR(LOCAL, "000")%] - 自動灑水系統
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;肥料を撒く
	IF ITEM:195 && PLANT_FOOD:ARG == 0
		LOCAL = ARG * 100 + 5
		LOCALS = [%TOSTR(LOCAL, "000")%] - 肥料を撒く
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
ELSE
	PRINTL 空き
	;苗植え
	LOCAL = ARG * 100 + 0
	LOCALS = [%TOSTR(LOCAL, "000")%] - 種苗
	PRINTBUTTONLC LOCALS, LOCAL
ENDIF
PRINTL

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE418
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(418)
	RETURN RESULT
;庭
SIF !家庭菜園可(CFLAG:MASTER:現在位置)
	RETURN 0
;家庭菜園セット
SIF !ITEM:150
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1

