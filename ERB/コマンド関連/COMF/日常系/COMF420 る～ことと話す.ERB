;-------------------------------------------------
;る～ことと話す
;日常系コマンド、レベル1
;-------------------------------------------------
@COM420
PRINTL 「你有什么事吗？」
PRINTL [0] - 想找人
PRINTL [1] - 想做掃除
PRINTL [2] - 关于git控制系统
PRINTL [100] - 返回
INPUT
IF RESULT == 0
	PRINTL 「要寻找誰呢？」
	CALL HITOSAGASI()
ELSEIF RESULT == 1
CALL OHEYA_CHECK
ELSEIF RESULT == 2
	PRINTDATA
		DATAFORM 「使用git控制系统的话可以将补丁一次性整合起来哦」
		DATAFORM 「有git控制系统就可以很简单的把握变更的地方」
		DATAFORM 「過去的代码也能很好的得到保存」
	ENDDATA
	PRINTW 好像很厉害的様子
	RETURN -1
ELSEIF RESULT == 100
	RETURN -1
ELSE
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
SIF RESULT == 0
	RETURN -1
IF FLAG:倉庫施錠
	PRINTW 锁打不开
	RESTART
ENDIF
IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:教養 < 1 && !MAIN_MAP && !INRANGE(CFLAG:MASTER:初始位置,26,35)
	PRINTFORMW 未熟的%CALLNAME:MASTER%不能被発現…
ELSE
	IF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
ENDIF
RETURN -1

@OHEYA_CHECK
VARSET LOCAL
TFLAG:移動不能メッセージ = 0
FOR LOCAL,MINROOM(),MAXROOM
	LOCALS = %NAME_FROM_PLACE(LOCAL)%
	SIF LOCAL == CFLAG:MASTER:311
		LOCALS = %CALLNAME:MASTER%私室
	SIF LOCALS == ""
		GOTO SYOURYAKU
	SIF CFLAG:MASTER:現在位置 == LOCAL
		SETCOLOR 0 , 255, 0
	PRINTBUTTON "["+LOCALS+"]", LOCAL
	RESETCOLOR
	PRINTFORM %" "*(22-STRLENS(LOCALS))%
	LOCALS = %SEIKETU(LOCAL)%
	PRINTFORM %LOCALS,15,RIGHT%     
	RESETCOLOR
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	$SYOURYAKU
NEXT
PRINTFORML 
DRAWLINE
PRINTBUTTON "[確認終了]", 0

@SEIKETU(ARG)
#FUNCTIONS
IF OUTROOF(ARG) && DAY:2 == 4
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "積雪量:干净"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "積雪量:小"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "積雪量:較小"
		CASE IS < 2000
			SETCOLOR 0xFFFFFF
			RETURNF "積雪量:中"
		CASE IS < 3500
			SETCOLOR 0xFFFFFF
			RETURNF "積雪量:較大"
		CASE IS < 5000
			SETCOLOR 0xFFFFFF
			RETURNF "積雪量:大"
		CASEELSE
			SETCOLOR 0xFFFFFF
			RETURNF "積雪量:雪山"
	ENDSELECT
ELSE
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "清潔度:最高"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "清潔度:高"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "清潔度:較高"
		CASE IS < 2000
			SETCOLOR 0xFFFF00
			RETURNF "清潔度:中"
		CASE IS < 3500
			SETCOLOR 0xFF00FF
			RETURNF "清潔度:較低"
		CASE IS < 5000
			SETCOLOR 0xFF00FF
			RETURNF "清潔度:低"
		CASEELSE
			SETCOLOR 0xFF0000
			RETURNF "清潔度:垃圾山"
	ENDSELECT
ENDIF

@HITOSAGASI(ARG)
IF ARG
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:神社在住 > 0
			CONTINUE
		SIF AT_HOME(LOCAL) == 1
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 == MAXROOM && CFLAG:LOCAL:睡眠
			CONTINUE
		FOR LOCAL:1,10,100,10
			IF CFLAG:LOCAL:現在位置 == ARG * 100 + LOCAL:1
				PRINTFORML %CALLNAME:LOCAL,15,LEFT%
				LOCAL:2 ++
			ENDIF
		NEXT
	NEXT
	SIF !LOCAL:2
		PRINTFORML 誰也不在
ELSE
	FOR LOCAL,1,CHARANUM
		SIF AT_HOME(LOCAL) == 0
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 < MINROOM()
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 == MAXROOM && CFLAG:LOCAL:睡眠
			CONTINUE
		SIF CFLAG:LOCAL:神社在住
			SETCOLOR 160, 210, 255
		PRINTFORM %CALLNAME:LOCAL,15,LEFT%
		RESETCOLOR
		LOCALS = %NAME_FROM_PLACE(CFLAG:LOCAL:現在位置)%
		SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:311
			LOCALS = %CALLNAME:MASTER%私室
		PRINTBUTTON "["+LOCALS+"]", CFLAG:LOCAL:現在位置
		PRINTFORM %" "*(22-STRLENS(LOCALS))%
		IF LOCALS == ""
			PRINTFORML 不明な位置に居ます({CFLAG:LOCAL:現在位置})
		ELSEIF CFLAG:LOCAL:睡眠
			PRINTL 就寝中
		ELSE
			PRINTL
		ENDIF
	NEXT
ENDIF
PRINTBUTTON "[確認終了]", 0

;-------------------------------------------------
;実行判定
;-------------------------------------------------
;る～ことと話す
@COM_ABLE420
;自宅外は不可
SIF AT_HOME(MASTER) == 0
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(420)
	RETURN RESULT
SELECTCASE MAIN_MAP
	CASE 0
		;る～ことが寝ていたらダメ
		SIF CFLAG:2:睡眠
			RETURN 0
		;る～ことの居る所
		SIF CFLAG:MASTER:現在位置 != CFLAG:2:現在位置
			RETURN 0
	CASE 3
		SIF CFLAG:MASTER:現在位置 != 323
			RETURN 0
	CASE 9
		SIF CFLAG:MASTER:現在位置 != 923
			RETURN 0
	CASEELSE
		RETURN 0
ENDSELECT

SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1

