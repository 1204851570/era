;-------------------------------------------------
;食事を取る
;日常系コマンド、レベル1
;-------------------------------------------------
@COM414
#DIM 回復前体力_MASTER
#DIM 回復前気力_MASTER
#DIM 回復後体力_MASTER
#DIM 回復後気力_MASTER
#DIM 回復前体力_TARGET
#DIM 回復前気力_TARGET
#DIM 回復後体力_TARGET
#DIM 回復後気力_TARGET
#DIM NUM_TARGET
#DIM NOW_TARGET
#DIM NOW_SUCCESS
#DIM T_ID
CALL AssembleForMeal
NUM_TARGET = GET_TARGETNUM()
VARSET LOCAL
回復前体力_MASTER = BASE:MASTER:体力
回復前気力_MASTER = BASE:MASTER:気力
IF TARGET
	回復前体力_TARGET = BASE:TARGET:体力
	回復前気力_TARGET = BASE:TARGET:気力
	CALL COM414_CTRL_EAT_DISH("PRI")
	SIF RESULT < 0
		RETURN
	;信頼
	TFLAG:98 += 3
	SIF ABL:MASTER:料理技能 > 1
		TFLAG:98 ++ 
	SIF ABL:MASTER:料理技能 > 3
		TFLAG:98 ++ 
	
	;イタズラを仕込まれていない
	IF !TASTE_GIVEUP(TARGET, DISH_RADICAL()) && TFLAG:192 >= 0
		;味付けがツボだと失敗しない
		IF CFLAG:TARGET:味覚嗜好 & TCVAR:MASTER:308
			TFLAG:193 = MAX(TFLAG:193,0)
			;信頼にも修正
			TFLAG:98 += 2
		ENDIF
	ENDIF
	SELECTCASE DISHTYPE
		CASE 1

		CASE 2,4,5

		CASE 3

	ENDSELECT
	
	;特殊な食事フラグ
	SELECTCASE DISHNAME
		CASE "涮涮鍋"
			;その場にはいてないキャラがいるか確認
			FOR LOCAL, 1, NUM_TARGET + 1
				SIF EQUIP:(TARGET:LOCAL):下半身内衣２ > 0
					CONTINUE
				TFLAG:194 = 1
				BREAK
			NEXT
		CASE "金吉獣料理"
			TFLAG:194 = 34
		CASE "奶酪番茄沙拉"
			TFLAG:194 = 30
		CASE "興奮劑高湯"
			TFLAG:194 = 31
		CASEELSE
			SIF DISHTYPE == 4
				TFLAG:194 = 2
	ENDSELECT
	
	;その場に居るTARGET全員で回す
	;TARGET:0以外を先に処理して最後にTARGET:0
	NOW_TARGET = TARGET
	NOW_SUCCESS = TFLAG:193
	FOR LOCAL:4, 1, NUM_TARGET + 1
		TARGET = TARGET:(LOCAL:4)
		SIF TARGET == NOW_TARGET
			CONTINUE
		CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
		CALL COM414_CTRL_EAT_DISH("LATER")
		CALL COM414_1
	NEXT
	TARGET = NOW_TARGET
	TFLAG:193 = NOW_SUCCESS

	IF !CFLAG:睡眠
		CALL COM414_1
	ELSE
		SIF TFLAG:193 == 1
			EXP:MASTER:会話経験 --
	ENDIF
	回復後体力_TARGET = BASE:TARGET:体力
	回復後気力_TARGET = BASE:TARGET:気力
ENDIF

;軽食
IF DISHTYPE == 1
	CALL 吃飯(MASTER)
	TIME += 20
	SIF CFLAG:同行
		CFLAG:同行 += 20
;料理がしゃぶしゃぶではいてないキャラがいるとノーパンしゃぶしゃぶに
ELSEIF TFLAG:194 == 1
	BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + MAXBASE:MASTER:体力 / 3 , MAXBASE:MASTER:体力)
	BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + MAXBASE:MASTER:気力 / 2 , MAXBASE:MASTER:気力)
	BASE:MASTER:精力 = MIN(BASE:MASTER:精力 + MAXBASE:MASTER:精力 / 2 , MAXBASE:MASTER:精力)
	;食事をしたフラグ
	CALL 満腹度上昇(MASTER,"主食")
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ELSEIF DISHTYPE == 2
	CALL 吃飯(MASTER)
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ELSEIF DISHTYPE == 3
	CALL 吃飯(MASTER)
	TIME += 20
	SIF CFLAG:同行
		CFLAG:同行 += 20
ELSEIF DISHTYPE == 4 || DISHTYPE == 5
	CALL 吃飯(MASTER)
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ENDIF
回復後体力_MASTER = BASE:MASTER:体力
回復後気力_MASTER = BASE:MASTER:気力
;CALL RESET_DISH
DRAWLINE
PRINTFORML 体力+{ABS(回復後体力_MASTER - 回復前体力_MASTER)}（%CALLNAME:MASTER%）
PRINTFORML 気力+{ABS(回復後気力_MASTER - 回復前気力_MASTER)}（%CALLNAME:MASTER%）
IF TARGET
	PRINTFORML 体力+{ABS(回復後体力_TARGET - 回復前体力_TARGET)}（%CALLNAME:TARGET%）
	PRINTFORML 気力+{ABS(回復後気力_TARGET - 回復前気力_TARGET)}（%CALLNAME:TARGET%）
ENDIF
RETURN 1


;食事結果の成功制御、判定部分がほぼ同一だったので切り出し
@COM414_CTRL_EAT_DISH(SCENE)
#DIMS SCENE
SELECTCASE TFLAG:192
CASE 0
	;通常
	LOCAL = RAND:(100 - ABL:MASTER:料理技能 * 2)
	LOCAL:1 = MIN(90 + GET_SUCCESS_RATE() / 5, 99)
	SELECTCASE LOCAL
		CASE IS <= 9
			TFLAG:193 = 1
		CASE IS >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
		CASEELSE
			;残りは成功
			TFLAG:193 = 0
	ENDSELECT
CASE 1
	IF RAND:(100 - ABL:MASTER:料理技能 * 2) <= 9
		TFLAG:193 = 1
	ELSE
		;残りは成功
		TFLAG:193 = 0
	ENDIF
CASE -1, -2
	TFLAG:193 = -1
ENDSELECT

IF SCENE == "PRI"
	SIF TFLAG:192 == -2
		RETURN -1
	SELECTCASE TFLAG:193
	CASE 1
		TFLAG:98 = 2
	CASE -1
		TFLAG:98 = -2
	CASEELSE
		TFLAG:98 = 0
	ENDSELECT
ENDIF

@COM414_1
#DIM 空腹
#DIM DISH_ITAZURA
空腹 = 0
IF DISHTYPE == 3
	SIF TIME_PROGRESS(TCVAR:甜点空腹時刻)
		空腹 = 1
ELSE
	SIF TIME_PROGRESS(TCVAR:空腹時刻)
		空腹 = 1
ENDIF
SIF !空腹
	RETURN

;満腹でない
;固定で獲得するソース
SELECTCASE DISHSPOIL
	CASE 0
		SOURCE:歓楽 = 800
	CASE 1
		SOURCE:歓楽 = 400
	CASE 2
		SOURCE:歓楽 = 100
		SOURCE:不潔 = 400
		SOURCE:反感 = 400
ENDSELECT

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 60)
ELSEIF ABL:従順 <= 3
	SOURCE:歓楽 += 500 + (ABL:従順 * 60)
ELSEIF ABL:従順 <= 5
	SOURCE:歓楽 += 700 + (ABL:従順 * 60)
ELSEIF ABL:従順 <= 8
	SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
ELSEIF ABL:従順 <= 11
	SOURCE:歓楽 += 1500 + (ABL:従順 * 75)
ELSE
	SOURCE:歓楽 += 3000 + (ABL:従順 * 30)
ENDIF

;ノーパンしゃぶしゃぶで露出追加
SIF TFLAG:194 == 1
	SOURCE:露出 += 1000 
;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;イタズラ料理を先に処理
DISH_ITAZURA = TASTE_GIVEUP(TARGET, DISH_RADICAL())
SELECTCASE DISH_ITAZURA
CASE 0
	;味覚嗜好を見る
	SIF CFLAG:TARGET:味覚嗜好 & TCVAR:MASTER:308
		SOURCE:歓楽 += 200
CASE DISH_RADICAL_超辣
	SOURCE:苦痛 += 500
CASE DISH_RADICAL_超甜
	SOURCE:反感 += 300
CASE DISH_RADICAL_芥末
	SOURCE:苦痛 += 500
CASE DISH_RADICAL_柠檬
	SOURCE:不潔 += 300
ENDSELECT
IF DISH_ITAZURA
	SOURCE:反感 += 500
	TFLAG:98 -= 2
	IF TALENT:幼稚
		TIMES SOURCE:苦痛, 1.2
		TIMES SOURCE:反感, 1.2
		TIMES SOURCE:不潔, 1.2
	ENDIF
	IF CFLAG:行動 == 5
		TIMES SOURCE:反感, 1.2
		TIMES SOURCE:不潔, 1.2
		TFLAG:98 -= 2
	ENDIF
	SOURCE:歓楽 = 0
	IF TALENT:大胃王
		TIMES SOURCE:反感, 1.2
		TIMES SOURCE:不潔, 1.2
		TFLAG:98 -= 2
	ENDIF
	CALL KIGEN_CHANGE(TARGET, 70, -1)
	CALL 吃飯(TARGET)
	RETURN
ENDIF

SOURCE:受動 = 200 + 100 * ABL:従順
SOURCE:征服 = 200 + 100 * ABL:施虐属性

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	CALL KIGEN_CHANGE(TARGET,30,1)
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	IF TALENT:心情 < 0
		CALL KIGEN_CHANGE(TARGET,100,1)
	ELSE
		CALL KIGEN_CHANGE(TARGET,50,1)
	ENDIF
ENDIF
SOURCE:歓楽 = SOURCE:歓楽 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
SOURCE:征服 = SOURCE:征服 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
SOURCE:受動 = SOURCE:受動 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
IF CFLAG:行動 == 5
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:98 += 2
ENDIF
CALL 吃飯(TARGET)

SIF TALENT:大胃王
	TIMES SOURCE:歓楽 , 1.50

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE414
;食事実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(414)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;イタズラをした料理は自分で食べない
SIF DISH_RADICAL() && !TARGET
	RETURN 0
;料理がない
SIF DISHNAME == ""
	RETURN 0
;軽食以外は屋内じゃなきゃ無理
SIF FLAG:20 >= 10 && !INROOM(CFLAG:MASTER:現在位置)
	RETURN 0
;食べたばかり
IF DISHTYPE == 3
	SIF !TIME_PROGRESS(TCVAR:MASTER:甜点空腹時刻)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:MASTER:空腹時刻)
		RETURN 0
ENDIF
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
RETURN 1

