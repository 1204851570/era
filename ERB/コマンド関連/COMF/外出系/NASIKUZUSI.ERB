@ONABARE(ARG)
#DIM なし崩し必要値
#DIM どうぞそのまま好感度上昇値
#DIM TARGET保存

CFLAG:ARG:最後に会った日 = DAY
;EVENT口上を直接呼ぶのでTARGETを捕捉
TARGET保存 = TARGET
TARGET = ARG

IF !TALENT:ARG:処女 && ABL:ARG:Ａ感覚 >= ABL:ARG:Ｖ感覚
	LOCAL:2 = 1
ELSE
	LOCAL:2 = 0
ENDIF


PRINTFORMW \@CFLAG:MASTER:311 == CFLAG:ARG:311? 在房间外窥探里面的情况# 因为门没锁就擅自进去了\@
PRINTFORMW 听到了苦闷的声音……

;口上チェック
TRYCCALLFORM M_KOJO_EVENT_K{ARG}_ONABARE_1(ARG)
	
CATCH
	PRINTFORMW 没能察觉到自己被偷窥、%CALLNAME:ARG%沉溺于自慰
	PRINTFORM 衣服没能挡住下腹部的%CALLNAME:ARG%、
	IF TALENT:ARG:恋慕
		PRINTFORM 念着%CALLNAME:MASTER%的名字
	;気弱系,真面目系
	ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
		PRINTFORM 压抑声音
	;強気系
	ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
		PRINTFORM 发出甜美的声音
	ENDIF
	PRINTFORMW 的同时、慢慢地用手指玩弄着

	;デカパイ
	IF TALENT:ARG:胸圍 > 0
		PRINTFORM 非常巨大且在晃动的乳房
	;ちっぱい
	ELSEIF TALENT:ARG:胸圍 < 0
		PRINTFORM 晃晃悠悠的贫乳
	;普通
	ELSE
		PRINTFORM 巨大且在晃动的乳房
	ENDIF
	PRINTFORMW 、%CALLNAME:ARG%的手指漸漸往大腿深処摸去
	PRINTFORMW 就這樣把中指插入\@ !LOCAL:2 ? 秘処 # 肛門 \@的樣子讓%CALLNAME:MASTER%垂涎欲滴
	PRINTFORMW 這時、%CALLNAME:ARG%突然抬頭看向了門口
	PRINTFORML 注意到有来客的%CALLNAME:ARG%浑身僵直、就那么愣愣的看着%CALLNAME:MASTER%、连遮掩身体都忘了……
ENDCATCH

CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,0,0)
PRINTFORMW 
$LOOP

PRINTFORML 怎麼辦？
CALL ASK_M("失禮了",1,"請在我的注視下繼續做吧",1,"比起一个人來說两个人更加舒服哦",BASE:MASTER:精力 > MAXBASE:MASTER:精力 / 5)
IF RESULT == 0
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %CALLNAME:MASTER%急忙鑽進了被窩
	ELSE
		PRINTFORML %CALLNAME:MASTER%把眼前的景象深深的印在脑海里的基础上、在动摇的%CALLNAME:ARG%清醒前说了一声道歉、离开了
	ENDIF
	SIF !CFLAG:ARG:被掌握把柄
		PRINTFORML 掌握了%CALLNAME:ARG%的【把柄】
	CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,1,0)
	CFLAG:ARG:オナバレ = 1
	CFLAG:ARG:被掌握把柄 = 1
	TIME += 5
ELSEIF RESULT == 1
	なし崩し必要値 = 抱き寄せ必要値(ARG) - 50 + TALENT:ARG:羞恥心 * 50 - ABL:ARG:露出癖 * 30 - CFLAG:ARG:积攒度 / 20
	SIF  TALENT:ARG:自尊心 > 0
		なし崩し必要値 += 100
	SIF CFLAG:ARG:既成事実 & 合意_诶嘿嘿
		なし崩し必要値 -= 50
	SIF TALENT:ARG:胆量 < 0
		なし崩し必要値 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:合意判定,50)
	CALL SHOW_REASONWALL(なし崩し必要値 ,50)
	;失敗
	IF CFLAG:ARG:合意判定 <= なし崩し必要値
		GOTO GOOUT
	;成功
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,2,0)
		;口上チェック
		TRYCCALLFORM M_KOJO_EVENT_K{NO:ARG}_ONABARE_2(LOCAL:2)
		CATCH
			PRINTFORM 片刻后
			IF TALENT:ARG:恋慕
				PRINTFORM 脸上浮现出高兴和困惑的表情
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORM 害羞的扭扭捏捏的
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORM 呆呆的一副为难的表情
			ENDIF
			PRINTFORML %CALLNAME:ARG%轻轻的叹了口气
			IF TALENT:ARG:恋人
				PRINTFORML 脫掉了上半身的衣服、对着%CALLNAME:MASTER%大大的张开脚
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORML 害羞的稍微袒露出身体、脚紧紧的合着
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORML 虽然脱下了衣服、但是关键的地方还是用手遮掩着
			ENDIF
			PRINTFORML 就这様、%CALLNAME:ARG%在%CALLNAME:MASTER%的催促下
			PRINTFORMW 暗暗的叹了一口气、%CALLNAME:ARG%慢慢的开始动起手来

			PRINTFORML 插入中指的\@ !LOCAL:2 ? 膣口 # 菊座 \@发出了水声
			;デカパイ
			IF TALENT:胸圍 > 0
				PRINTFORML 用手指来回抚摸抓着巨大的乳房
			;ちっぱい
			ELSEIF TALENT:胸圍 < 0
				PRINTFORML 用手指触摸着立起来的乳头
			;普通
			ELSE
				PRINTFORML 用手来回搓胸部
			ENDIF
			PRINTFORMW 在这个煽情的景象中、开始响起了下流而甘甜的喘气声
			IF TALENT:ARG:恋人
				PRINTFORML 看着腆着脸、一边叫着%CALLNAME:MASTER%的名字、一边激烈的动起手指
				PRINTFORML 黏糊糊的、上下的口都留着口水、出神的表情的%CALLNAME:ARG%
				PRINTFORMW 、%CALLNAME:MASTER%不禁咽下口水
				PRINTFORML 慢慢的用手揉起胸部、\@ !LOCAL:2 ? 插着膣口 # 插着菊座 \@的手指动作变快了
				PRINTFORMW 不一会、叫着%CALLNAME:MASTER%的名字全身颤抖的绝顶了
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORML 愉快的观察着有点害臊的弄着胸部和\@ !LOCAL:2 ? 秘処 # 肛門 \@的%CALLNAME:ARG%
				PRINTFORML 而且透过些许发红的皮肤可以看到乳头
				PRINTFORMW 慢慢的、声音变大了、脚一点点的张开来了
				PRINTFORML 已经无力去在意%CALLNAME:MASTER%的视线了、开始控制不住自己了
				PRINTFORML 粘稠的爱液一点点的流出、大腿不时的抽搐着
				PRINTFORMW 太过突然的潮吹使得%CALLNAME:ARG%一下子攀上了快感的巅峰
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORML 说完动手后%CALLNAME:MASTER%直盯盯的看着、%CALLNAME:ARG%
				PRINTFORML 轻轻的摆弄着胸部和\@ !LOCAL:2 ? 秘処 # 肛門 \@、发出奇怪的声音
				PRINTFORMW 看到这边不满的様子、似乎明白了什么、叹了口气、%CALLNAME:ARG%张大了腿
				PRINTFORML 点点滴落的爱液和突起的陰蒂看起来十分的煽情
				PRINTFORML 感受到目光、%CALLNAME:ARG%的手指缓缓的加快了
				PRINTFORMW 手指突然深深的刺入\@ !LOCAL:2 ? 膣腔 # 肛腔 \@后、%CALLNAME:ARG%高潮了
			ENDIF
		ENDCATCH
	ENDIF
	PRINTFORML 哆哆嗦嗦、无法控制的大腿内侧和浑身颤抖的%CALLNAME:ARG%、
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %CALLNAME:MASTER%把浑身脱力的她放上床上盖上被子
	ELSE
		PRINTFORML 筋疲力尽的様子让%CALLNAME:MASTER%无法动手、直接回去了
		IF !CFLAG:ARG:没穿内裤 && EQUIP:ARG:下半身内衣２ && CFLAG:MASTER:约会中 != MAIN_MAP
			PRINTFORML 难得来一趟、要带点礼物回去么？
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 回去的时候、%CALLNAME:MASTER%把%CALLNAME:ARG%的%PANTSNAME(EQUIP:ARG:下半身内衣２, ARG)%放入怀里
				CALL PANTS_GET(ARG)
			ENDIF
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 3
	どうぞそのまま好感度上昇値 = ABL:ARG:親密 * CFLAG:ARG:积攒度 / 250 * MIN(ABL:ARG:露出癖,10)
	CALL CHANGE_CFLAG(2,ARG,どうぞそのまま好感度上昇値)
	CALL CHANGE_CFLAG(4,ARG,どうぞそのまま好感度上昇値 * -1 / 20)
	CALL JUEL_UP_SPECIAL(ARG,"恥情","自慰中毒",30,1)
	CALL JUEL_UP_SPECIAL(ARG,"欲情","自慰中毒",30,1)
	CALL ADD_EXP_LESNIAN(CFLAG:ARG:积攒度 / 100, ARG, "PRINT")
	EXP:ARG:露出快楽経験 += CFLAG:ARG:积攒度 / 200 * ABL:ARG:露出癖
	EXP:ARG:自慰経験 +=  CFLAG:ARG:积攒度 / 200 * ABL:ARG:露出癖
	TIME += 27
ELSEIF RESULT == 2
	なし崩し必要値 = 抱き寄せ必要値(ARG) - 150 + TALENT:ARG:羞恥心 * 30 - CFLAG:ARG:积攒度 / 20
	SIF  TALENT:ARG:自尊心 > 0
		なし崩し必要値 += 50
	SIF CFLAG:ARG:既成事実 & 合意_诶嘿嘿
		なし崩し必要値 -= 50
	SIF TALENT:ARG:胆量 < 0
		なし崩し必要値 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:合意判定,50)
	CALL SHOW_REASONWALL(なし崩し必要値 ,50)
	$ GOOUT
	IF CFLAG:ARG:合意判定 <= なし崩し必要値
		PRINTFORML %CALLNAME:ARG%不讲理的把%CALLNAME:MASTER%赶了出去
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,3,0)
		SIF !CFLAG:ARG:被掌握把柄
			PRINTFORML 掌握了%CALLNAME:ARG%的【把柄】
		CALL GET_ANGRY(ARG,1)
		CFLAG:ARG:オナバレ = 1
		CFLAG:ARG:被掌握把柄 = 1
		TIME += 5
	ELSE
		CALL NASIKUZUSI(ARG,LOCAL:2)
		CFLAG:ARG:ヤラせちゃった = 1
	ENDIF
ENDIF
TARGET = TARGET保存

@NASIKUZUSI(ARG,ARG:1)
#DIM 前戯１
;1=C 2=B 3=M
#DIM 前戯２
#DIM 本番はどっちか
#DIM 注入量
#DIM 基本注入量
#DIM 好感度上昇量
#DIM 信頼低下量
#DIM 回数
#DIM 経験増加数,58
;０ならＶ、１ならＡ
VARSET 経験増加数
CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,4,0)
回数 = MAX(MIN(ABL:ARG:欲望 / 2, 5) + CFLAG:ARG:积攒度 / 200,1)
注入量 = 0
IF TALENT:ARG:無接吻経験
	前戯１ = 3
	経験増加数:4 += 3 + ABL:ARG:Ｍ感覚 * 2
	経験増加数:25 = 経験増加数:4
	経験増加数:27 = 経験増加数:4
	EXP:MASTER:Ｍ経験 += 経験増加数:4
	EXP:MASTER:接吻経験 += 経験増加数:4
	EXP:MASTER:口淫経験 += 経験増加数:4
ELSEIF !RAND:3
	前戯１ = 3
	経験増加数:4 += 3 + ABL:ARG:Ｍ感覚 * 2
	経験増加数:27 = 経験増加数:4
ELSEIF ABL:ARG:Ｃ感覚 >= ABL:ARG:Ｂ感覚
	前戯１ = 1
	経験増加数:0 += RAND:(回数 * 2) 
	経験増加数:5 += 経験増加数:0 / 2
	経験増加数:10 += 経験増加数:0 / 2
ELSE
	前戯１ = 2
	経験増加数:3 += RAND:(回数 * 2) 
	経験増加数:8 += 経験増加数:3 / 2
	経験増加数:10 += 経験増加数:0 / 2
ENDIF
前戯２ = RAND:3
本番はどっちか = ARG:1

SIF 本番はどっちか == 1
	前戯２ += 10
IF 本番はどっちか
	基本注入量 = (ABL:ARG:肛 + 1) * 20 + BASE:MASTER:精力 /25
	経験増加数:21 += 回数
	経験増加数:17 += 回数
	経験増加数:10 += 回数
	経験増加数:7 += 2 * 回数
	経験増加数:2 += 回数 * 2
	EXP:MASTER:挿入経験 += 回数
	EXP:MASTER:射精経験 += 回数
	EXP:MASTER:絶頂経験 += 回数
	
ELSE
	基本注入量 = (ABL:ARG:膣 + 1) * 20 + BASE:MASTER:精力 / 25
	SIF TALENT:ARG:処女 == 1
		基本注入量 += 100
	経験増加数:20 += 回数
	経験増加数:16 += 回数
	経験増加数:10 += 2 * 回数
	経験増加数:6 += 2 * 回数
	経験増加数:1 += 2 * 回数
	EXP:MASTER:挿入経験 += 回数
	EXP:MASTER:射精経験 += 回数
	EXP:MASTER:絶頂経験 += 回数
ENDIF


FOR LOCAL,1,回数
	注入量 += 基本注入量 + RAND:(基本注入量)
NEXT

注入量 = MIN(注入量, BASE:MASTER:精力)

SIF !HAS_PENIS(MASTER)
	注入量 = 0
IF !本番はどっちか
	CALL ADD_SAMEN_V(ARG, 注入量)
ELSE
	CALL ADD_SAMEN_A(ARG, 注入量)
ENDIF

;口上チェック
TRYCCALLFORM M_KOJO_EVENT_K{NO:ARG}_ONABARE_3(前戯１,本番はどっちか,回数,注入量)
CATCH
	;以下デフォルト地の文
	PRINTFORML %CALLNAME:MASTER%看著動搖的%CALLNAME:ARG%冒失的闖了進來還關上了門、
	PRINTFORMW 抱住%CALLNAME:ARG%打聽她現在想要什麼
	PRINTFORM 然後按照她的期望、

	SELECTCASE 前戯１
		CASE 1
			PRINTDATA
				DATAFORM 捏着陰蒂
				DATAFORM 用手指弹陰蒂
				DATAFORM 用手揉捏着陰蒂
				DATAFORM 舔着陰蒂
			ENDDATA
		CASE 2
			PRINTDATA
				DATAFORM 用手指捏着乳头
				DATAFORM 爱抚着胸
				DATAFORM 用手指揉搓着乳房
				DATAFORM 用舌头舔着乳头
				DATAFORM 轻柔的咬着乳头
			ENDDATA
		CASE 3
			PRINTDATA
				DATAFORM 抚摸着头发
				DATAFORM 悄悄的
				DATAFORM 热情地
			ENDDATA
	ENDSELECT
	IF 前戯１ == 3
		IF TALENT:ARG:無接吻経験
			PRINTFORM 把初吻夺走了
		ELSE
			PRINTFORM 接吻
		ENDIF
	ENDIF
	PRINT 、

	SELECTCASE 前戯２
		CASE 0
			PRINTFORM 抚摸着陰唇的周围
		CASE 1
			PRINTFORM 手指微微的插入腔口
		CASE 2
			PRINTFORM 用舌头舔着陰道口
		CASE 10
			PRINTFORM 细心的揉搓着屁股
		CASE 11
			PRINTFORM 抚摸着肛門周围
		CASE 12
			PRINTFORM 手指微微的插入肛門
	ENDSELECT
	PRINT 、

	PRINTDATA
		DATAFORM 彻底的挑逗了
		DATAFORM 彻底的弄去了
		DATAFORM 用下流的语言责备
	ENDDATA
	PRINTW
	PRINTFORM それから%TEXTR("四肢扒着。让人从背后像野兽一様的上/从正面拥抱/まんぐり返しの姿勢を取らせ/从正面拥抱")%
	PRINTDATA
		DATAFORM 诉说着甜言蜜语
		DATAFORM 温柔的
		DATAFORM 粗暴的
		DATAFORM 纠缠着
	ENDDATA

	IF TALENT:ARG:処女 && !本番はどっちか
		PRINTFORM 夺走了純潔、
	ELSE
		PRINTFORM {回数}度%TEXTR("犯し/交わり/貫き")%、
	ENDIF
	PRINTFORMW \@本番はどっちか ? 肛門# 膣道\@內被注入了{注入量}精液……

	;以上デフォルト地の文
ENDCATCH

IF 前戯１ == 3 
	SIF TALENT:MASTER:無接吻経験
		CALL SET_FIRSTKISS(MASTER, ARG, 2)
	SIF TALENT:ARG:無接吻経験
		CALL SET_FIRSTKISS(ARG, MASTER, 2, "就那様")
ENDIF
IF TALENT:ARG:処女 == -1 && !本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【無自覚非処女喪失】
	TALENT:ARG:処女 = 0
ENDIF
IF TALENT:ARG:処女 == 1 && !本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【処女喪失】
	CALL SET_HISTORY_LOST_V(ARG, MASTER, "阴茎", "自室", "就那様")
ENDIF
IF !CFLAG:ARG:尻穴処女喪失記念日 && 本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【尻穴処女喪失】
	CALL SET_HISTORY_LOST_A(ARG, MASTER, "阴茎", "自室", "就那様")
ENDIF
SETCOLOR C_YELLOW
FOR LOCAL,0,58
	IF 経験増加数:LOCAL
		PRINTFORML %EXPNAME:LOCAL% ＋{経験増加数:LOCAL}
		EXP:ARG:LOCAL += 経験増加数:LOCAL
	ENDIF
NEXT
RESETCOLOR
CALL JUEL_UP_SPECIAL(ARG,"欲情","欲望",20,1)
SELECTCASE 前戯１
;C
	CASE 1
		CALL JUEL_UP_SPECIAL(ARG,"快Ｃ","Ｃ感覚",10,1)
;B
	CASE 2
		CALL JUEL_UP_SPECIAL(ARG,"快Ｂ","Ｂ感覚",10,1)
;M
	CASE 3
		CALL JUEL_UP_SPECIAL(ARG,"快Ｍ","Ｍ感覚",20,1)
ENDSELECT
IF 本番はどっちか
	CALL JUEL_UP_SPECIAL(ARG,"快Ａ","Ａ感覚",15,1)
ELSEIF TCVAR:ARG:破瓜
	CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",100,1)
ELSE
	CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",15,1)
ENDIF
CALL ADD_EXP_LESNIAN(回数 * 5, ARG, "PRINT")
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,5,0)
TIME += 回数 * 20
EXP:MASTER:清掃経験 ++
DOWNBASE:MASTER:体力 = 回数 * 100
DOWNBASE:MASTER:気力 = 回数 * 150
好感度上昇量 = 回数 * ABL:ARG:親密 * 5
信頼低下量 = 好感度上昇量 / 5
SIF TALENT:MASTER:謎之魅力 || TALENT:MASTER:魅惑
	信頼低下量 /= 2
IF TALENT:ARG:思慕 || TALENT:ARG:炮友
	信頼低下量 /= 2
	SIF TALENT:ARG:快感応答 == 1
		信頼低下量 /= 2
ENDIF
IF TALENT:ARG:恋慕 || TALENT:ARG:愛欲
	信頼低下量 /= 4
	SIF TALENT:ARG:快感応答 == 1
		信頼低下量 = 0
ENDIF

CALL CHANGE_CFLAG(2,ARG,好感度上昇量)
SIF 信頼低下量 > 0
	CALL CHANGE_CFLAG(4,ARG,信頼低下量 * -1)
CFLAG:ARG:积攒度 = 0
TCVAR:ARG:破瓜 = 0

IF TALENT:ARG:恋慕 && CFLAG:MASTER:约会中 != MAIN_MAP
	PRINTFORML 行为结束的%CALLNAME:MASTER%想站起来、%CALLNAME:ARG%却还想在一起…
	CALL ASK_YN("尽管如此还是回去","在这住下")
	IF !RESULT
		GOTO BYEBYE
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,6)
		;お招きフラグにARGを代入
		CFLAG:ARG:招待 = ARG
		CFLAG:MASTER:招待 = ARG
		;初期位置をOMANEKIBEYA()=98にする
		CFLAG:ARG:初始位置 = OMANEKIBEYA()
		;移動させる
		CFLAG:ARG:現在位置 = OMANEKIBEYA()
		CFLAG:MASTER:現在位置 = OMANEKIBEYA()
		IF NEMUKE() < 720 && BASE
			PRINTFORML %CALLNAME:MASTER%今天一天都在%CALLNAME:ARG%的住所度过了
			PRINTFORML 一起睡了会、起床后打扫卫生、调情、做饭吃饭
			PRINTFORMW 再调情后、做爱然后睡着了
			VARSET 経験増加数
			IF 本番はどっちか
				注入量 = ABL:ARG:肛 * MIN(ABL:ARG:親密,15) * 10
				経験増加数:21 += MIN(ABL:ARG:親密,15)
				経験増加数:17 += MIN(ABL:ARG:親密,15)
				経験増加数:7 += MIN(ABL:ARG:親密,15) * 2
				経験増加数:2 += MIN(ABL:ARG:親密,15) * 2
			ELSE
				注入量 = ABL:ARG:膣 * MIN(ABL:ARG:親密,15) * 10
				経験増加数:20 += MIN(ABL:ARG:親密,15)
				経験増加数:16 += MIN(ABL:ARG:親密,15)
				経験増加数:6 += 2 * MIN(ABL:ARG:親密,15)
				経験増加数:1 += 2 * MIN(ABL:ARG:親密,15)
			ENDIF
			経験増加数:41 =  MIN(ABL:ARG:親密,15) * 2
			注入量 = MIN(注入量, BASE:MASTER:精力)
			CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",15)
			IF !本番はどっちか
				CALL ADD_SAMEN_V(ARG, 注入量)
				CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",10,1)
			ELSE
				CALL ADD_SAMEN_A(ARG, 注入量)
				CALL JUEL_UP_SPECIAL(ARG,"快Ａ","Ａ感覚",10,1)
			ENDIF
			FOR LOCAL,0,58
				IF 経験増加数:LOCAL
					PRINTFORML %EXPNAME:LOCAL% ＋{経験増加数:LOCAL}
					EXP:ARG:LOCAL += 経験増加数:LOCAL
				ENDIF
			NEXT
			CALL ADD_EXP_LESNIAN(ABL:ARG:親密 * 2,ARG, "PRINT")
			好感度上昇量 = ABL:ARG:親密 * 10
			EXP:MASTER:清掃経験 ++
			EXP:MASTER:料理経験 ++
			TIME += 720 - NEMUKE()
		ELSE
			PRINTFORML 太困了、就那様和%CALLNAME:ARG%一起睡着了
			CALL ADD_EXP_LESNIAN(ABL:ARG:親密,ARG, "PRINT")
			好感度上昇量 = ABL:ARG:親密 * 5
		ENDIF
		CALL CHANGE_CFLAG(2,ARG,好感度上昇量)

		SIF GETBIT(TALENT:MASTER:性別, 1)
			DOWNBASE:MASTER:精力 = 注入量

		CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
		BEGIN AFTERTRAIN
		RETURN 1
	ENDIF
ENDIF

;TWあなたは片づけせずにはいられない
$BYEBYE

SIF GETBIT(TALENT:MASTER:性別, 1)
	DOWNBASE:MASTER:精力 = 注入量
PRINTFORM 後始末をした%CALLNAME:MASTER%は、\@ 本番はどっちか? 尻穴# 秘裂\@から\@TCVAR:ARG:破瓜 ? 破瓜血混じりの# \@白濁を溢れさせて
PRINTFORML \@ TALENT:ARG:回復速度? こちらを上気した表情で看了过来#呆ける \@%CALLNAME:ARG%に、

IF ABL:ARG:精液中毒 >= (3 + 本番はどっちか * 2)
	PRINTFORML 説道用嘴巴把這個打掃乾淨然後把\@TCVAR:ARG:破瓜 ?還沾著她的純潔之血的#滿是愛液的 \@肉棒插入了%CALLNAME:ARG%口中
	PRINTFORMW %CALLNAME:ARG%は\@ TALENT:ARG:回復速度 > 0 ? 蕩けた表情で精心地#ぼんやりした表情で \@精液を舐めとっている……
	SETCOLOR C_YELLOW
	PRINTFORML 精液経験 ＋{ABL:ARG:精液中毒}
	EXP:ARG:精液経験 += ABL:ARG:精液中毒
	RESETCOLOR
	CALL JUEL_UP_SPECIAL(ARG,"屈服","精液中毒",5,1)
	PRINTFORM 完全滿足了的%CALLNAME:MASTER%、
ENDIF

PRINTFORMW 説道\@ CFLAG:MASTER:约会中 == MAIN_MAP? 晚安# 做得非常好、下次來%GET_MAPNAME(MAIN_MAP)%吧\@後\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ? 睡著了# 離開了\@
CFLAG:ARG:オナバレ = 2
IF !CFLAG:ARG:没穿内裤 && CFLAG:MASTER:311 != CFLAG:ARG:311 && CFLAG:MASTER:约会中 != MAIN_MAP && PANTSNAME(EQUIP:ARG:下半身内衣２, ARG) != "没穿内裤"
	PRINTFORML 难得来一趟、要带点礼物回去么？
	CALL ASK_YN
	IF !RESULT
		PRINTFORML 回去的时候、%CALLNAME:MASTER%把%CALLNAME:ARG%的%PANTSNAME(EQUIP:ARG:下半身内衣２, ARG)%放入怀里
		CALL PANTS_GET(ARG)
	ENDIF
ENDIF
