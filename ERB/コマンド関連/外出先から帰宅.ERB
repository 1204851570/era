@从外面回家
#DIM DIST_TO_HOME
#DIM ReqTSP

ReqTSP = TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中) * 3
IF !FLAG:约会的对象 && !FLAG:70 && BASE:MASTER:TSP >= ReqTSP
	PRINTL 消費TSP、可以不消耗時間帰宅。
	PRINTL 要使用TSP吗？
	CALL ASK_YN
	IF !RESULT
		IF BASE:MASTER:TSP < ReqTSP
			PRINTW TSP不足
		ELSE
			BASE:MASTER:TSP -= ReqTSP
			GOTO WARP
		ENDIF
	ENDIF
ENDIF
IF !FLAG:70
	CALL ENEMY_ENCOUNT(CFLAG:MASTER:约会中)
	CALL 帰宅の時間経過
ELSEIF BASE:MASTER:TSP < ReqTSP
	PRINTW TSP不足無法在時間停止的時候回去了
	RETURN -1
ELSE
	BASE:MASTER:TSP -= ReqTSP
ENDIF

$WARP
IF CFLAG:負け犬 && TALENT:路人 == 1
	PRINTFORML %CALLNAME:TARGET%を携带しました
ELSE
	PRINTFORMW 從\@ CFLAG:MASTER:约会中 != CFLAG:TARGET:约会中 || TARGET == MASTER ? 外出 # 约会 \@中回來了
ENDIF
CALL 守矢神簽约会的对象のリセット()
FOR LOCAL,1,CHARANUM
	IF CHK_DATENOW(CFLAG:LOCAL:约会中)
		CALL DATE_EVENT(LOCAL)
		CALL SET_DATE(99,LOCAL)
	ENDIF
NEXT
SIF CHK_DATENOW(CFLAG:MASTER:约会中)
	CALL SET_DATE(99,MASTER)

CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)

@帰宅の時間経過
;残り時間算出
LOCAL = (GET_DATE_PLACE(CFLAG:MASTER:現在位置)) * 10
IF LOCAL && (TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中) - LOCAL) > 0
	;道中の場合
	TIME = TIME + (TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中) - LOCAL)
ELSE
	;デート先到着済みの場合
	TIME = TIME + TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:约会中)
ENDIF
CALL 守矢神簽约会的对象のリセット()

@外出先から誘えるか(ARG)
LOCAL = FLAG:约会的对象
IF CFLAG:ARG:行動 == 5
	PRINTFORMW %CALLNAME:ARG%は今仕事中だ
	RETURN 0
ENDIF
IF LOCAL != ARG && LOCAL != MASTER
	PRINTFORM %CALLNAME:LOCAL%
	;気弱系
	IF CFLAG:LOCAL:性格傾向 == 1
		PRINTFORML 抓緊了裙角朝著%CALLNAME:MASTER%搖了搖頭
	;強気系
	ELSEIF CFLAG:LOCAL:性格傾向 == 2
		PRINTFORML 眼光一閃凝視著%CALLNAME:MASTER%
	;素直系
	ELSEIF CFLAG:LOCAL:性格傾向 == 3
		PRINTFORML 凝視著%CALLNAME:MASTER%訴說到一直想要見面
	;真面目系
	ELSEIF CFLAG:LOCAL:性格傾向 == 4
		PRINTFORML 対著%CALLNAME:MASTER%說道那是不行的
	;その他
	ELSEIF CFLAG:LOCAL:性格傾向 == 5
		PRINTFORML 対著%CALLNAME:MASTER%歎息一聲、搖了搖頭
	ENDIF
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ARG%放棄了邀請
	RETURN 0
ENDIF
IF !VISIT(ARG)
	PRINTFORMW 今は駄目だと言われた
	RETURN 0
ENDIF
IF ARG == 93 && !ITEM:簡易泳池
	PRINTW 没办法带她出去…
	RETURN 0
ELSEIF BASE:ARG:気力 < 500
	PRINTFORMW %CALLNAME:ARG%好像太累了
	RETURN 0
ELSEIF !CHARA_HOLIDAY(ARG) && BASE:ARG:工作量 && TCVAR:ARG:工作開始 < TIME + 180
	PRINTFORMW %CALLNAME:ARG%之後還有工作
	RETURN 0
ELSE
	CFLAG:ARG:拠点来訪 = 1
	RETURN 1
ENDIF


;FileName_DATE_CMN.ERB ------------------------------- Rev1.00
;デートに誘えるか
;-----------------------------------------------------------
@CAN_DATE(ARG)
#FUNCTION

SIF !CHK_DATENOW(CFLAG:MASTER:约会中)
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF 0
IF !CFLAG:ARG:負け犬
	SIF !CFLAG:ARG:態度 
		RETURNF 0
	SIF CFLAG:ARG:ブチギレ
		RETURNF 0
ENDIF

SIF CHK_DATENOW(CFLAG:ARG:约会中)
	RETURNF 0
SIF (!TALENT:TARGET:思慕 && !TALENT:TARGET:恋慕 && !TALENT:TARGET:愛欲 && TARGET != RANDOM_CHARANUM)
	RETURNF 0
RETURNF 1

