;FileName_USERCOM.ERB ------------------------------ Rev1.00
;コマンド判定処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@USERCOM
#DIM 能力表示用ダミー
#DIM 装備部位
#DIM 胖次消費
#DIM なだめ判定
$LOOP_USERCOM
;一般コマンド(COMF)
SIF MEETCHECKDLC
	MEETTIME:0:TARGET = DAY
IF RESULT >= 0 && RESULT < 800
	;連れ出す判定
	IF RESULT == 351 && !CHK_DATENOW(CFLAG:MASTER:约会中)
		CALL COM_ABLE351
		SIF !RESULT
			RETURN 0
		PRINTL 带出去
		STR:0 = 帯出去
		CALL COM351
		RETURN 0
	ENDIF
	IF DEBUGGERR && RESULT == 666
		CALL DEBUG_MODE
		RETURN 0
	ENDIF
	;コマンド登録チェック
	SIF TRAINNAME:RESULT == ""
		RETURN 0
	;SELECTCOMとTFLAG:3(SELECTCOM退避)に入力値セット
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	;一部コマンドの特殊制限
	SELECTCASE SELECTCOM
		CASE 100 TO 107,140 TO 145
			IF TALENT:TARGET:妊娠
				PRINTW 不能对孕妇做这种事
				RETURN 0
;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 4)
;				CALL ＳＭ許可
			ENDIF
		CASE 60 TO 71
			IF TALENT:TARGET:妊娠 && (CFLAG:TARGET:903 <= 30 || CFLAG:TARGET:903 >= 71)
				PRINTFORMW 现在最好不要对%CALLNAME:TARGET%乱来比较好
				RETURN 0
;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 3)
;				CALL Ａ許可
			ENDIF
	ENDSELECT
	SIF GLOBAL_COMABLE2(SELECTCOM)
		RETURN 0
	;コマンド使用可能判定
	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		RETURN 0
	;実行制限(実行拒否等)チェック
	TRYCALLFORM CAN_COM{SELECTCOM}(1)
	IF RESULT == 0 && !CFLAG:負け犬 && !TCVAR:强行
		;実行値表示のため再度CAN_COM
		TRYCALLFORM CAN_COM{SELECTCOM}(0)
		RETURN 0
	ENDIF
	;ダミーTRAIN
	DOTRAIN 999
;履歴表示（未実装）
ELSEIF RESULT == 800
	CALL SHOW_EQUIP
	TFLAG:Info表示中 = 0
	RETURN 1
;汚れ表示
ELSEIF RESULT == 801
	CALL STAIN_INFO
	TFLAG:Info表示中 = 0
	RETURN 1
;TARGET変更
ELSEIF RESULT == 802
	IF !CFLAG:陪睡中
		CALL TARGET_CHANGE
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
;TARGETの状態表示
ELSEIF RESULT == 803
	IF TARGET == MASTER
		CALL SINGLE_CHARA_STATE(MASTER, "「調教中」")
	ELSE
		CALL TARGET_LIST_STATE
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;MASTERの状態表示
ELSEIF RESULT == 804
	CALL SINGLE_CHARA_STATE(MASTER, "「調教中」")
	TFLAG:Info表示中 = 0
	RETURN 1
;コマンドフィルタ
ELSEIF RESULT == 809
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 815 && FLAG:70 && !CFLAG:TARGET:睡眠 && AT_HOME(MASTER)
	IF BATHCHECK(CFLAG:MASTER:現在位置) || IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTFORMW 果然在这个地方不太合适
	ELSEIF TFLAG:運搬 != TARGET
		PRINTFORMW %CALLNAME:MASTER%背起了%CALLNAME:TARGET%
		TFLAG:運搬 = TARGET
	ELSE
		PRINTFORMW %CALLNAME:MASTER%放下了%CALLNAME:TARGET%
		TFLAG:運搬 = 0
	ENDIF
ELSEIF RESULT == 816
	FLAG:画像表示 = !FLAG:画像表示
;集合
ELSEIF RESULT == 817 && FLAG:70 && TALENT:MASTER:集合 && AT_HOME(MASTER) && !CFLAG:诶嘿嘿
	CALL SYUGO()
ELSEIF RESULT == 817
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 818
	CALL SHOW_GATHERING_LIST_SELECT(GATHERING_SPOT(CFLAG:MASTER:現在位置))
;服を脱がせる
ELSEIF RESULT == 820 && EQUIP:0 && !(TFLAG:102 == 3 && TFLAG:抱住模式)
	CALL 脱衣服
;下着をくすねるコマンドの切り離し
ELSEIF RESULT == 821 && EQUIP:0 && EQUIP:TARGET:下半身内衣２
	IF !TARGET && !FLAG:70 && !CFLAG:MASTER:恶作剧
		CALL SIMATTYAUOJISAN
	ELSE
		SIF CFLAG:TARGET:291 == 0
			CALL 脱衣処理(TARGET, "くすねる")
	ENDIF
;上半身下着を脱がす
ELSEIF RESULT == 822 && EQUIP:TARGET:0 && EQUIP:TARGET:上半身内衣１ != 0
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		IF EQUIP:TARGET:上半身外衣１ || EQUIP:TARGET:上半身外衣２ || EQUIP:TARGET:連衣裙 || EQUIP:TARGET:和服 || EQUIP:TARGET:連体内衣
			PRINTFORM 将%CALLNAME:TARGET%的%SHOW_TOP(TARGET,1)%袒露开来、
		ELSE
			PRINTFORM %CALLNAME:TARGET%の
		ENDIF
		PRINTFORMW %CLOTHNAME(7,EQUIP:TARGET:上半身内衣１)%直接脫掉
		EQUIP:TARGET:上半身内衣１ = 0
		TEQUIP:TARGET:上半身着衣状況 = 1
		TEQUIP:TARGET:上半身裸露状態 = 1
		EQUIP:MASTER:上半身内衣１ = 0
		TEQUIP:MASTER:上半身着衣状況 = 1
		TEQUIP:MASTER:上半身裸露状態 = 1
		RETURN 0
	ENDIF
;下着姿にする
ELSEIF RESULT == 823 && EQUIP:0 && (EQUIP:TARGET:上半身外衣１ || EQUIP:TARGET:上半身外衣２ || EQUIP:TARGET:連衣裙 || EQUIP:TARGET:和服 || EQUIP:TARGET:外衣)
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		CALL 脱衣処理(TARGET, "内衣姿にする")
		CALL 脱衣処理(MASTER, "内衣姿にする")
	ENDIF
;身体測定する
ELSEIF RESULT == 824 && MEASUREMENTCHECK(CFLAG:TARGET:現在位置)
	CALL ASK_M(@"%CALLNAME:MASTER%を測定",1,@"%CALLNAME:TARGET%を測定",1,"やめる",1)
	SELECTCASE RESULT
		CASE 0
			CALL SOMATOMETRY(MASTER)
		CASE 1
			CALL SOMATOMETRY(TARGET)
		CASE 2
	ENDSELECT
	RETURN 0
;下着収奪コマンドし
ELSEIF RESULT == 825 && TFLAG:胖次収奪
	IF !FLAG:70
		CALL SIMATTYAUOJISAN
	ELSE
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:LOCAL:没穿内裤 && EQUIP:LOCAL:下半身内衣２
				SIF !PANTS_ALREADY_COLLECTED(LOCAL)
					SETCOLOR C_PINK
				PRINTFORMW %CALLNAME:LOCAL%のはいていた%PANTSNAME(EQUIP:LOCAL:下半身内衣２, LOCAL)%を脱がして懐に入れた
				RESETCOLOR
				CALL PANTS_GET(LOCAL)
			ENDIF
		NEXT
		TFLAG:Info表示中 = 0
		RETURN 0
	ENDIF
ELSEIF RESULT == 826
	IF TEQUIP:挑逗モード
		TEQUIP:挑逗モード = 0
	ELSE
		IF TCVAR:MASTER:要去了
			PRINTFORMW 已经没有那么做的余裕了！
	 	ELSEIF (EX:20 + EX:21 + EX:22 + EX:23 + EX:24) > 0
	 		PRINTFORMW 在挑逗中
	 	ELSE
	 		TEQUIP:挑逗モード = 1
	 	ENDIF
	 ENDIF
;過激なイタズラ（未実装）
ELSEIF RESULT == 830 && CFLAG:恶作剧 < 2
	PRINTFORML %CALLNAME:MASTER%将%CALLNAME:TARGET%的衣服慢慢地袒露开来
	EQUIP:0 = 0
	SIF CFLAG:TARGET:睡眠姦 < 2
	CFLAG:TARGET:睡眠姦 = 2
	CALL SET_PRANK(2,TARGET,1)
	CALL 脱衣処理(TARGET, "全裸", "「表示なし」")
	FOR LOCAL,1,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	NEXT
	RETURN 1
;test
;ELSEIF RESULT == 99999
;	TIME:5 = 4
;	RETURN 1
;抜け出す
ELSEIF RESULT == 850
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	DOTRAIN 999
;居場所察知=============================================================================================================================
ELSEIF RESULT == 811 && TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:诶嘿嘿
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL HITOSAGASI()
	INPUT
	IF GROUPMATCH(RESULT, 0, CFLAG:MASTER:現在位置)
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;====================================================
ELSEIF RESULT == 812 && TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:诶嘿嘿
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL OHEYA_CHECK
	INPUT
	IF GROUPMATCH(RESULT, 0, CFLAG:MASTER:現在位置)
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
ELSEIF RESULT == 813 && GETBIT(TALENT:MASTER:劣情察知, 1) && !CFLAG:诶嘿嘿
	PRINTFORML 花费精力来寻找性欲高涨的少女。
	;CALL ASK_M("やめる",1,"発動",1,"触媒を使って発動",CSTR:MASTER:12 != "")
	CALL ASK_M("放棄",1,"発動",1,"使用触媒発動",FLAG:胖次輸送)
	SELECTCASE RESULT
		CASE 0
			RETURN -1
		CASE 1,2
			PRINTFORMW %CALLNAME:MASTER%\@RESULT == 2 ?对着脱下的胖次、 # \@集中精神！
			SETCOLOR 255,100,255
			FONTBOLD
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:积攒度 >= 500
					PRINTFORML %CALLNAME:LOCAL%欲求不满…！
					SIF CFLAG:LOCAL:現在位置 == SUKIMA() && RESULT == 2
						PRINTFORML 然后、%CALLNAME:LOCAL%现在在家里！　……大概。
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
			IF RESULT == 1
				BASE:MASTER:気力 -= 500
			ELSE
				PRINTFORMW %CALLNAME:MASTER%在放松下来的时候、手中的胖次慢慢的融化在空气中了
				CSTR:MASTER:12 =
				FLAG:胖次輸送 = 0
				VARSET PANTS_TEMP
				BASE:MASTER:気力 -= 50
			ENDIF
			SIF !FLAG:70
				TIME += 5
			TFLAG:Info表示中 = 0
			RETURN 1
	ENDSELECT

;フィルタ----------------------------
ELSEIF RESULT == 839
	FLAG:家事過濾器 = !FLAG:家事過濾器
	RETURN -1
ELSEIF RESULT == 4000
	CFLAG:自動喘息 ++
	SIF CFLAG:自動喘息 > 2
		CFLAG:自動喘息 = 0
	RETURN -1
ELSEIF RESULT >= 831 && RESULT <= 843
	CFLAG:TARGET:(RESULT - 830 + 590) = !CFLAG:TARGET:(RESULT - 830 + 590)
	RETURN -1
;未完成---------------------------- 

;ターゲット変更
ELSEIF RESULT >=1001 && RESULT < (CHARANUM + 1000) && CFLAG:(RESULT - 1000):同室
	;立ち絵種類切り替え
	SIF RESULT - 1000 == TARGET
		FLAG:立絵種類 = !FLAG:立絵種類
	TFLAG:104 = (RESULT - 1000)
;ストーキング対象切り替え
ELSEIF RESULT >=4001 && RESULT < (CHARANUM + 4000) && CFLAG:(RESULT - 4000):同室
	IF STALKED == RESULT - 4000
		STALKED = 0
	ELSE
		STALKED = RESULT - 4000
	ENDIF
;一日を終える
;ELSEIF RESULT == 888
;	BEGIN AFTERTRAIN
;神社に誘う
ELSEIF RESULT == 990 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CALL 帰宅の時間経過
		CALL SET_DATE(99,TARGET)
		CFLAG:TARGET:現在位置 = ENTRANCE
		CFLAG:MASTER:現在位置 = ENTRANCE
		CFLAG:TARGET:约会中 = MAIN_MAP
		CFLAG:MASTER:约会中 = MAIN_MAP
		CFLAG:TARGET:同行 = 60
		CFLAG:MASTER:同行 = 60
		IF TARGET == 93 && !GETBIT (FLAG:泳池, 0)
			TIME += 30
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 500,500)
		ENDIF
		IF CFLAG:負け犬
			PRINTFORMW 嫌がる%CALLNAME:TARGET%を強引に連れて帰った
		ELSE
			PRINTFORMW 带着有空的%CALLNAME:TARGET%一起回家了
		ENDIF
		RETURN 1
	ENDIF
;納戸施錠
ELSEIF RESULT == 991 
	IF CFLAG:MASTER:初始位置 == 53 && CFLAG:MASTER:現在位置 == 53
		FLAG:倉庫施錠 = !FLAG:倉庫施錠
		RETURN 0
	ELSEIF FLAG:倉庫施錠
		FLAG:倉庫施錠 = 0
		RETURN 0
	ENDIF
;デートに誘う
ELSEIF RESULT == 992 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CFLAG:MASTER:同行 = 60
		CFLAG:TARGET:同行 = 60
		CFLAG:TARGET:约会中 = CFLAG:MASTER:現在位置 / 100
		FLAG:约会的对象 = TARGET
		TFLAG:约会前好感度 = CFLAG:TARGET:好感度
		TIME += 1
		IF CFLAG:負け犬
			PRINTFORMW 嫌がる%CALLNAME:TARGET%を強引に連れ出した
		ELSE
			PRINTFORMW 带着有空的%CALLNAME:TARGET%去约会了
		ENDIF
	ENDIF
;カレンダー
ELSEIF RESULT == 993
	CALL 日历
ELSEIF RESULT == 994
	CALL SHOW_HAVE_COLLECTION()
ELSEIF RESULT == 995
	IF AT_HOME(MASTER)
		INVERTBIT FLAG:瞬間移動, 1
	ELSE
		INVERTBIT FLAG:瞬間移動, 0
	ENDIF
;時間停止解除
ELSEIF RESULT == 999 && FLAG:70 == 1 && !CFLAG:诶嘿嘿
	CALL 時間停止解除
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身裸露状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
		;身体測定終了
		SIF CFLAG:LOCAL:身体測定 == 2
			CFLAG:LOCAL:身体測定 = 1
	NEXT
;逢瀬終了
ELSEIF RESULT == 999 && CFLAG:诶嘿嘿
	SIF CFLAG:MASTER:恶作剧 == 2
		CALL SET_PRANK(0,TARGET,2)
	IF CFLAG:诶嘿嘿 == 2
			SELECTCOM = RESULT
			TFLAG:3 = SELECTCOM
			DOTRAIN 999
	ELSE
		IF FLAG:70 == 1
			FOR LOCAL,0,CHARANUM
				SIF CFLAG:诶嘿嘿 == 2
					CALL SET_DERAY(5,LOCAL)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ELSE
			CALL AFTER_AFFAIR
			FOR LOCAL,1,CHARANUM
				;うふふ終了口上呼び出し
				SIF CFLAG:LOCAL:诶嘿嘿 == 1 || (CFLAG:LOCAL:恶作剧 >= 1 && CFLAG:LOCAL:眠姦口上有)
					CALL KOJO_MESSAGE_SEND("EVENT",15,LOCAL,2,0)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ENDIF
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		TEQUIP:上半身裸露状態 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上着脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
		RETURN 1
	ENDIF
;イタズラ終了
ELSEIF RESULT == 999 && CFLAG:恶作剧 && !CFLAG:诶嘿嘿
	FOR LOCAL,0,CHARANUM
		CALL SET_PRANK(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身裸露状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;添い寝終了
ELSEIF RESULT == 999 && CFLAG:陪睡中
	FOR LOCAL,0,CHARANUM
		CALL SET_TOGETHER(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	CALL KOJO_MESSAGE_SEND("EVENT",16,TARGET,2,0)
	;衰弱なら寝る
	SIF CFLAG:TARGET:衰弱
		CALL CHARA_SLEEP, TARGET,0

	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身裸露状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;デート終了
ELSEIF RESULT == 999 && CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL 从外面回家
	RETURN 1
ELSEIF RESULT == 3000
	TFLAG:PALAM非表示 = !TFLAG:PALAM非表示
ELSEIF RESULT == 3001
	TFLAG:Status非表示 = !TFLAG:Status非表示
ELSEIF RESULT == 3002
	TFLAG:USERCOM非表示 = !TFLAG:USERCOM非表示
ELSEIF RESULT == 3003
	TFLAG:LOOK非表示 = !TFLAG:LOOK非表示
	
;リソース表示関連
ELSEIF RESULT == 3100
	;表示設定の切り替え
	FLAG:画像表示設定 = !FLAG:画像表示設定
ELSEIF RESULT == 3101
	;画像表示人数 -
	FLAG:画像表示 = MAX(FLAG:画像表示-1,1)
	
ELSEIF RESULT == 3102
	;画像表示人数 +
	FLAG:画像表示 = MIN(FLAG:画像表示+1,10)
	
ELSEIF RESULT == 3103
	;表示位置切り替え
	FLAG:画像表示位置 = !FLAG:画像表示位置

ELSEIF RESULT == 3104
	;枠表示 ON/OFF
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 3105
	FLAG:立絵種類 = !FLAG:立絵種類
ELSEIF RESULT == 3106
	isModifier = MAX(isModifier - 10, -50)
ELSEIF RESULT == 3107
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 9999
	FOR LOCAL,1,CHARANUM
		PRINTFORMLC %CALLNAME:LOCAL% 現在位置：{CFLAG:LOCAL:現在位置}
	NEXT
ENDIF
TFLAG:Info表示中 = 0
RETURN 0

;FileName_USERCOM.ERB ------------------------------ Rev1.00
;TARGET変更処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TARGET_CHANGE
PRINTFORML 做了%CALLNAME:TARGET%的对手
PRINTL [0] - 返回
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL% (好感度:{CFLAG:LOCAL:2})
NEXT
INPUT
IF RESULT <= 0 || RESULT >= CHARANUM
	RETURN 0
ELSEIF CFLAG:RESULT:現在位置 == CFLAG:MASTER:現在位置
	TFLAG:104 = RESULT
ENDIF

;----------------------------------------------------------
;FileName_USERCOM.ERB ------------------------------ Rev1.00
;履歴表示処理（未実装）
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_EQUIP
WAIT

;FileName_USERCOM.ERB ------------------------------ Rev1.00
;日常コマンドの成否判定
;CALL					USER
;ARG:0（条件最大値		コマンド事の成否条件の最大値
;ARG:1（成功値			コマンド事の成功値
;ARG:2（大成功補正値	コマンド事の大成功に対する補正値
;RETURN		VOID
;COMMENT	各コマンドに散らばっている成否判定を一括
;-------------------------------------------------
@SUC_FAIL(条件最大値,成功値,大成功補正値)
#DIM 条件最大値
#DIM 条件値
#DIM 成功値
#DIM 大成功補正値

SIF !条件最大値
	条件最大値 = 100

条件値 = RAND:条件最大値

SIF !成功値
	成功値 = MIN(90 + GET_SUCCESS_RATE() / 5, 99)

SELECTCASE TFLAG:192
	;通常
	CASE 0
		;大成功
		IF 条件値 <= 9 + 大成功補正値
			TFLAG:193 = 1
		;失敗
		ELSEIF 条件値 >= 成功値
			TFLAG:193 = -1
		;成功
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功(強制成功時は大成功しやすいように
	CASE 1
		;大成功
		IF 条件値 <= 19 + 大成功補正値 / 2
			TFLAG:193 = 1
		;成功
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制失敗orコマンド終了
	CASE -1,-2
		TFLAG:193 = -1
ENDSELECT
条件最大値 = 0
条件値 = 0
成功値 = 0
大成功補正値 = 0

@SOMATOMETRY(ARG)
PRINTFORML %CALLNAME:ARG%の身体測定を行った
CALL MAKE_SIZE(ARG)
CFLAG:ARG:身体測定 = 1
CALL SHOW_SIZE(ARG)
CALL ASK_YN("再測定する","これでよし")
IF !RESULT
	CFLAG:ARG:身体測定 = 2
	RESTART
ENDIF

