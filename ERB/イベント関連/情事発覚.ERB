;FileName_情事発覚.ERB --------------------------------- Rev1.00
;情事の発覚
;CALL		USER
;ARG		0:入室キャラNO, 1:発覚状況(0:同室, 1:覗き)
;RETURN		0:同室, 1:立ち去る
;COMMENT	
;-----------------------------------------------------------
@AFFAIR_DISCLOSURE(ARG, NOZOKI)
#DIM PREV_TARGET
#DIM IRAI_ABLE
#DIM NOZOKI
#DIM FORCE_STOP
#DIM FORCE_GETOUT

VARSET LOCAL
FORCE_STOP = 0
FORCE_GETOUT = 0

LOCAL:1 = IN_ROOM_MEMBER("MAX", CFLAG:MASTER:現在位置, "CFLAG", GETNUM(CFLAG, "诶嘿嘿"))
SIF CFLAG:MASTER:恶作剧
	LOCAL:1 = TARGET
IF LOCAL:1 && (CFLAG:(LOCAL:1):诶嘿嘿 || CFLAG:(LOCAL:1):恶作剧)
	DRAWLINE
	IF CFLAG:(LOCAL:1):恶作剧
		PRINTFORMW 対睡在地上的%CALLNAME:ARG%惡作劇的時候被%CALLNAME:(LOCAL:1)%看到了！
		IF !CFLAG:ARG:掌握把柄 && CFLAG:(LOCAL:1):恶作剧 == 2
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
		ELSE
			PRINTFORMW %CALLNAME:ARG%露出了呆愣的表情
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
		DRAWLINE
		FORCE_STOP = 1
		CFLAG:ARG:掌握把柄 += 10
	ELSEIF TFLAG:102 == 3 && TFLAG:抱住模式
		PRINTFORMW 和%CALLNAME:(TFLAG:現在的TARGET)%调情的时候被%CALLNAME:ARG%看到了！
		PRINTFORMW %CALLNAME:(TFLAG:現在的TARGET)%遺憾的從%CALLNAME:MASTER%身上離開…
		CALL UWAKI_RECORD(ARG,0,TFLAG:現在的TARGET)
		TFLAG:抱住模式 = 0
		TFLAG:102 = 1
		CALL 被逆推終了
	ELSE
		IF NOZOKI
			PRINTFORMW 【%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%】にいた被%CALLNAME:ARG%撞见了与%CALLNAME:(TFLAG:現在的TARGET)%的情事！
		ELSE
			PRINTFORMW %CALLNAME:ARG%闖入了和%CALLNAME:(TFLAG:現在的TARGET)%的情事現場！
		ENDIF
		CALL UWAKI_RECORD(ARG)
		;恋人同士（もしくは恋人を含む）場合
		IF TALENT:MASTER:恋人 && (TALENT:(TFLAG:現在的TARGET):恋人 || WITH_恋人())
			;出くわした
			IF !NOZOKI
				[SKIPSTART]
				;恋慕かある程度仲が良い
				IF (CFLAG:ARG:6 > 1 || TALENT:ARG:恋慕)
					PRINTFORMW %CALLNAME:ARG%面色不虞的様子…
					PRINTL [0] 一并推倒
					PRINTL [1] 还是算了
					$INPUT_EMBRACE
					INPUT
					IF RESULT == 0
						IF !NOZOKI
							PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很开心的依偎了过来
							CFLAG:ARG:诶嘿嘿 = CFLAG:(LOCAL:1):诶嘿嘿
							CFLAG:ARG:同室 = 1
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,4)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,4)
						ENDIF
					ELSEIF RESULT == 1
						PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
						IF !NOZOKI
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,3)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,3)
						ENDIF
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
						ENDIF
						;キャラを動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						CLEARLINE 1
						GOTO INPUT_EMBRACE
					ENDIF
				;参戦するほど仲は良くない
				ELSE
				[SKIPEND]
				;入ってきた方が恋人だった場合、割り込んでくる
;				IF TALENT:MASTER:恋人 && TALENT:MASTER:恋人 == TALENT:ARG:恋人
				IF TALENT:ARG:恋人
					PRINTFORMW %CALLNAME:ARG%屈服了并依偎到了%CALLNAME:MASTER%的身边
					CFLAG:ARG:诶嘿嘿 = CFLAG:(LOCAL:1):诶嘿嘿
					;恋人が他の女といちゃついてたら、理性吹っ飛んでもおかしくないし、テンションマックスって意味でムードが上がってもいいんじゃないかなぁと愚考する次第
					;BASE:ARG:ムード = MAXBASE:ARG:ムード
					;BASE:ARG:理性 = 0
					;上記がコメントアウトされてるけど、それが原因のバグが出てるっぽいので追記
					;まあ、マックス＆0にするのはさすがにやり過ぎだった気がしないでもない
					SIF BASE:ARG:情緒 < 300
						BASE:ARG:情緒 = 300
					SIF BASE:ARG:理性 > 300
						BASE:ARG:理性 = 300
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					;CALL KOJO_EVENT(4,LOCAL:1,ARG)
					CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,2)
					CALL UWAKI_RECORD(LOCAL:1, 1)
					DRAWLINE
					RETURN 0
				;してる方が恋人だった場合、そそくさと立ち去る（恋人が混じっている多人数プレイかつTARGETが恋人でない場合も立ち去る
				ELSE
					PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
					IF !NOZOKI
						;見た人
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,3)
						;見られた人（最も地位の高いTARGETで代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,3)
					ENDIF
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					;キャラを動かしたのでTARGET再設定
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ENDIF
			;覗きみた
			ELSE
				PRINTFORMW %CALLNAME:ARG%看上去面色不虞背过脸去慌慌张张地离开了现场…
				IF !NOZOKI
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,3)
				ENDIF
				IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
					CALL GETOUT(ARG)
				ELSE
					CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				ENDIF
				;キャラを動かしたのでTARGET再設定… まぁ、覗きなんで大丈夫なはず。
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				RETURN 1
			ENDIF
		;ここから恋人が含まれていない場合
		ELSEIF (TALENT:ARG:淫乱 || TALENT:ARG:風騷)
			PRINTFORMW %CALLNAME:MASTER%一招手示意%CALLNAME:ARG%就好像很开心的依偎了过来
			CFLAG:ARG:诶嘿嘿 = CFLAG:(LOCAL:1):诶嘿嘿
			CFLAG:ARG:同室 = 1
			;見た人
			CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,4)
			;見られた人（最も地位の高いTARGETで代表）
			CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,4)
		ELSEIF TALENT:ARG:無知 && !CFLAG:ARG:既成事実 & 合意_诶嘿嘿
		;無知かつうふふ未経験
			IF NOZOKI
				PRINTFORMW %CALLNAME:ARG%從%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅、目不轉睛的# 饒有興趣的\@眺望着%CALLNAME:MASTER%們的行為……
			ELSE
				PRINTFORML %CALLNAME:ARG%、\@ TALENT:ARG:羞恥心 > 0 ? 滿臉通紅的 # 露出很感興趣的樣子\@打聽着%CALLNAME:MASTER%們到底在做什麼
				PRINTFORMW %CALLNAME:MASTER%們慌忙的把%CALLNAME:ARG%趕走了
				CALL GETOUT(ARG)
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ENDIF
		;そのキャラの私室でヤってた場合
		ELSEIF CFLAG:ARG:初始位置 == CFLAG:MASTER:現在位置
			PRINTFORML 発現自己的房間被當做偷情地點的%CALLNAME:ARG%、憤怒的把%CALLNAME:MASTER%們趕出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:行動 == 5 && CFLAG:MASTER:現在位置 == CFLAG:ARG:職場
			PRINTFORML 発現工作場所被當做偷情地點的%CALLNAME:ARG%、憤怒的把%CALLNAME:MASTER%們趕出去了
			PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			CALL GET_ANGRY
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:掌握把柄 += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:態度 < 3 && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) != TARGET
			SIF !CFLAG:ARG:掌握把柄
				PRINTFORMW 被%CALLNAME:ARG%掌握住了把柄
			IF !NOZOKI
				;見た人
				;CALL KOJO_EVENT(1,ARG,LOCAL:1)
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,1)
				;見られた人（最も地位の高いTARGETで代表）
				;CALL KOJO_EVENT(2,LOCAL:1,ARG)
				CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,1)
			ENDIF
			CALL TOUCH_SET(0,0,0,1)
			DRAWLINE
			FORCE_STOP = 1
			CFLAG:ARG:掌握把柄 += 10
			;謹慎処分とか弱味とかの処理を入れる予定
		ELSE
			IF !NOZOKI
				;態度だけだと既成事実とか見てないことに気づく、ので合意がある場合に迫ってくるように
				IF TCVAR:MASTER:亲热 >= 3 && GETBIT(CFLAG:ARG:既成事実 , 1)
					PRINTFORMW %CALLNAME:ARG%屈服了并依偎到了%CALLNAME:MASTER%的身边
					CFLAG:ARG:诶嘿嘿 = CFLAG:(LOCAL:1):诶嘿嘿
					;一応、押し倒し可能なムードに
					;理性は押し倒しに関係してないけど、理性的なら混ざろうとしないだろうと
					BASE:ARG:情緒 = MAX(BASE:ARG:情緒, 300)
					BASE:ARG:理性 = MIN(BASE:ARG:理性, 300)
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,2)
					CALL UWAKI_RECORD(ARG,1)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,2)
					CALL UWAKI_RECORD(LOCAL:1,1)
					DRAWLINE
					RETURN 0
				ELSE
					PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?滿臉失望的 #不高興的轉過了臉、匆忙 \@離開了…
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,LOCAL:1,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,LOCAL:1,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前一次的位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
					ENDIF
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					IF TALENT:ARG:恋慕
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 2
						CFLAG:ARG:情事目撃 = TARGET
					ENDIF
					RETURN 1
				ENDIF
			;覗かれた場合
			ELSE
				PRINTFORMW %CALLNAME:ARG%\@TALENT:ARG:恋慕 ?滿臉失望 #不高興的轉過了臉 \@
				IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
					IF TCVAR:ARG:亲热 < 3
						TALENT:ARG:心情 = MIN(TALENT:ARG:心情, 0)
						BASE:ARG:憤怒 += MAXBASE:ARG:憤怒 / 4
					ENDIF
					CFLAG:ARG:情事目撃 = TARGET
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF FORCE_STOP
	SIF CFLAG:MASTER:恶作剧
		CALL SET_PRANK(0,TARGET,2)
	FOR LOCAL,0,CHARANUM
		CALL ENDUFUFU(LOCAL)
		SIF LOCAL == LOCAL:1 || LOCAL == ARG
			CONTINUE
		CALL GETOUT(LOCAL)
	NEXT
	TFLAG:106 = 0
	RETURN 0
ENDIF
;通常遭遇
;こちらからも見える位置にいる、もしくは同室
IF !CFLAG:ARG:睡眠 && (!NOZOKI || CAN_SEE(ARG)) && !FLAG:70 && !CHK_DATENOW(CFLAG:ARG:约会中) && TIME != TIME:3
	;デート中に偶然遭遇
	IF CHK_DATENOW(CFLAG:MASTER:约会中)
		IF !CFLAG:(NO:ARG):面識 && TALENT:ARG:路人
			PRINTFORMW %CALLNAME:MASTER%向%CALLNAME:ARG%打了声招呼
		ELSE
			PRINTFORMW 在目的地遇到了%CALLNAME:ARG%
		ENDIF
		IF CFLAG:(NO:ARG):面識 == 0
			CALL SYOTAIMEN(ARG)
		ELSE
			FOR LOCAL:4,1,CHARANUM
				IF CHK_DATENOW(CFLAG:(LOCAL:4):约会中)
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,7,LOCAL:4)
					BREAK
				ELSEIF LOCAL:4 == CHARANUM - 1
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,6,0)
				ENDIF
			NEXT
		ENDIF
	;さっきから居るのは分かっていたか否か
	ELSEIF CFLAG:ARG:前一次的位置 == CFLAG:ARG:現在位置
		PRINTFORM %CALLNAME:ARG%在
		PRINTFORM %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
		PRINTFORMW 那里\@CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:現在位置) == 2 ? 曾经呆着 # 呆着\@
		LOCAL:3 = 1
	;風呂に入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && BATHCHECK(CFLAG:MASTER:現在位置)
		PRINTFORMW %CALLNAME:ARG%进入了浴場…
		IF 浴室逐出条件(ARG)
			IF CFLAG:ARG:掌握把柄 && HETEROSEX(MASTER,ARG)
				PRINTFORMW %CALLNAME:MASTER%没法违抗%CALLNAME:ARG%被从浴場里赶了出来
				CFLAG:MASTER:現在位置 = KICKOUT()
				LOCAL:3 = 5
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ELSE
				PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%一对上视线就慌慌张张地离开了现场…
				CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				LOCAL:3 = 4
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:ARG%好像想和%CALLNAME:MASTER%一起进来的様子
			LOCAL:3 = 3
		ENDIF
	;トイレに入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前一次的位置, CFLAG:ARG:前一次的位置) < 2 && CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置 && IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTW 有人在敲门
		$TOILET_INPUT_LOOP
		PRINTL [0]里面有人ー　[1]请进
		INPUT
		IF RESULT == 1
			PRINTFORM %CALLNAME:ARG%が
			PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
			PRINTFORMW 进来了
			LOCAL:3 = 2
		ELSEIF RESULT == 0
			PRINTW 脚步声远去了
			CFLAG:ARG:現在位置 = CFLAG:ARG:前一次的位置
			CALL TARGETSET_CHACK
			CFLAG:ARG:同室 = 0
			LOCAL:3 = 0
		ELSE
			GOTO TOILET_INPUT_LOOP
		ENDIF
	;ELSEIF CAN_MOVE(CFLAG:MASTER:前ターン位置, CFLAG:ARG:前ターン位置) < 2 && CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置
	;↑の条件では来訪時に初対面口上が出ない場合があるので変更
	ELSEIF CFLAG:ARG:前一次的位置 != CFLAG:MASTER:前一次的位置
		IF CFLAG:ARG:前一次的位置 != MAXROOM && CFLAG:ARG:前一次的位置 != OMANEKIBEYA()
			PRINTFORMW %CALLNAME:ARG%來到了%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
		ENDIF
		LOCAL:3 = 2
	ENDIF
	IF !FLAG:70 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
		IF !NOZOKI && LOCAL:3
		;初対面
			IF CFLAG:(NO:ARG):面識 == 0
				CALL SYOTAIMEN(ARG)
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
				PRINTFORMW 被%CALLNAME:ARG%追问了刚才和%CALLNAME:(CFLAG:ARG:情事目撃)%在一起的事情…
				;CALL KOJO_EVENT(0, ARG, 2)
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,1,0)
				CFLAG:ARG:情事目撃 = 0
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 == CFLAG:ARG:現在位置
				PRINTFORMW %CALLNAME:ARG%就刚才的事情对%CALLNAME:MASTER%和%CALLNAME:(CFLAG:ARG:情事目撃)%进行了説教…
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,2,0)
				CFLAG:ARG:情事目撃 = 0
			ELSE
				;通常遭遇口上
				CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,LOCAL:3,0)
			ENDIF
		ENDIF
		CALL FirstMeetToday(ARG)
	ENDIF
ENDIF

@OSASOI(ARG)
#DIM 目的地
#DIM 自宅地域
#DIM 現在地域

自宅地域 = CFLAG:ARG:自宅位置 / 100
現在地域 = GET_MAPID(CFLAG:MASTER:現在位置)

SIF GROUPMATCH(TIME:5, 5, 9)
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
IF (TALENT:ARG:恋慕 || TALENT:ARG:思慕) && CFLAG:ARG:宴会参加 && TFLAG:宴会場 / 100 != MAIN_MAP && TFLAG:宴会場 / 100 != 現在地域
	目的地 = TFLAG:宴会場 / 100
	PRINTFORML %CALLNAME:ARG%邀请你一起去今天有宴会的%GET_MAPNAME(目的地)%
	GOTO INVITE_ENKAI
ENDIF
IF CHARA_HOLIDAY(ARG) && TALENT:ARG:恋慕 && !RAND:5 && !CFLAG:ARG:宴会参加
	IF CFLAG:ARG:积攒度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !CFLAG:ARG:自宅位置
		PRINTFORML 被%CALLNAME:ARG%邀請去房間
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,3,自宅地域)
		CALL ASK_YN("去","不去")
		IF !RESULT
			PRINTFORML 接受了%CALLNAME:ARG%的邀請、%CALLNAME:MASTER%走向了她的房間……
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,4,自宅地域)
			PRINTFORMW +{TIME_REQUIRED(現在地域, 自宅地域)}分
			TIME += TIME_REQUIRED(現在地域, 自宅地域)
			CFLAG:ARG:约会中 = 自宅地域
			CFLAG:MASTER:约会中 = 自宅地域
			FLAG:约会的对象 = ARG
			CFLAG:MASTER:同行 = 60
			CFLAG:ARG:同行 = 60
			TFLAG:约会前好感度 = CFLAG:ARG:2
			TARGET = ARG
			CALL OMANEKI_ENTER(ARG)
			CALL TARGETSET_CHACK
			;お誘いに乗ったか断ったかをRETURNで返すようにしました
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:ARG%內心非常遺憾
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,5,自宅地域)
			RETURN 0
		ENDIF
	ELSEIF !RAND:4 && CFLAG:ARG:經常去的地方 >= 0 && CFLAG:ARG:經常去的地方 != MAIN_MAP
		目的地 = CFLAG:ARG:經常去的地方
	ELSE
		目的地 = RAND:10
		SIF 目的地 == MAIN_MAP
			RETURN
	ENDIF
	PRINTFORML %CALLNAME:ARG%邀请去%GET_MAPNAME(目的地)%
	$INVITE_ENKAI
	CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,0,目的地)
	CALL ASK_YN("去","不去")
	IF !RESULT
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,1,目的地)
		CFLAG:MASTER:同行 = 60
		CFLAG:ARG:同行 = 60
		TFLAG:约会前好感度 = CFLAG:ARG:2
		CALL SET_DATE(目的地,ARG)
		CALL TARGETSET_CHACK
		RETURN 1
	ELSE
		PRINTFORMW %CALLNAME:ARG%表示遺憾
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,2,目的地)
		RETURN 0
	ENDIF
ENDIF

RETURN 0



@UWAKI_RECORD(ARG,公認,浮気相手)
#DIM 浮気相手
#DIM 公認

SIF UWAKI_KNOWN:ARG:0 == ""
	UWAKI_KNOWN:ARG:0 = /
SIF UWAKI_ACCEPTED:ARG:0 == ""
	UWAKI_ACCEPTED:ARG:0 = /


IF 浮気相手
	LOCALS = %CALLNAME:浮気相手%
	SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
		UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
	SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
		UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
ELSE
	;第二引数が0の場合、あなたと同じ場所にいるキャラ全員記録
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
			UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
		SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
			UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
	NEXT
ENDIF

@GET_UWAKI_HISTORY(ARG,浮気相手)
;公認だと2を返す
#FUNCTION
#DIM 浮気相手
LOCALS = /%CALLNAME:浮気相手%/

IF STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS)
	RETURNF 2
ELSEIF STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SYOTAIMEN(ARG)
SIF CFLAG:ARG:面識
	RETURN
IF TALENT:ARG:路人 == 1
	IF TALENT:MASTER:魅惑
		PRINTFORMW %CALLNAME:ARG%被%CALLNAME:MASTER%【魅惑】了
		CALL COLORMESSAGE(@"好感度と親密が上がった",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:謎之魅力
		PRINTFORMW %CALLNAME:ARG%感到%CALLNAME:MASTER%的【謎之魅力】、胸口剧烈的跳动
		CALL COLORMESSAGE(@"好感度と親密が上がった",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
ENDIF
IF !FLAG:扮演
	SIF NO:ARG != 拠点_家主
		PRINTFORMW %CALLNAME:ARG%向初次见面的%CALLNAME:MASTER%作了问候
	CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,1,0)
ENDIF
CFLAG:ARG:面識 = 1

@FirstMeetToday(ARG)
#DIM PREV_TARGET
#DIM IRAI_ABLE
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF TCVAR:ARG:今天有否见面
	RETURN

TCVAR:ARG:今天有否见面 = 1
CFLAG:ARG:最後に会った日 = DAY
SIF TCVAR:ARG:另一种便服
	PRINTFORMW 今天的%CALLNAME:ARG%打扮得跟往常不一様
IF CFLAG:ARG:オナバレ
	;陥落素質あり、【快感に素直】の時、わりと素直な反応に
	IF CFLAG:ARG:オナバレ == 1
		IF (TALENT:ARG:恋慕 || TALENT:ARG:愛欲 || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 先日自慰現場を目撃された%CALLNAME:ARG%は、%TEXTR("すまし顔だ…/何か言いたげな顔でこちらを見ている…/つとめて平然としている…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的%CALLNAME:ARG%很難為情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 2
		IF (TALENT:ARG:恋慕 || TALENT:ARG:愛欲 || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 先日自慰現場を直撃したとき、ついでヤらせてくれた%CALLNAME:ARG%が%TEXTR("艶っぽい流し目をくれてきた…/こちらを見てニヤニヤしている…/イイ笑顔を浮かべている…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的、於是身體沒有得到滿足的%CALLNAME:ARG%很難為情…
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 3
		IF (TALENT:ARG:恋慕 || TALENT:ARG:愛欲 || TALENT:ARG:炮友) && TALENT:ARG:快感応答 == 1
			PRINTFORMW 先日自慰現場を直撃したとき、盛大なアクメを披露してくれた%CALLNAME:ARG%は%TEXTR("こちらにウインクをしてみせた…/こちらをチラチラ見ている…/稍微照れくさそうだ…")%
		ELSE
			PRINTFORMW 前日被目撃了自慰現場的、而且還在別人眼前激烈高潮的%CALLNAME:ARG%很難為情…
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 0
ELSEIF TALENT:ARG:心情 == 1
	PRINTFORMW 不知是不是发生了什么好事呢、今天的%CALLNAME:ARG%心情很好的様子
ELSEIF TALENT:ARG:心情 == -1
	PRINTFORMW 总觉的今天的%CALLNAME:ARG%似乎心情不好的様子…
ENDIF
IF CFLAG:ARG:积攒度 >= 500 && TALENT:MASTER:劣情察知
	SETCOLOR 255,100,255
	FONTBOLD
	PRINTFORMW 发现！
	PRINTFORMW %CALLNAME:MASTER%发现%CALLNAME:ARG%欲求不满中……！
	RESETCOLOR
	FONTREGULAR
ENDIF
CALL SEASONAL(ARG)

PREV_TARGET = TARGET
TARGET = ARG
CALL CAN_IRAI_START
IRAI_ABLE = RESULT
IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(ARG) == "依頼未受託" && !FLAG:依頼スルー
	;CALL先の判定でTARGET==CLIENT前提の処理が多々あるので一時変更、そのまま戻ってくるので退避しとく
	CALL IRAI_START(ARG)
	TARGET = PREV_TARGET
ELSEIF IRAI_ABLE && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(ARG), "期限切れ", "報告忘れ")
	PRINTFORML %CALLNAME:ARG%責怪%CALLNAME:MASTER%沒有按照要求履行委託
	;TFLAG:104の使い方がよくわからん
	;TFLAG:104 = ARG
	CALL IRAI_REPORT(ARG)
	TARGET = PREV_TARGET
ELSEIF TALENT:ARG:心情 >= 0 && BASE:MASTER:気力 > 0 && BASE:ARG:気力 > 0 && !CFLAG:MASTER:同行 && NEMUKE() < 720 && !睡眠時間(ARG)
	CALL OSASOI(ARG)
	SIF !RESULT
		TARGET = PREV_TARGET
ELSE
	TARGET = PREV_TARGET
ENDIF