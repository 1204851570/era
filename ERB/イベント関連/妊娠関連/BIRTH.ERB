@BIRTH(ARG)
#DIM CHILDNUM
DRAWLINE
;妊娠消去、出産経験、性別の決定、誕生日決定
TALENT:ARG:妊娠 = 0
EXP:ARG:出産経験 += 1
CHILDNUM = EXP:ARG:出産経験 % 10
SIF !EXP:ARG:出産経験 % 10
	CHILDNUM = 10
IF FLAG:兒童の性別
	CHILD_SEX:ARG:CHILDNUM = FLAG:兒童の性別
ELSE
	CHILD_SEX:ARG:CHILDNUM = RAND:2 + 1
ENDIF
CHILD_BIRTHDAY:ARG:CHILDNUM = DAY
CHILD_BIRTHDATE:ARG:CHILDNUM = (DAY / 124 + (DAY % 124 ? 1 # 0)) * 1000
CHILD_BIRTHDATE:ARG:CHILDNUM += DAY:2 * 100 + DAY:3
CHILD_WEATHER:ARG:CHILDNUM = %GET_WEATHER()%
IF CFLAG:ARG:無自覚妊娠
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,4,CHILDNUM)
ELSEIF TALENT:ARG:恋慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,1,CHILDNUM)
ELSEIF TALENT:ARG:思慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,2,CHILDNUM)
ELSE
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,3,CHILDNUM)
ENDIF
PRINTFORMW %CALLNAME:ARG%平安无事的生下了%CALLNAME:MASTER%的孩子
PRINTFORMW \@ CHILD_SEX:ARG:CHILDNUM == 1 ? %CALLNAME:ARG%是一個很元気的女孩子 # %CALLNAME:MASTER%是一個很元気的男孩子 \@
PRINTFORMW %CALLNAME:ARG%接过刚刚诞生的小宝宝、温柔的抱住并开始喂起母乳来
SIF TALENT:ARG:母性 == 0
	PRINTFORMW %CALLNAME:ARG%觉醒了[母性]！
PRINTL ******************************************************************
PRINTFORML 第{CHILDNUM}子　母：%CALLNAME:ARG%　父：%CALLNAME:MASTER%
PRINTFORML 　性別：\@ CHILD_SEX:ARG:CHILDNUM == 1 ? 女 # 男 \@
PRINTFORM 誕生日：
CALL PRINT_DATE(DAY)
PRINTFORML  %FESTIVAL()% -%GET_WEATHER()%-
;PRINTFORML 　身長：
;PRINTFORML 　体重：
;PRINTFORML 　種族：
;PRINTFORML 髪の色：
;PRINTFORML 目の色：
PRINTL ******************************************************************


;育児中・母性追加
CALL CHILD_NAME(ARG)
CFLAG:ARG:育児人数 += 1
CFLAG:MASTER:育児人数 += 1
CFLAG:ARG:出産経過日 = 1
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0 
TALENT:ARG:母性 = 1

CFLAG:ARG:産休 = 0

EXP:ARG:愛情経験 += 10
CFLAG:ARG:兒童人数 += 1
CFLAG:MASTER:兒童人数 += 1
IF FLAG:甲斐性無
	PRINTFORML %CALLNAME:MASTER%因為自己孩子的誕生
	CALL ASK_YN("萌生起了作爲父母的責任感","對今後可能發生起的各種各樣的事態感到不安")
	IF !RESULT
		PRINTFORMW %CALLNAME:MASTER%的毫無志氣消失了
	ELSE
		PRINTFORMW 人的性格會這麼簡單就改変的話那麼就沒有人會覺得辛苦了
		PRINTFORMW %CALLNAME:MASTER%還是以前那樣不可靠的性格、幸福的和%CALLNAME:ARG%一起面臨著育児
	ENDIF
	FLAG:甲斐性無 = RESULT
ENDIF
		

@CHILD_NAME(YOME)
#DIM YOME
#DIM CHILDNUM
CHILDNUM = EXP:YOME:出産経験 % 10
SIF !EXP:YOME:出産経験 % 10
	CHILDNUM = 10
$LOOP0
PRINTL 請決定孩子的名字
PRINTFORML 
CALL ASK_M("自己決定",1,"一覧隨機候補",1,"取名為帯著「墨西哥吹來的熱風！」意味的「桑塔納」怎麼樣！",1)

SELECTCASE RESULT
	CASE 0
		$LOOP1
		PRINTL 請輸入孩子的名字
		INPUTS
		IF RESULTS == ""
			GOTO LOOP1
		ELSE
			PRINTFORML %RESULTS%可以麼？
			CALL ASK_YN
			IF RESULT
				GOTO LOOP1
			ELSE
				CHILDNAME:YOME:CHILDNUM = %RESULTS%
			ENDIF
		ENDIF
	CASE 1
		$LOOP2
		FOR LOCAL,1,10
			LOCALS:LOCAL = %随机キャラ_命名()%
			PRINTFORML [{LOCAL}] - %LOCALS:LOCAL%
		NEXT
		DRAWLINE
		PRINTFORML [99] - 候補再生成
		PRINTFORML [100] - 返回
		$LOOP3
		INPUT
		SELECTCASE RESULT
			CASE 0 TO 9
				CHILDNAME:YOME:CHILDNUM = %LOCALS:RESULT%
			CASE 99
				GOTO LOOP2
			CASE 100
				GOTO LOOP0
			CASEELSE
				GOTO LOOP3
		ENDSELECT
	CASE 2
		PRINTFORML 生氣的%CALLNAME:YOME%自己決定好了名字…
		CHILDNAME:YOME:CHILDNUM = %随机キャラ_命名()%
ENDSELECT

PRINTFORML 取下了%CHILDNAME:YOME:CHILDNUM%的名字
TALENT:YOME:育児中 = CHILDNUM
;----------------------------------------------------
;そのキャラに特定の年齢の子供がいたら返す関数
;----------------------------------------------------
@CHK_HAVE_CHILD(ARG,ARG:1,ARG:2)
#FUNCTION
#DIM 年齢
SIF !CFLAG:ARG:育児人数
	RETURNF 0

FOR LOCAL, 1, 11
	年齢 = DAY - CHILD_BIRTHDAY:ARG:LOCAL
	SIF INRANGE(年齢,ARG:1,ARG:2-1)
		RETURNF LOCAL
NEXT

RETURNF 0
;----------------------------------------------------
;誕生日の子供がいるか返す関数
;eraTW4.570proto_bugfix_m05での仕様変更
;・REFを使用し、配列を書き換えるF関数にした（引数を全て渡さないとエラーになる）
;・仕様上、配列の想定最大要素数は10*キャラクタ数上限となる（＝RESULTの要素数を突破してエラーが出たので仕様変更）
;・BREAKやCONTINUEを使用して処理を効率化
;----------------------------------------------------
@CHILD_HAPPY_BIRTHDAY(CNT_BIRTHDAY, NAME_CHILD, AGE_CHILD)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
#DIM  REF CNT_BIRTHDAY
#DIMS REF NAME_CHILD
#DIM  REF AGE_CHILD
FOR CHARA, 1, CHARANUM
	SIF !CFLAG:CHARA:兒童人数
		CONTINUE
	FOR CHILDNUM, 1, MIN(11, CFLAG:CHARA:兒童人数 + 1)
		;名無し≒未登録なら次のキャラへ
		SIF !STRLENS(CHILDNAME:CHARA:CHILDNUM)
			BREAK
		;誕生日以外はスキップ
		SIF (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) % 124
			CONTINUE
		CNT_BIRTHDAY ++
		NAME_CHILD:CNT_BIRTHDAY = %CHILDNAME:CHARA:CHILDNUM%
		AGE_CHILD:CNT_BIRTHDAY = (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) / 124
	NEXT
NEXT
RETURNF CNT_BIRTHDAY
