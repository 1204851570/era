@出産経過(ARG)
#DIM CHILDNUM

SIF CFLAG:ARG:妊娠経過日数 > 100
	CALL BIRTH(ARG)
SIF CFLAG:ARG:妊娠経過日数 > 1
	CALL PERCIEVE(ARG)
SIF CFLAG:ARG:妊娠経過日数 > 0
	CFLAG:ARG:妊娠経過日数 += 1
SIF CFLAG:ARG:出産経過日 > 0
	CFLAG:ARG:出産経過日 += 1

CHILDNUM = TALENT:ARG:育児中

;子離れ
IF CFLAG:ARG:出産経過日 > CHILD_自立
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_自立
	CATCH
		PRINTFORMW %CALLNAME:ARG%和%CALLNAME:MASTER%的孩子%CHILDNAME:ARG:CHILDNUM%平安的迎來了獨自生活的日子、自立了。
		PRINTFORML 
		PRINTFORML 希望能偶爾見見面
		PRINTFORML %CALLNAME:MASTER%的愿望沒能說出口……。
		PRINTFORMW 
	ENDCATCH
	
	CFLAG:ARG:育児人数 -= 1
	CFLAG:MASTER:育児人数 -= 1
	;IF CFLAG:ARG:育児人数 >= 0
	IF CFLAG:ARG:育児人数 > 0
		;PRINTFORML 遠くなる子の背中を見ながらまだ小さい他の子供も無事に育てようと、
		PRINTFORML 看着逐渐远去的孩子、
		PRINTFORML 希望還小的孩子能順利成長、
		;PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%と静かに誓った。
		PRINTFORMW %CALLNAME:ARG%握著%CALLNAME:MASTER%的手、默默祈願。
	ELSE
		;PRINTFORML 遠くなる子の背中を見ながら%CALLNAME:ARG%は%CALLNAME:MASTER%とこどもの無事を祈った。
		PRINTFORMW 看著遠去的孩子的背影的%CALLNAME:ARG%和%CALLNAME:MASTER%祈禱著孩子平安。
	ENDIF
	;PRINTFORML 別れ際、子供は%CALLNAME:MASTER%が%CALLNAME:ARG%を顧みず他の女の尻を追い回していたことを口汚く罵り、
	;PRINTFORML 自分は絶対に%CALLNAME:MASTER%のようにはならないと吐き捨てて去っていった

	IF CFLAG:ARG:育児人数 == 0
		TALENT:ARG:育児中 = 0
	ENDIF
	CFLAG:ARG:出産経過日 = 0
ENDIF
IF CFLAG:ARG:出産経過日 == CHILD_自立前
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_自立前
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%還有一段時間就要分別了。
		PRINTFORML 個子也長高了、已經和%CALLNAME:ARG%差不多了。
		PRINTFORMW 這個孩子不久也要離開家門了吧、要好好珍惜剩下的時間……。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_就学
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_就学
	CATCH
		PRINTFORML %CALLNAME:ARG%的孩子%CHILDNAME:ARG:CHILDNUM%開始上私塾了。
		PRINTFORML 孩子的成長很迅速。
		PRINTFORML 離開父母的那天很快就會到來的吧……。
		PRINTFORMW %CALLNAME:MASTER%変得有些傷感。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_しつけ
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_しつけ
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%越來越聰明了、做出了各種各樣的行動、
		PRINTFORML 同時悪作劇和任性次數也増加了。
		PRINTFORMW 必須要認真考慮教育的事了……。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_語彙
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_語彙
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%的詞匯量増加了。
		PRINTFORMW 說的內容只是単獨的２、３個音節完全無法形成句子。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_喋る
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_喋る
	CATCH
		PRINTFORML %CALLNAME:ARG%以驚人的氣勢跑了過來。
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%呼喊了%CALLNAME:ARG%的名字。
		PRINTFORML 趕緊去看看、確實在看著%CALLNAME:ARG%的臉喊著名字。
		PRINTFORMW %CALLNAME:MASTER%為了聽見孩子叫自己、把臉靠過去也聽見了連喊爸爸的聲音。
	ENDCATCH
ELSEIF INRANGE(CFLAG:ARG:出産経過日, CHILD_喋る - 3, CHILD_喋る - 1)
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_会話寸前
	CATCH
		PRINTFORML 最近%CHILDNAME:ARG:CHILDNUM%常常張口說話。
		PRINTFORML 雖然說不出意義清楚的話、只能用手指著想要的東西喊著名字、以此表達自己的意思。
		PRINTFORMW 不久就能説出清楚的話吧……。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_よちよち
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_よちよち
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%用雙腳站立開始搖搖晃晃的走路了。
		PRINTFORML 因為視線和行動半径的擴大対各種各樣的事情產生了興趣。
		PRINTFORMW %CALLNAME:MASTER%牽著還在搖搖晃晃的%CHILDNAME:ARG:CHILDNUM%的手移動到了各種地方。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_つかまり立ち
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_つかまり立ち
	CATCH
		PRINTFORML %CALLNAME:MASTER%蹲了下去讓%CHILDNAME:ARG:CHILDNUM%握住手指。
		PRINTFORMW 然後慢慢地抬起手指、做著站立的練習。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_玩具
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_玩具
	CATCH
		PRINTFORML 為了%CHILDNAME:ARG:CHILDNUM%玩耍%CALLNAME:MASTER%用木頭製作了簡単的玩具。
		PRINTFORML 反正肯定會被扔來扔去所以只要単純的做結實就好。
		PRINTFORMW 為了不讓孩子受傷、%CALLNAME:MASTER%仔細的打磨著断面。
	ENDCATCH
ELSEIF CFLAG:ARG:出産経過日 == CHILD_離乳
	DRAWLINE
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_離乳
	CATCH
		PRINTFORMW 剛剛斷奶的嬰兒滿臉笑容的樣子讓%CALLNAME:MASTER%嘴角上揚。
	ENDCATCH
	PRINTFORML %CALLNAME:ARG%は授乳期間结束了。
	IF TALENT:ARG:母乳体質 == 1 && EXP:ARG:噴乳経験 > 500 && EXP:ARG:出産経験 > 1
		PRINTFORML 但是、持续产出了超过哺乳程度的%CALLNAME:ARG%的乳房、至今仍有美味的乳汁……
		PRINTFORML %CALLNAME:ARG%得到【母乳体質】
		TALENT:ARG:母乳体質 = 2
	ELSE
		PRINTFORMW 完成任务的%CALLNAME:ARG%的胸部变小了、母乳也没有了。
		SIF TALENT:ARG:母乳体質 == 1
			TALENT:ARG:母乳体質 = 0
	ENDIF
	;乳ランクアップフラグたってて、今絶壁じゃないなら小さくなる。通常出産以外の妊娠終了なんかも考慮するとCALLで呼び出す方が良いのだろうが…
	IF CFLAG:ARG:乳品质提升 == 1 && TALENT:ARG:胸圍 > -2
		TALENT:ARG:胸圍 -= 1
		CFLAG:ARG:乳品质提升 = 0
	ENDIF
ELSEIF CFLAG:ARG:出産経過日 == 20 && CFLAG:ARG:回復速度降低
	DRAWLINE
	PRINTFORMW 在平安完成産後恢复后、%CALLNAME:ARG%完全恢复了元気。
	;口上チェック
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_回復
	CATCH
		;体質変化があるので省略しない
	ENDCATCH
	MAXBASE:ARG:体力 += 500
	MAXBASE:ARG:気力 += 500
	SIF MAXBASE:ARG:体力 / 10 * 11 <= CSVBASE(ARG, GETNUM(BASE, "体力")) * 2
		MAXBASE:ARG:体力 = MAXBASE:ARG:体力 / 10 * 11
	SIF MAXBASE:ARG:気力 / 10 * 11 <= CSVBASE(ARG, GETNUM(BASE, "気力")) * 2
		MAXBASE:ARG:気力 = MAXBASE:ARG:気力 / 10 * 11
	TALENT:ARG:回復速度 = CFLAG:ARG:回復速度降低 - 2
	CFLAG:ARG:回復速度降低 = 0
ENDIF


;子供が通学するか
@GO_SCHOOL(ARG)
#FUNCTION
SIF !INRANGE(DAY % 7, 1, 5) 
	RETURNF 0
SIF FESTIVAL() != ""
	RETURNF 0
SIF !TALENT:ARG:育児中
	RETURNF 0
;大雨と吹雪
SIF TIME:5 == 5
	RETURNF 0
SIF TIME:5 == 9
	RETURNF 0
SIF CFLAG:ARG:出産経過日 >= CHILD_就学
	RETURNF 1

;現在育児中の子供の名前を表示
@SHOW_CHILDNAME(ARG)
#FUNCTIONS
RETURNF CHILDNAME:ARG:(TALENT:ARG:育児中)

;成長した子供と親をランダムで拾ってくる関数
@PickRandGrownChild
#DIM YOME
#DIM CHILDNUM
SIF !CFLAG:MASTER:兒童人数
	RETURN -1
YOME = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	IF CFLAG:LOCAL:兒童人数 - CFLAG:LOCAL:育児人数 > 0
		YOME = LOCAL
		CHILDNUM = RAND:(CFLAG:LOCAL:兒童人数 - CFLAG:LOCAL:育児人数) + 1
		BREAK
	ENDIF
NEXT
RETURN YOME, CHILDNUM