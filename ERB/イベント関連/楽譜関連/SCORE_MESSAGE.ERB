;-------------------------------------------------
;楽譜の入手条件を取得
;SCORE_NO 楽譜番号
;
;RETURNFS 入手条件
;-------------------------------------------------
@GET_SCORE_INFO_MESSAGE(SCORE_NO)
#FUNCTIONS
#DIM SCORE_NO		; 楽譜番号
#DIM SCORE_INFO_NO	; 入手条件番号
#DIMS MESSAGE		; メッセージ

SCORE_INFO_NO = GET_SCORE_INFO_NO(SCORE_NO)

SELECTCASE SCORE_INFO_NO
	CASE IS < 楽譜入手方法：随机
		; 関連キャラの演奏大成功時に入手
		MESSAGE = 让「%CSVNAME(SCORE_INFO_NO)%」听到大成功的演奏時入手
	CASE 楽譜入手方法：随机
		MESSAGE = 演奏時随机入手
	CASE 楽譜入手方法：シリーズ初取得
		MESSAGE = 在第一次入手本系列的楽譜時入手
	CASE 楽譜入手方法：シリーズ随机コンプ
		MESSAGE = 在入手了本系列的其他任意一个楽譜時入手
	CASE 楽譜入手方法：シリーズ関連キャラ弾幕勝負
		MESSAGE = 在和本系列的関連角色进行弾幕勝負時入手
	CASE 楽譜入手方法：シリーズ関連キャラ陥落
		MESSAGE = 在陥落了本系列的関連角色（恋慕・愛欲）時入手
	CASE 楽譜入手方法：シリーズ完全コンプ
		MESSAGE = 在入手了本系列的其他所有譜時入手
	CASE 楽譜入手方法：随机（キャラ無し）
		; キャラテーマだが、関連キャラがいないのでランダム入手
		MESSAGE = 演奏時随机入手
	; --- 特殊条件 ---
	CASE 楽譜入手方法：妖精依頼成功
		; 妖精大戦争
		MESSAGE = 桑尼・露娜・斯塔・大妖精・琪露諾的依頼成功時入手
	CASE 楽譜入手方法：文依頼成功
		;東方文花帖
		MESSAGE = 文的依頼成功時入手
	CASE 楽譜入手方法：極依頼成功
		;ダブルスポイラー
		MESSAGE = 極的依頼成功時入手
	CASE 楽譜入手方法：依賴失敗時
		;弾幕アマノジャク
		MESSAGE = 依賴失敗時入手
	CASEELSE
		; それ以外
		MESSAGE = 错误
ENDSELECT

RETURNF MESSAGE

;-------------------------------------------------
;楽譜入手時のメッセージを取得
;SCORE_NO 楽譜番号
;
;RETURNFS 楽譜入手時のメッセージ
;-------------------------------------------------
@GET_SCORE_OBTAIN_MESSAGE(SCORE_NO)
#FUNCTIONS
#DIM SCORE_NO	; 楽譜番号

#DIM SERIES_NO		; シリーズ番号
#DIM SCORE_INFO_NO	; 入手条件番号
#DIMS MESSAGE		; メッセージ

SERIES_NO = GET_SERIES_NO(SCORE_NO)
SCORE_INFO_NO = GET_SCORE_INFO_NO(SCORE_NO)

SELECTCASE SCORE_INFO_NO
	CASE IS < 楽譜入手方法：随机
		MESSAGE = 演奏後%CSVCALLNAME(SCORE_INFO_NO)%给了你%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：随机, 楽譜入手方法：随机（キャラ無し）
		MESSAGE = 收到了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：シリーズ初取得
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%的攻略開始！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：シリーズ随机コンプ
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%的全阶段完成！ 获得了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：シリーズ関連キャラ弾幕勝負
		MESSAGE = 哔秋！ 尽管残機減少了但是入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：シリーズ関連キャラ陥落
		MESSAGE = 枕边放着%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜。
	CASE 楽譜入手方法：シリーズ完全コンプ
		MESSAGE = %GET_SERIES_SHORT_NAME(SERIES_NO)%完全Clear了！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	; --- 特殊条件 ---
	CASE 楽譜入手方法：妖精依頼成功
		MESSAGE = ⑨！ 拿到了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：文依頼成功
		MESSAGE = 取材成功！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：極依頼成功
		MESSAGE = Shoot Chance！ 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASE 楽譜入手方法：依賴失敗時
		MESSAGE = 入手了%GET_SCORE_FULLNAME(SCORE_NO)%的楽譜！
	CASEELSE
		MESSAGE = 错误
ENDSELECT

RETURNF MESSAGE
