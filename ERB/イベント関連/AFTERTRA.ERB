;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND
#LATER
#DIM 変動値
#DIM 会ってない日数
#DIM YANUSHI
PRINTL 一天结束了
PRINTFORML 
CALL DLCEVENT
;SIF STRLENS(TSTR:1)
;	CALL SELL_VIDEO

;睡眠フラグ
CALL SET_SLEEP(1,MASTER,0)

CFLAG:MASTER:286 = 0
CFLAG:MASTER:287 = 0
CFLAG:MASTER:288 = 0
CFLAG:MASTER:289 = 0
CFLAG:MASTER:290 = 0
CFLAG:MASTER:没穿内裤 = 0

;おみくじを引けるようにフラグリセット
CLEARBIT FLAG:999, 1
;あなた懐妊処理
;CALL MasterPregnancy

;中出しフラグリセット
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:286 = 0
	CFLAG:LOCAL:287 = 0
	CFLAG:LOCAL:288 = 0
	CFLAG:LOCAL:289 = 0
	CFLAG:LOCAL:290 = 0
	;願掛けによる感度上昇解除
	SIF GROUPMATCH(TCVAR:LOCAL:祈願補正,23,31,41,70,80,82,83,101,102,103,104,106)
		TALENT:LOCAL:(TCVAR:LOCAL:祈願補正) -= 1
	;好きな体位決定
	SIF CFLAG:LOCAL:喜欢的体位 > 10 
		CFLAG:LOCAL:喜欢的体位 = 0
	FOR COUNT,1,10
		SIF EXP:LOCAL:(COUNT + 100) > EXP:LOCAL:(CFLAG:LOCAL:喜欢的体位 + 100) + 50
			CFLAG:LOCAL:喜欢的体位 = COUNT
	NEXT
	;パンツフラグ
	CFLAG:LOCAL:没穿内裤 = 0
	CFLAG:LOCAL:衣装一時変更 = 0
	SIF TCVAR:LOCAL:本来的服装
		CFLAG:LOCAL:基本服装セット = TCVAR:LOCAL:本来的服装
	SELECTCASE ABL:LOCAL:精液中毒
		CASE 0
			LOCAL:1 = 5
		CASE 1 TO 3
			LOCAL:1 = 7
		CASE 4 TO 7
			LOCAL:1 = 10
		CASEELSE
			LOCAL:1 = 20
	ENDSELECT
	;機嫌
	TALENT:LOCAL:心情 = 0
	CALL KIGEN_CHANGE(LOCAL,10)
	SIF TALENT:MASTER:濃厚精液
		LOCAL:1 *= 2
	SETCOLOR 0xEE60EE
	IF EX:LOCAL:膣内精液 > 200 || EX:LOCAL:肛門内精液 > 200
		PRINTFORM 在%CALLNAME:MASTER%大量注入的精液影响下、%CALLNAME:LOCAL%的
		IF EX:LOCAL:膣内精液 > 200 && EX:LOCAL:肛門内精液 > 200
			PRINTL 陰道和肛門又热又痒…
		ELSEIF EX:LOCAL:膣内精液 > 200
			PRINTL 陰道又热又痒…
		ELSE
			PRINTL 肛門又热又痒…
		ENDIF
		IF EX:LOCAL:膣内精液 > 200
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
		ENDIF
		IF EX:LOCAL:肛門内精液 > 200
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:肛門内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:肛門内精液
		ENDIF
	ENDIF
	IF (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) > 0
		PRINTFORM %CALLNAME:LOCAL%忍耐着
		SIF EX:LOCAL:20
			PRINT 陰蒂
		SIF EX:LOCAL:20 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:21
			PRINT 膣内
		SIF EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:22
			PRINT 肛門
		SIF EX:LOCAL:22 && (EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:23
			PRINT 胸部
		SIF EX:LOCAL:23 && EX:LOCAL:24
			PRINT 和
		SIF EX:LOCAL:24
			PRINT 嘴唇
		PRINTFORMW 傳来的痛楚…
		CFLAG:LOCAL:积攒度 += (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) * 100
	ENDIF
	IF (TALENT:LOCAL:150 || TALENT:LOCAL:151) && !TALENT:LOCAL:初潮 && EXP:LOCAL:Ｖ絶頂経験 > MAX(0, 200 - CFLAG:LOCAL:子宝補正) && !RAND:(10 - MIN(9, CFLAG:LOCAL:子宝補正 / 30)) && EX:LOCAL:膣内精液
		PRINTFORML 在对幼小的身体而言为时尚早的快楽冲击下、%CALLNAME:LOCAL%变成了「女人」……
		PRINTFORMW %CALLNAME:LOCAL%迎来了初潮
		PRINTFORMW %CALLNAME:LOCAL%的身体变得可以怀孕了
		TALENT:LOCAL:初潮 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 < 3
		IF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 15000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10
			PRINTFORML 被无数次注入精液的%CALLNAME:LOCAL%的身体、已经记住了%CALLNAME:MASTER%精液的滋味…
			PRINTL 今后即使是無意識下也会变得主动想要了吧
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了3级
			ABL:LOCAL:精液中毒 = 3
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 7500 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 < 2
			PRINTFORML %CALLNAME:LOCAL%的身体、已经与精液十分熟稔了…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了2级
			ABL:LOCAL:精液中毒 = 2
		ELSEIF CFLAG:LOCAL:累計精浴 + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計肛門精液 >= 3000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !ABL:LOCAL:精液中毒
			PRINTFORML %CALLNAME:LOCAL%的身体、已经记住了精液的味道…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了1级
			ABL:LOCAL:精液中毒 = 1
		ENDIF
	ENDIF
	IF CFLAG:LOCAL:累計精飲 > 10000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !TALENT:LOCAL:精愛味覚
		PRINTFORML 持續喝下精液的%CALLNAME:LOCAL%的身体、変得會因精液的味道而感到喜悅…
		PRINTFORMW %CALLNAME:LOCAL%得到了【精愛味覚】
		TALENT:LOCAL:精愛味覚 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 >= 3
		IF CFLAG:LOCAL:累計膣内精液 > 3000 * ABL:LOCAL:膣射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 &&  EX:LOCAL:膣内精液 > 200 && ABL:LOCAL:膣射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%的媚粘膜完全被精液玷污…
			PRINTFORMW %CALLNAME:LOCAL%的膣道変得\@ ABL:LOCAL:膣射中毒 ? 強烈# \@渴望著%CALLNAME:MASTER%的精液
			ABL:LOCAL:膣射中毒 ++
		ELSEIF CFLAG:LOCAL:累計肛門精液 > 3000 * ABL:LOCAL:肛射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && EX:LOCAL:肛門内精液 > 200 && ABL:LOCAL:肛射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%的直腸完全被精液玷污…
			PRINTFORMW %CALLNAME:LOCAL%的肛門変得\@ ABL:LOCAL:肛射中毒 ? 強烈# \@渴望著%CALLNAME:MASTER%的精液
			ABL:LOCAL:肛射中毒 ++
		ENDIF
	ENDIF
	RESETCOLOR
	IF FLAG:妊娠开关 == 1
		SIF CFLAG:LOCAL:妊娠経過日数 == 0 && !FLAG:避妊 && !TCVAR:LOCAL:口服避孕薬 && TALENT:LOCAL:路人 != 2
			CALL GET_CHILD(LOCAL)
	ELSE
	;
	ENDIF
	CALL 出産経過(LOCAL)
	SIF CFLAG:LOCAL:600
		CFLAG:LOCAL:600 = 0
	IF CFLAG:LOCAL:面識
		会ってない日数 = DAY - CFLAG:LOCAL:最後に会った日
		CALL KOJO_MESSAGE_SEND("EVENT",31,LOCAL,会ってない日数)
	ENDIF
NEXT

FOR LOCAL,MINROOM(),MAXROOM
	YOGORE:LOCAL += RAND:50
	YANUSHI = PRIVATEROOM:(LOCAL % 100)
	IF YANUSHI && YOGORE:LOCAL > (4000 - ABL:YANUSHI:清掃技能 * 500)
		PRINTFORML %CALLNAME:YANUSHI%打扫了自己的房间
		EXP:YANUSHI:清掃経験 ++
		YOGORE:LOCAL /= 4
	ENDIF
NEXT
IF FLAG:加護
	FLAG:加護 = 0
	DRAWLINE
	PRINTFORMW 神的加護的効果消失了
ENDIF
IF FLAG:異常気象
	FLAG:異常気象 = 0
	DRAWLINE
	PRINTFORMW 異常気象消失了
ENDIF
IF GatheringSeason
	GatheringSeason = 0
	PRINTFORMW 回到了原来的季節
ENDIF
IF TFLAG:上门清扫
	DRAWLINE
	PRINTFORML 应委托前去执行上门清扫服务
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%里面变得干净了！
	FOR LOCAL,MINROOM(),MAXROOM
		YOGORE:LOCAL /= 4
	NEXT
ENDIF

[SKIPSTART]
;依頼の期限切れをチェック
FOR LOCAL,1,6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	SIF IRAI_DEADLINE:LOCAL > DAY
		CONTINUE
	DRAWLINE
	PRINTFORML %CALLNAME:(IRAI_SLOT_TO_CLIENT(LOCAL))%的委托期限到了……
	CALL COLORMESSAGE("【依頼失敗】",C_YELLOW,2,1)
	CFLAG:(IRAI_CLIENT:LOCAL):依頼状況 = 4
NEXT
[SKIPEND]

;デイリー
;SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:MASTER:デート中 == MAIN_MAP
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初始位置
	CALL DAIRYEVENT
;畑の管理
CALL YASAI

;貸本屋
CALL BOOKLEND

CALL 酒造

CALL MOENY_HENDOU



MONEY:1 += MAXBASE:MASTER:TSP - BASE:MASTER:TSP
IF MONEY:1 >= 30000 && MAXBASE:MASTER:TSP == 2500
	DRAWLINE
	PRINTFORML 時間停止能力進化到了極限！
	MAXBASE:MASTER:TSP = 4000
ELSEIF MONEY:1 >= 10000 && MAXBASE:MASTER:TSP == 1500
	DRAWLINE
	PRINTFORML 時間停止能力的使用已经炉火纯青了！
	MAXBASE:MASTER:TSP = 2500
ELSEIF MONEY:1 >= 5000 && MAXBASE:MASTER:TSP == 800
	DRAWLINE
	PRINTFORML 時間停止能力的使用已经相当在行了！
	MAXBASE:MASTER:TSP = 1500
ELSEIF MONEY:1 >= 1800 && MAXBASE:MASTER:TSP == 500
	DRAWLINE
	PRINTFORML 時間停止能力的使用已经很拿手了！
	MAXBASE:MASTER:TSP = 800
ELSEIF MONEY:1 >= 500 && MAXBASE:MASTER:TSP == 200
	DRAWLINE
	PRINTFORML 時間停止能力的使用已经有点门道了！
	MAXBASE:MASTER:TSP = 500
ELSEIF MONEY:1 >= 150 && MAXBASE:MASTER:TSP == 100
	DRAWLINE
	PRINTFORML 時間停止能力的使用已经有点入门了！
	MAXBASE:MASTER:TSP = 200
ENDIF

BASE:MASTER:TSP = MAXBASE:MASTER:TSP

IF TALENT:MASTER:2 & 2
	IF BASE:MASTER:精力 == MAXBASE:MASTER:精力 && !GETBIT(TFLAG:一日一回,8)
		FLAG:71 ++
		IF FLAG:71 >= 5 && !RAND:3
			DRAWLINE
			PRINTFORML 因为最近没有做过、所以感到烦躁……
			PRINTFORML %CALLNAME:MASTER%在被子里滚来滚去
			CALL COLORMESSAGE("气力的最大值因为烦躁暂时下降了",C_YELLOW,2,1)
			変動値 = MIN(MAXBASE:MASTER:気力 * 20 / 100, 500)
			CALL BUFF_BASE(MASTER,BASE_気力,-変動値)
		ELSEIF FLAG:71 >= 3
			TALENT:MASTER:120 = 1
			PRINTL 话说这边的小兄弟也蓄势已久了呢…
		ENDIF
	ELSE
		FLAG:71 = 0
		TALENT:MASTER:120 = 0
	ENDIF
	IF BASE:MASTER:精力 < MAXBASE:MASTER:精力 * 2 / 10 && BASE:MASTER:体力 > 500
		DRAWLINE
		PRINTFORML 今天满满的射出来了……
		PRINTFORML %CALLNAME:MASTER%舒舒服服的睡着了
		CALL COLORMESSAGE("TSP和气力最大值暂时增加了",C_YELLOW,2,1)
		変動値 = MIN(MAXBASE:MASTER:気力 / 10, 300)
		CALL BUFF_BASE(MASTER,BASE_気力,変動値)
		変動値 = MIN(MAXBASE:MASTER:TSP / 2, 500)
		CALL BUFF_BASE(MASTER,BASE_TSP,変動値)
	ENDIF
ENDIF



CALL ONANISM

SIF FLAG:酒屋買取強化 > 0
	FLAG:酒屋買取強化 --
;何の珠を得られたか
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
	CALL JUEL_CHECK(LOCAL)
	CALL DENIAL_CHECK(LOCAL)
	SIF TALENT:LOCAL:路人 
		CFLAG:LOCAL:出禁 = 2
NEXT

BEGIN ABLUP



@JUEL_CHECK(ARG)
FOR LOCAL,0,100
	IF PALAM:ARG:LOCAL < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:1*3
		LOCAL:1 = 1
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2
		LOCAL:1 = 2
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2*3
		LOCAL:1 = 10
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3
		LOCAL:1 = 20
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3*2
		LOCAL:1 = 100
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:4
		LOCAL:1 = 200
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:5
		LOCAL:1 = 1000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:6
		LOCAL:1 = 2000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:7
		LOCAL:1 = 3000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:8
		LOCAL:1 = 5000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:9
		LOCAL:1 = 8000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:10
		LOCAL:1 = 12000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:11
		LOCAL:1 = 25000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:12
		LOCAL:1 = 40000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:13
		LOCAL:1 = 60000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:14
		LOCAL:1 = 100000
	ELSE
		LOCAL:1 = 150000
	ENDIF
	;快感系
	IF LOCAL >= 0 && LOCAL < 9
		GOTJUEL:ARG:LOCAL = LOCAL:1 + EX:ARG:LOCAL * 1000
	;その他
	ELSEIF LOCAL < 30
		GOTJUEL:ARG:LOCAL = LOCAL:1
	;否定の珠
	ELSEIF LOCAL == 30 || LOCAL == 31 || LOCAL == 32
		GOTJUEL:ARG:100 += LOCAL:1
	ENDIF
NEXT
JUEL:ARG:100 += GOTJUEL:ARG:100
FOR LOCAL,0,100
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF LOCAL != 9 &&LOCAL != 30 && LOCAL != 31 && LOCAL != 32
		JUEL:ARG:LOCAL += GOTJUEL:ARG:LOCAL
NEXT
@DENIAL_CHECK(ARG)
SIF JUEL:ARG:100 == 0
	RETURN 0
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恭順 && !JUEL:ARG:欲情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恥情 && !JUEL:ARG:苦痛 && !JUEL:ARG:恐怖 && !JUEL:ARG:好意 && !JUEL:ARG:優越)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:5
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 14
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
		CASE 4
			LOCAL:2 = 21
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

@ONANISM
#DIM 欲求不満度
#DIM 満足度
#DIM 嫉妬度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加Ｖ自慰
#DIM 追加Ａ自慰
#DIM 自慰タイプ
#DIM 自慰発生閾値
VARSET LOCAL

FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	部位指定 = 0
	自慰タイプ = 0
	追加Ｖ自慰 = 0
	追加Ａ自慰 = 0
	自慰発生閾値 = LIMIT(750 - 200 * TALENT:LOCAL:容易自慰 - 100 * TALENT:LOCAL:快感応答 - 100 * TALENT:LOCAL:自己愛 + 200 * TALENT:LOCAL:自制心,500,999)
	自慰発生閾値 -= 300 * TALENT:LOCAL:自慰狂
	IF CFLAG:LOCAL:オナバレ
		PRINTFORML %CALLNAME:LOCAL%把臉埋在了枕頭裡面…
	ELSEIF CFLAG:LOCAL:积攒度 > 自慰発生閾値
		LOCAL:1 = 0
		IF TALENT:LOCAL:愛欲
			LOCAL:1 = 4
		ELSEIF TALENT:LOCAL:恋慕
			LOCAL:1 = 3
		ELSEIF TALENT:LOCAL:炮友
			;愛人もここ
			LOCAL:1 = 2
		ELSEIF TALENT:LOCAL:思慕
			LOCAL:1 = 1
		ENDIF
		IF TALENT:LOCAL:自制心 && LOCAL:1 < 5
			LOCAL:1 += 1
		ENDIF
		IF CFLAG:LOCAL:既成事実 & 合意_诶嘿嘿 && !RAND:(8 - LOCAL:1)
			PRINTFORML %CALLNAME:LOCAL%忍耐著身體的蠢動、想到明天就能和%CALLNAME:MASTER%做了、強忍著睡著了……
			自慰タイプ = 7
			;CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,7)
			CFLAG:LOCAL:积攒度 = LIMIT(CFLAG:LOCAL:积攒度 + 100, 0 ,1000)
			SIF !CFLAG:LOCAL:神社在住 
				CFLAG:LOCAL:翌日来訪 = 1
			PRINTL
		ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初始位置
			PRINTFORML 夜里、%CALLNAME:MASTER%听到了一些动静而睁开了眼睛
			PRINTFORML ……%CALLNAME:LOCAL%一边抓着%CALLNAME:MASTER%的手一边压抑着呻吟声沉溺于自慰之中……
			自慰タイプ = 2
		ELSEIF ABL:LOCAL:精液中毒 > 2 && EX:LOCAL:膣内精液 && !RAND:4
			PRINTFORML 無法忍耐的%CALLNAME:LOCAL%、伴著膣内殘留的%CALLNAME:MASTER%的精液開始了自慰……
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:無知
			LOCAL:1 = 0
			LOCAL:1 = EXP:LOCAL:自慰経験 - EXP:LOCAL:調教自慰経験
			IF LOCAL:1 >= 120 - (MIN(ABL:LOCAL:親密,6) * 10) && !FLAG:無知自動喪失禁止
				PRINTFORMW 終於%CALLNAME:LOCAL%和監護人商量了……
				PRINTFORMW %CALLNAME:LOCAL%失去了無知……
				TALENT:LOCAL:無知 = 0
			ELSEIF LOCAL:1 >= 80 - (MIN(ABL:LOCAL:親密,4) * 10)
				PRINTFORML 尚不明白自慰是自慰的%CALLNAME:LOCAL%埋頭自慰中……
			ELSEIF LOCAL:1 >= 50 - (MIN(ABL:LOCAL:親密,3) * 10)
				PRINTFORML %CALLNAME:LOCAL%到桌子角一邊按胯揮動腰肢、一邊刺激祕所……
			ELSEIF LOCAL:1 >= 30 - (MIN(ABL:LOCAL:親密,2) * 10)
				PRINTFORML %CALLNAME:LOCAL%把書夾在胯間刺激著幼小的祕裂……
			ELSEIF LOCAL:1 >= 15 - (MIN(ABL:LOCAL:親密,1) * 10)
				PRINTFORML %CALLNAME:LOCAL%在學習的時候把筆貼在了胯下、就這樣摩擦起秘縫……
			ELSE
				PRINTFORML %CALLNAME:LOCAL%躺在被窩裏一邊側躺使大腿互相摩擦一邊悶悶……
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && (TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人 || TALENT:LOCAL:愛欲 || TALENT:LOCAL:炮友) && !RAND:3
			PRINTFORML 怎么也忍耐不住了的%CALLNAME:LOCAL%、一边嗅着沾在衣服上的%CALLNAME:MASTER%的气味一边激烈的自慰着……
			LOCAL:2 = RAND:5
			IF (TALENT:LOCAL:Ｃ感度 == 1 || ABL:LOCAL:Ｃ感覚 >= 10) && LOCAL:2 == 4
				PRINTFORM 揉搓著陰核、即使潮吹了還一味地持續著動作
				部位指定 = 0
			ELSEIF (TALENT:LOCAL:Ｖ感度 == 1 || ABL:LOCAL:Ｖ感覚 >= 10) && LOCAL:2 == 3
				PRINTFORM 把手指深深地插入到蜜壷之中持續扣弄著愛液
				部位指定 = 1
				追加Ｖ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ａ感度 == 1 || ABL:LOCAL:Ａ感覚 >= 10) && LOCAL:2 == 2
				PRINTFORM 持續玩弄著蜜穴和菊穴
				部位指定 = 2
				追加Ａ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ｂ感度 == 1 || ABL:LOCAL:Ｂ感覚 >= 10) && LOCAL:2 == 1
				IF TALENT:LOCAL:胸圍 >= 1
					PRINTFORM 吸吮著自己那豊満的乳房、
				ELSEIF TALENT:LOCAL:胸圍 <= -1
					PRINTFORM 撫摸著貧瘠的胸部來回揉搓著乳頭、
				ELSE
					PRINTFORM 用下流的手法揉弄著柔軟的乳房、
				ENDIF
				PRINTFORM 高潮了好幾次
				部位指定 = 3
			ELSE
				;PRINTFORML しかし、どんなに快感を得ようとも%CALLNAME:MASTER%への想いは治まることはなく、%CALLNAME:LOCAL%は朝まで悶々とした時間を過ごした……
				PRINTFORM 因體內的快感餘韻而顫抖著身體
			ENDIF
			IF TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人
				PRINTFORML 、對%CALLNAME:MASTER%的思念平息了……
			ELSE
				PRINTFORML 、燃燒了身體的熱量還沒有平息……
			ENDIF
			追加Ｖ自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && !RAND:2
			PRINTFORM 已经无法用手指满足的%CALLNAME:LOCAL%、用从河童那买来的玩具
			IF ABL:LOCAL:Ａ感覚 >= 5
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 尽可能深的插入陰道和肛門的深处
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私处和肛門
				ELSE
					PRINTFORM 玩弄着私处和尻穴
				ENDIF
				部位指定 = 2
				追加Ａ自慰 = 1
			ELSE
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 插入陰道的深处
					部位指定 = 1
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私处
					部位指定 = 0
				ELSE
					PRINTFORM 玩弄着私处
					部位指定 = 0
				ENDIF
			ENDIF
			PRINTFORML 沉溺于自慰之中…
			自慰タイプ = 6
		ELSE
			PRINTFORML 身体变的躁动难安的%CALLNAME:LOCAL%、自己用手指玩弄起了私处…
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,自慰タイプ,部位指定)
		SIF 自慰タイプ == 7
			CONTINUE
		EXP:LOCAL:自慰経験 += ABL:LOCAL:自慰中毒 * 2 + 1
		CALL COLORMESSAGE(@"自慰経験＋{ABL:LOCAL:自慰中毒 * 2 + 1}",C_PINK,1)
		SELECTCASE ABL:LOCAL:自慰中毒
			CASE 0 TO 2
				珠入手倍率 = 5
			CASE 3 TO 5
				珠入手倍率 = 3
			CASEELSE
				珠入手倍率 = 1
		ENDSELECT
		;快C
		CALL JUEL_UP_SPECIAL(LOCAL,"快Ｃ","Ｃ感覚",珠入手倍率,1)
		;恥情
		CALL JUEL_UP_SPECIAL(LOCAL,"恥情","自慰中毒",珠入手倍率 * 2,1)
		;欲情
		CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",珠入手倍率,1)
		SIF 追加Ｖ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｖ","Ｖ感覚",珠入手倍率,1)
		SIF 追加Ａ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ａ","Ａ感覚",珠入手倍率,1)
		CFLAG:LOCAL:积攒度 /= 4
		PRINTFORML 
	ENDIF
	;まず欲望で基礎値を決める
	SELECTCASE ABL:LOCAL:欲望
		CASE 0 TO 2
			欲求不満度 = 10
		CASE 3 TO 5
			欲求不満度 = 20
		CASE 6 TO 10
			欲求不満度 = 30
		CASE 10 TO 15
			欲求不満度 = 50
		CASEELSE
			欲求不満度 = 80
	ENDSELECT

	欲求不満度 += ABL:LOCAL:自慰中毒 * 7 + (ABL:LOCAL:膣射中毒 + ABL:LOCAL:肛射中毒) * 3
	欲求不満度 = 欲求不満素質補正(LOCAL,欲求不満度)
	SELECTCASE 欲求不満度
		CASE IS > 400
			欲求不満度 = (欲求不満度 - 400) / 10 + 400
		CASE IS > 200
			欲求不満度 = (欲求不満度 - 200) / 4 + 200
		CASE IS > 100
			欲求不満度 = (欲求不満度 - 100) / 2 + 100
	ENDSELECT
	SIF 自慰タイプ
		欲求不満度 = 0
	SIF TCVAR:LOCAL:今天有否见面
		欲求不満度 /= 2
	SIF TALENT:LOCAL:妊娠
		欲求不満度 /= 5
	SIF TALENT:LOCAL:育児中
		欲求不満度 /= 2
	;溜まってる度は0~1000の間に
	CFLAG:LOCAL:积攒度 = LIMIT(CFLAG:LOCAL:积攒度 + 欲求不満度, 0, 1000)
	DEBUGPRINTFORML %CALLNAME:LOCAL%的満足度：{満足度}　欲求不満上昇値：{欲求不満度}　积攒度：{CFLAG:LOCAL:积攒度}
NEXT

@DAIRYEVENT
;とりあえず30だけ。
#DIM EVENT_DAY,30

CALL FISHER_YATES_SHAFFLE(30)
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_DAY"

DRAWLINE
FOR LOCAL,0,30
	RESULT = 0
	TRYCALLFORM DAIRY_CONDITION_EV{EVENT_DAY:LOCAL}
	IF RESULT > 0
		DEBUGPRINTFORML 每日活动{EVENT_DAY:LOCAL}发生了
		DRAWLINE
		BREAK
	ENDIF
	SIF RESULT == -1
		DEBUGPRINTFORML 每日活动{EVENT_DAY:LOCAL}未满足发生条件
NEXT
;PRINTFORMW 
PRINTFORML 


@欲求不満素質補正(ARG,ARG:1)
#FUNCTION
LOCAL = ARG:1
SIF TALENT:ARG:自制心
	LOCAL /= 2
SIF TALENT:ARG:自己愛 < 0
	LOCAL /= 2
SIF TALENT:ARG:自己愛 > 0
	LOCAL = LOCAL * 5 / 4
SIF TALENT:ARG:幼稚 && !TALENT:ARG:初潮
	LOCAL /= 4
SIF TALENT:ARG:妊娠
	LOCAL /= 4
SIF TALENT:ARG:育児中
	LOCAL /= 2
SIF CFLAG:ARG:オナバレ
	LOCAL /= 4
RETURNF LOCAL