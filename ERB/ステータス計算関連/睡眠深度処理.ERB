;FileName_SOURCE1.ERB ------------------------------ Rev1.00
;睡眠深度の減少処理
;ARG		キャラNO
;-----------------------------------------------------------
@睡眠深度減少(ARG)
SIF CFLAG:MASTER:恶作剧 != 2
	RETURN

LOCAL:1 += CUP:ARG:不快 * 10 + CUP:ARG:苦痛 * 10 + CUP:ARG:恐怖 + CUP:ARG:反感 + CUP:ARG:抑鬱 / 2
SELECTCASE CUP:ARG:欲情
	CASE 50 TO 100
		LOCAL:1 += 50
	CASE 101 TO 500
		LOCAL:1 += 250
	CASE 501 TO 1000
		LOCAL:1 += 500
	CASE IS > 1000
		LOCAL:1 += 1000
	CASEELSE
ENDSELECT
;		PRINTFORM 睡眠深度基礎値{LOCAL:1}
SELECTCASE EXP:MASTER:睡眠姦経験
	CASE 100 TO 500
		LOCAL:1 -= 100
	CASE 501 TO 1000
		LOCAL:1 -= 250
	CASE IS > 1000
		LOCAL:1 -= 500
	CASEELSE
ENDSELECT
SELECTCASE EXP:TARGET:睡眠姦経験
	CASE 100 TO 500
		LOCAL:1 -= 100
	CASE 501 TO 1000
		LOCAL:1 -= 250
	CASE IS > 1000
		LOCAL:1 -= 500
	CASEELSE
ENDSELECT
LOCAL:1 = MAX(LOCAL:1, 0)
SIF TCVAR:TARGET:睡眠薬強度 == 1
		LOCAL:1 = LOCAL:1 / 2
SIF TCVAR:TARGET:睡眠薬強度 == 2
		LOCAL:1 = LOCAL:1 / 4
SIF TCVAR:TARGET:睡眠薬強度 == 3
		LOCAL:1 = 0
;PRINTFORM 睡眠深度が{LOCAL:1}減った
TCVAR:ARG:睡眠深度 -= LOCAL:1


;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;好感度上昇計算処理
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@睡眠姦発覚(ARG)
IF TCVAR:ARG:睡眠薬強度 == 3 && 睡眠時間(ARG)
;昏睡だと起きない
ELSE
	PRINTFORML %CALLNAME:ARG%醒来了
	IF !TEQUIP:TARGET:縄
	;縄で縛っていない
		IF TALENT:ARG:恋慕
			PRINTFORML %CALLNAME:ARG%発現衣服凌亂之後吃驚的盯著%CALLNAME:MASTER%
			PRINTFORMW 整理好服装的%CALLNAME:ARG%說了一句要適可而止…
			CFLAG:ARG:好感度 += ABL:ARG:欲望
			PRINTFORML 好感度＋{ABL:ARG:欲望}
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			CALL ENDUFUFU(ARG)
		ELSEIF TALENT:TARGET:炮友 || TALENT:TARGET:愛欲
			PRINTFORML 因快感而醒來的%CALLNAME:ARG%発現%CALLNAME:MASTER%就像野獣一樣貪求著自己的身體
			PRINTFORMW %CALLNAME:ARG%擺出淫靡的姿勢誘惑著%CALLNAME:MASTER%想要更多的疼愛…
			CFLAG:ARG:好感度 += ABL:ARG:欲望 * 2
			PRINTFORML 好感度＋{ABL:ARG:欲望 * 2}
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			FOR LOCAL, 1, GET_TARGETNUM() + 1
				CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
			NEXT
		ELSE
			PRINTFORML %CALLNAME:ARG%発現衣服凌亂之後難以置信的瞪著%CALLNAME:MASTER%
			PRINTFORMW %CALLNAME:ARG%的脸上充满了愤怒……
			IF MARK:ARG:反発刻印 <= 2
				MARK:ARG:反発刻印 = 3
				PRINTFORML %CALLNAME:ARG%因憤怒而神色扭曲、說出了怨言……
				SETCOLOR C_RED
				PRINTFORMW %CALLNAME:ARG%获得了反発刻印LV3
				RESETCOLOR
			ENDIF
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			CALL ENDUFUFU(ARG)
		ENDIF
	ELSE
	;縄でしばってるならそのままヤッちゃう
		IF TALENT:ARG:恋慕
			PRINTFORML 醒來的%CALLNAME:ARG%発現自己被綁起來了
			PRINTFORMW 発現%CALLNAME:MASTER%後帯著驚訝的表情說道趕緊放開我…
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			FOR LOCAL,1, GET_TARGETNUM() + 1
				CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
			NEXT
		ELSEIF TALENT:TARGET:炮友 || TALENT:TARGET:愛欲
			PRINTFORML 因快感而醒來的%CALLNAME:ARG%注意到了自己正被%CALLNAME:MASTER%綁起來侵犯
			PRINTFORMW %CALLNAME:ARG%扭動著腰誘惑著%CALLNAME:MASTER%想被更加強烈的侵犯…
			CFLAG:ARG:好感度 += ABL:ARG:欲望 * 3
			PRINTFORML 好感度＋{ABL:ARG:欲望 * 3}
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			FOR LOCAL,1, GET_TARGETNUM() + 1
				CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
			NEXT
		ELSE
			PRINTFORML 醒來的%CALLNAME:ARG%注意到了自己正被%CALLNAME:MASTER%綁起來侵犯
			IF TALENT:TARGET:胆量 < 0
				PRINTFORMW %CALLNAME:ARG%哭著懇求請住手……
			ELSE
				PRINTFORMW %CALLNAME:ARG%滿臉憤怒大罵著%CALLNAME:MASTER%……
			ENDIF
			IF MARK:ARG:反発刻印 <= 2
				MARK:ARG:反発刻印 = 3
				PRINTFORM 讓%CALLNAME:ARG%
				IF TALENT:TARGET:胆量 < 0
					PRINTFORM 哭著
				ELSE
					PRINTFORM 因憤怒而神色扭曲、
				ENDIF
				PRINTFORML 說出了怨言……
				SETCOLOR C_RED
				PRINTFORMW %CALLNAME:ARG%获得了反発刻印LV3
				RESETCOLOR
			ENDIF
			CALL CHARA_AWAKE(ARG,1,1)
			CALL SET_PRANK(0,ARG,2)
			FOR LOCAL,1, GET_TARGETNUM() + 1
				CFLAG:(TARGET:LOCAL):诶嘿嘿 = 1
			NEXT
			TCVAR:ARG:强行 = 1
		ENDIF
	ENDIF
	CFLAG:ARG:眠姦発覚 = 1
ENDIF
