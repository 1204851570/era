;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;処女喪失時の処理
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@LOST_VIRGIN(ARG)
#DIM DYNAMIC 喪失者No
#DIMS DYNAMIC 喪失者名
#DIM DYNAMIC 取得者No
#DIMS DYNAMIC 取得者名
#DIMS 修飾語
;-------------------------------------------------
;処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == 1 && EXP_UP(1,ARG) > 0 && !TFLAG:2
	修飾語 = ""
	IF ARG == MASTER
		CFLAG:ARG:処女喪失对手 = TARGET
		喪失者No = MASTER
		喪失者名 = %CALLNAME:MASTER%
		取得者No = MASTER_POSE(SET_P,SET_V)
		;Ｐ以外で喪失した場合、MASTER_POSE(SET_P,SET_V)は0となり、結果的に取得者名まであなたになってしまう
		;よって取得者No=0ならTARGETを代入
		SIF !取得者No
			取得者No = TARGET
		取得者名 = %CALLNAME:取得者No%
	ELSE
		CFLAG:ARG:処女喪失对手 = PLAYER
		喪失者名 = %CALLNAME:ARG%
		喪失者No = ARG
		取得者名 = %CALLNAME:MASTER%
		取得者No = MASTER
	ENDIF
	IF ARG != MASTER
		;貞操観念
		IF TALENT:ARG:貞操 > 0
			IF !TALENT:ARG:恋慕
				TIMES SOURCE:ARG:情愛 , 0.60
				TIMES SOURCE:ARG:反感 , 10.00
			ENDIF
		;貞操無頓着
		ELSEIF TALENT:ARG:貞操 < 0
			TIMES SOURCE:ARG:情愛 , 0.60
			TIMES SOURCE:ARG:反感 , 0.50
		ELSEIF TALENT:ARG:無知
			TIMES SOURCE:ARG:情愛 , 0.30
			TIMES SOURCE:ARG:反感 , 0.75
			SOURCE:ARG:恐怖 += 3000
		ENDIF
		IF CFLAG:ARG:诶嘿嘿 == 2
			TIMES SOURCE:ARG:情愛 , 1.50
			TIMES SOURCE:ARG:反感 , 0.25
		ENDIF
	ENDIF
	SETCOLOR 247,171,166
	PRINTFORMW 【処女喪失】
	IF TEQUIP:ARG:50 == MASTER && GROUPMATCH(TALENT:MASTER:形状,5,6)
		PRINTFORMW %取得者名%那凶悪的肉棒、毫不留情的撕裂了%喪失者名%的純潔……
		TIMES SOURCE:ARG:苦痛, 3.00
		TIMES SOURCE:ARG:恐怖, 3.00
	ENDIF
	IF FLAG:70
		IF ARG == MASTER
			PRINTFORMW %取得者名%在无意識的情况下、\@ TALENT:ARG:路人 == 1 ? 奪走了刚遇到的# \@%喪失者名%的処女……
		ELSE
			PRINTFORMW %喪失者名%在无意識的情况下、\@ TALENT:ARG:路人 == 1 ? 被刚遇到的# \@%取得者名%奪走了処女……
		ENDIF
	ELSE
		;陥落素質による処理
		;MASTER喪失
		IF ARG == MASTER
			IF TCVAR:TARGET:烂醉
				PRINTFORMW 混濁着的%取得者名%、把%喪失者名%的処女奪走了……
			;睡眠姦、お前がパパになるんだよ！
			ELSEIF CFLAG:TARGET:睡眠
				PRINTFORMW 趁着%取得者名%睡的正香、、强行奪走了%喪失者名%的処女
			;無知ふたろり（ショタ）で処女喪失とか最高……いや、再考
			ELSEIF TALENT:TARGET:無知
				;IF GROUPMATCH(TALENT:MASTER:形状,5,6)
				;無知な相手のちんちんが入ってる場合
				IF TEQUIP:MASTER:50 == 取得者No
					PRINTFORMW 性知識匮乏的%取得者名%不知道自己做了什么、因为\@ TALENT:取得者No:2 & 2 ?強烈的快感和 # \@%喪失者名%流出的鲜血而胆怯……
				;ちんちん以外
				ELSE
					PRINTFORMW 看到了破瓜血的%取得者名%、以为自己被%喪失者名%弄伤了而慌张了起来
				ENDIF
				PRINTFORMW %喪失者名%一边强忍着破瓜的疼痛、一边让%取得者名%冷静下来
			;素質恋人
			ELSEIF TALENT:TARGET:恋人  || TALENT:TARGET:恋慕 > 1
				PRINTFORMW %取得者名%用呆住的表情、看向了%喪失者名%秘処流出的鮮血……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情愛 , 3.00
				TIMES SOURCE:TARGET:反感 , 0.10
			;恋慕
			ELSEIF TALENT:TARGET:恋慕
				PRINTFORMW %取得者名%は嬉しげな露出笑容、献出了処女%喪失者名%を看了过来……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情愛 , 2.00
				TIMES SOURCE:TARGET:反感 , 0.30
				EXP:TARGET:愛情経験 += 30
				CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			ELSEIF TALENT:TARGET:思慕
				IF TCVAR:TARGET:强行 == 1
					PRINTFORMW 在%取得者名%被快感所俘虏的同时、%喪失者名%用含着泪的眼睛看了过来……
					TIMES SOURCE:TARGET:情愛 , 0.80
					TIMES SOURCE:TARGET:反感 , 1.50
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSEIF TALENT:TARGET:貞操 >= 0
					PRINTFORMW %取得者名%は困惑しつつ、总觉得很开心似的%喪失者名%を看了过来……
					TIMES SOURCE:TARGET:情愛 , 1.30
					TIMES SOURCE:TARGET:反感 , 0.50
				ENDIF
				CFLAG:ARG:処女喪失対象 |= 喪失_思慕
			ELSE
				IF TCVAR:TARGET:强行 == 1
					TIMES SOURCE:TARGET:情愛 , 0.10
					TIMES SOURCE:TARGET:反感 , 3.00
					PRINTFORMW %取得者名%は、憤怒と戸惑いを浮かべて%喪失者名%を睨んできた……
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSE
					PRINTFORMW %取得者名%以不知道该说什么的表情、看向了和%喪失者名%的結合部……
				ENDIF
			ENDIF
		;MASTERがTARGETの膣に挿入している
		ELSEIF TEQUIP:ARG:50 == MASTER
			IF TCVAR:ARG:烂醉
				PRINTFORMW %CALLNAME:ARG%意識処在混沌之中時被奪走了処女……
			;睡眠姦　良い子は眠っている間にママにしちゃおうね～
			ELSEIF CFLAG:ARG:睡眠
				PRINTFORMW %喪失者名%的第一次在睡著的時候被奪走了……
			ELSEIF TALENT:ARG:無知
				PRINTFORMW 缺乏性知識的%喪失者名%对自己被做了什么懵懂不知、因为処女喪失的痛楚而困惑并害怕着……
				PRINTFORMW %取得者名%安慰起了%喪失者名%
			;親愛かつ相手が主人(現在親愛がないので、恋人で代用
			ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕 > 1
				PRINTFORMW %喪失者名%因为能让%取得者名%成为第一次的对象而感到无上的幸福感的様子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情愛 , 3.00
				TIMES SOURCE:ARG:反感 , 0.10
				 CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			;恋慕かつ相手が主人
			ELSEIF TALENT:ARG:恋慕
				PRINTFORMW %喪失者名%因为把处女献给了%取得者名%而感到幸福的様子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情愛 , 2.00
				TIMES SOURCE:ARG:反感 , 0.30
				EXP:ARG:愛情経験 += 30
				CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			ELSEIF TALENT:ARG:思慕
				IF TCVAR:ARG:强行 == 1
					PRINTFORMW 被怀有好感的对象硬上了的%喪失者名%、眼泪如雨点般开始洒落下来……
					TIMES SOURCE:ARG:情愛 , 0.80
					TIMES SOURCE:ARG:反感 , 1.50
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSEIF TALENT:ARG:貞操 >= 0
					PRINTFORMW 尽管怀有好感、%喪失者名%还是对被%取得者名%夺去貞操感到不知如何是好……
					TIMES SOURCE:ARG:情愛 , 1.30
					TIMES SOURCE:ARG:反感 , 0.80
				ENDIF
				CFLAG:ARG:処女喪失対象 |= 喪失_思慕
			ELSE
				IF TCVAR:ARG:强行 == 1
					TIMES SOURCE:ARG:情愛 , 0.10
					TIMES SOURCE:ARG:反感 , 3.00
					IF CFLAG:ARG:負け犬
						PRINTFORMW %喪失者名%作为敗北的代价、被%取得者名%奪走了処女……
					ELSE
						PRINTFORMW %喪失者名%的抵抗毫无作用、被%取得者名%奪走了処女……
					ENDIF
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
					IF TALENT:ARG:路人 == 1
						CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","され今はもう老實","され、夺去了処女")%
						CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","肛門夺去了処女","夺去了処女")%
					ENDIF
				ELSEIF TALENT:ARG:路人 == 1
					PRINTFORMW 看到了自己秘処流下的鲜血、%喪失者名%对把处女献给了连面都没见过的%取得者名%这件事感到了些许的後悔……
				ELSE
					PRINTFORMW %喪失者名%半推半就地被%取得者名%奪走了処女……
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	IF CSTR:ARG:処女喪失履歴 == ""
		IF GROUPMATCH(TCVAR:喪失者No:counter行動, 80, 83, 85)
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "阴茎")
		ELSEIF TCVAR:取得者No:counter行動 == 57
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "指")
		ELSEIF SELECTCOM == 44
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "振動棒")
		ELSE
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "阴茎")
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
;-------------------------------------------------
;再生処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == 2 && EXP_UP(1,ARG)
	TCVAR:ARG:破瓜 = 2
	;ビデオ撮影
	IF TEQUIP:28
		;性交
		IF TEQUIP:ARG:50
			LOCALS = LV1/
		ELSEIF TEQUIP:ARG:13
			LOCALS = LV3/
		;その他
		ELSE
			LOCALS = LV4/
		ENDIF
		TSTR:1 += LOCALS
	ENDIF
	PRINTW 再生処女喪失
ENDIF
;-------------------------------------------------
;無自覚処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == -1 && EXP_UP(1,ARG) && CFLAG:ARG:292 == 1 && !FLAG:70 && !TCVAR:ARG:烂醉 && !CFLAG:MASTER:恶作剧
	TCVAR:ARG:破瓜 = -1
	CFLAG:ARG:292 = 0
	PRINTW 処女喪失？
	SETCOLOR 247,171,166
	PRINTFORMW %CALLNAME:ARG%在感到剧烈痛楚的同时困惑不已…
	RESETCOLOR
ENDIF

@童貞無接吻経験喪失(ARG)
#DIM シチュエーション
;シチュエーション	 0:通常
;					 1:デート帰り
;					 2:オナバレ
;					 4:ペニスに
;					 8:自発的に
;					16:無自覚
;					32:買春
シチュエーション = 0
VARSET LOCALS
FONTBOLD
SETCOLOR 255,100,255
IF !TALENT:ARG:非童貞 && TALENT:ARG:性別 & 2
	IF TCVAR:ARG:童貞喪失 == 1
		PRINTFORMW 童貞喪失(%CALLNAME:ARG%)
		TALENT:ARG:非童貞 = 1
	ENDIF
ENDIF

;キス未経験喪失
IF ARG == MASTER && TALENT:ARG:無接吻経験 && GROUPMATCH(SELECTCOM,2,20,312)
	シチュエーション |= 8
	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,2) && TALENT:TARGET:性別 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(ARG, TARGET, シチュエーション)
	ENDIF
ELSEIF TALENT:MASTER:無接吻経験 && GROUPMATCH(TCVAR:ARG:counter行動,30,39,60,71,91)
	IF GROUPMATCH(TCVAR:ARG:counter行動,30,39,60)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(TCVAR:ARG:counter行動,71,91) && TALENT:ARG:性別 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ENDIF
ENDIF
IF TALENT:ARG:無接吻経験 && (TCVAR:ARG:ＦＫ喪失 || GROUPMATCH(TCVAR:ARG:counter行動,30,39,60,51))
	;無自覚
	SIF CFLAG:ARG:睡眠 || FLAG:70
		シチュエーション |= 16
	;自発的に
	SIF GROUPMATCH(TCVAR:ARG:counter行動,30,39,60,51)
		シチュエーション |= 8
	;ペニスに
	SIF TCVAR:ARG:ＦＫ喪失 == 2 || GROUPMATCH(TCVAR:ARG:counter行動,51)
		シチュエーション |= 4

	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		;デコチューを除外する
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,81,88,140,337) || GROUPMATCH(TCVAR:ARG:counter行動,30,60,51)
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF TCVAR:ARG:counter行動 == 39
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション, "糖と一緒に")
	ENDIF
ENDIF

;アナル処女喪失
IF CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止 || !CFLAG:ARG:尻穴処女喪失記念日
	IF TCVAR:ARG:Ａ喪失発生 
		;時間停止中に経験済み
		IF !FLAG:70 && ARG && CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止
			SIF FIRSTTIME("Ａ破瓜",0)
				PRINTFORMW 肛門処女喪失？(%CALLNAME:ARG%)
			TCVAR:ARG:Ａ破瓜 = -1
		;完全に初めて
		ELSEIF !CFLAG:ARG:尻穴処女喪失記念日
			PRINTFORMW 肛門処女喪失(%CALLNAME:ARG%)
			IF CSTR:ARG:路人子プロフィール２ != ""
				CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","され今はもう老實","され、肛門夺去了処女")%
			ENDIF
			IF SELECTCOM == 45
				LOCALS = 肛門振動棒
			ELSEIF SELECTCOM == 46
				LOCALS = 肛門ビーズ
			ELSE
				LOCALS = 阴茎
			ENDIF
			IF !FLAG:70
				TCVAR:ARG:Ａ破瓜 = 1
			ELSE
				TCVAR:ARG:Ａ破瓜 = -2
			ENDIF
			IF ARG == TARGET
				CALL SET_HISTORY_LOST_A(ARG, PLAYER, @"%LOCALS%")
			ELSE
				CALL SET_HISTORY_LOST_A(ARG, TARGET, @"%LOCALS%")
			ENDIF
			
		ENDIF
	ENDIF
ENDIF
FONTREGULAR
RESETCOLOR

@LOST_VIRGIN_STOP(ARG)
SIF !SHIRAHU(ARG)
	RETURN
IF CFLAG:ARG:破瓜中止口上有
	;口上側から干渉可能に
	TRYCALLFORM M_KOJO_COLOR_K{ARG}
	TRYCALLFORM M_KOJO_EVENT_K{ARG}_LOST_VIRGIN_STOP
	IF RESULT == 1
		RETURN -1
	ELSE
		RETURN 0
	ENDIF
	RESETCOLOR
ELSEIF !TALENT:ARG:無知 && !TALENT:ARG:恋慕 && !TALENT:ARG:恋人 && !TCVAR:ARG:不耐烦 && !CFLAG:恶作剧
;貞操観念持ちor貞操普通かつ思慕なし
	IF TALENT:ARG:貞操 > 0 || (!TALENT:ARG:貞操 && !TALENT:ARG:思慕)
		PRINTFORMW %CALLNAME:ARG%表示这种事还是等一段时间再考虑吧
		IF (EXP:ARG:60 + EXP:ARG:61 + EXP:ARG:62 + EXP:ARG:63 + EXP:ARG:64) == 0
			IF TALENT:ARG:貞操 > 0
				PRINTFORMW 果然持身甚紧呢……
			ELSE
				PRINTFORMW 意外坚固的防线呢……
			ENDIF
		ELSE
			PRINTFORMW 麻烦的臭女人……
		ENDIF
		CALL ASK_YN("あきらめる","不收自己靠过来的女人是男人的恥辱")
		IF RESULT == 1
			IF TALENT:ARG:貞操 > 0 && ABL:ARG:戦闘能力 > ABL:MASTER:戦闘能力 && !TCVAR:ARG:强行
				PRINTFORMW %CALLNAME:MASTER%虽然想霸王硬上弓、但因为%CALLNAME:ARG%相当激烈的抵抗而放弃了
					RETURN 1
			ELSEIF TALENT:ARG:貞操 > 0
				PRINTFORMW %CALLNAME:ARG%流着眼泪拒绝了%CALLNAME:MASTER%
				PRINTL [0] 真的那么讨厌做就算了
				PRINTL [1] 上吧
				INPUT
				IF !RESULT
					PRINTFORMW %CALLNAME:ARG%松了一口气…
					PRINTFORMW %CALLNAME:MASTER%有点受伤
					RETURN -1
				ELSE
					PRINTFORMW %CALLNAME:MASTER%压在了不情愿的%CALLNAME:ARG%身上…
					TCVAR:ARG:不耐烦 = 1
					TCVAR:ARG:强行 = 1
					RETURN 0
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:MASTER%强行压了上去…
				TCVAR:ARG:不耐烦 = 1
				RETURN 0
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:ARG%松了一口气…
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SET_FIRSTKISS(ARG, 接吻相手, シチュエーション, 修飾語)
#DIM 接吻相手
;シチュエーション	 0:通常
;					 1:デート帰り
;					 2:オナバレ
;					 4:ペニスに
;					 8:自発的に
;					16:無自覚
;					32:買春
#DIM シチュエーション
#DIMS 修飾語
#DIM 自己

VARSET LOCALS
自己 = 0

;キス未経験が無ければ帰る
SIF !TALENT:ARG:無接吻経験
	RETURN
SIF TALENT:ARG:無接吻経験 < 0 && シチュエーション & 16
	RETURN

;FLAG:ＦＫ判定によっては帰る
SIF !(FLAG:ＦＫ判定 & 1) && シチュエーション & 16
	RETURN
SIF !(FLAG:ＦＫ判定 & 2) && シチュエーション & 4
	RETURN


IF ARG == 接吻相手
	IF ARG == MASTER
		接吻相手 = TARGET
	ELSE
		接吻相手 = MASTER
	ENDIF
ENDIF

SETCOLOR 247,171,166
PRINTFORM 無接吻経験喪失
SIF シチュエーション & 16
	PRINT ？
PRINTFORMW (%CALLNAME:ARG%)
RESETCOLOR

CFLAG:ARG:初吻喪失記念日 = DAY
TALENT:ARG:無接吻経験 = 0
CFLAG:ARG:初吻喪失对手 = 接吻相手

;対象と修飾語決定
IF TALENT:ARG:思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF ARG == MASTER && TALENT:(接吻相手):恋人
	自己 = 1
	LOCALS:0 = 爱着的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	自己 = 1
	LOCALS:0 = 爱着的
ENDIF
IF シチュエーション & 4
	LOCALS:1 = の阴茎
	CFLAG:ARG:初吻喪失対象 = 1
ENDIF
SIF シチュエーション & 8
	自己 = 1
IF シチュエーション & 16
	IF FLAG:70
		LOCALS:2 = 在時間停止中
	ELSEIF CFLAG:ARG:睡眠
		LOCALS:2 = 在沉睡中
	ELSE
		LOCALS:2 = 在自己不知道的时候
	ENDIF
	CFLAG:ARG:初吻喪失対象 = 2
	TALENT:ARG:無接吻経験 = -1
ENDIF
SIF TALENT:ARG:無知 && シチュエーション & 4
	LOCALS:2 = %LOCALS:2%连这代表什么都不知道

;場所決定
IF シチュエーション & 1
	CSTR:ARG:初吻位置 = 約會後回去的路上
	SIF ARG != MASTER
		自己 = 1
ELSEIF シチュエーション & 2
	IF ARG != MASTER
		CSTR:ARG:初吻位置 = 自室
	ELSE
		CSTR:ARG:初吻位置 = %CALLNAME:ARG%宅
	ENDIF
ELSE
	IF CHK_DATENOW(CFLAG:ARG:约会中) && CFLAG:ARG:現在位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_SPOT()%
	ELSEIF CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_PLACE()%の%DATENAME_SPOT()%
	ELSE
		CSTR:ARG:初吻位置 = %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
	ENDIF
ENDIF

;動詞決定
IF 修飾語 == "糖と一緒に"
	LOCALS:1 += "和"
	LOCALS:3 = 交换了
ELSEIF 自己
	LOCALS:1 += "に"
	LOCALS:3 = 献上了
ELSEIF シチュエーション & 32
	LOCALS:1 += "に"
	LOCALS:3 = 奪走了
ELSE
	LOCALS:1 += "に"
	LOCALS:3 = 被奪走了
ENDIF
IF CSTR:ARG:初吻喪失履歴 == ""
	CSTR:ARG:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%で%LOCALS:2%%LOCALS:0%%CALLNAME:接吻相手%%LOCALS:1%%修飾語%初吻を%LOCALS:3%
ELSE
	CSTR:ARG:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%で%LOCALS:2%%LOCALS:0%%CALLNAME:接吻相手%%LOCALS:1%%修飾語%初吻を%LOCALS:3%￥ＢＲ%CSTR:ARG:初吻喪失履歴%
ENDIF
;既成事実もつけておく
SIF ARG != MASTER && !(シチュエーション & 16)
	SETBIT CFLAG:ARG:既成事実, 0
;愛情経験もここで入れることにする
SIF ARG != MASTER && !(シチュエーション & 16)
	EXP:ARG:愛情経験 += 5

;===================================================================================
;履歴文字列設定用関数群
;対象が未記入だと"ペニス"が代入されます。
;またその場合含め対象がペニス"だと相手が生えてない場合自動でペニスバンドになります。
;場所が未記入だと自動で位置を取得します。
;時間停止中だとか酔いつぶれてるだとかは自動で代入されます。
;各種フラグ処理も関数側で設定します。
;つまり呼び出し側で分岐入れる必要がほとんどなくなりました。
;ARGがあなたのペニスで処女喪失したなら極端な話CALL SET_HISTORY_LOST_V(ARG)だけでOK。
;やったね。凄いね。
;===================================================================================

@SET_HISTORY_LOST_A(ARG,相手,対象,場所,修飾語)
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語

CFLAG:ARG:尻穴処女喪失記念日 = DAY
CFLAG:ARG:尻穴処女喪失对手 = 相手

CSTR:ARG:尻穴処女喪失履歴 = %HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,"尻穴処女")%

@SET_HISTORY_LOST_V(ARG,相手,対象,場所,修飾語)
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語

;非処女化、処女喰いフラグ、喪失記念日なども関数側で設定
SIF ARG != MASTER
	FLAG:享用処女 ++
CFLAG:ARG:処女喪失記念日 = DAY
TALENT:ARG:処女 = 0
TCVAR:ARG:破瓜 = 1
CFLAG:ARG:処女喪失对手 = 相手
CSTR:ARG:処女喪失履歴 = %HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,"処女")%

@HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,喪失したもの)
#FUNCTIONS
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語
#DIMS 喪失したもの
#DIMS 状況
#DIM 自己

VARSET LOCALS
自己 = 0
IF (ARG != MASTER && ARG != TARGET) || ARG == MASTER
	LOCALS:0 = 
ELSEIF TALENT:ARG:思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	自己 = 1
	LOCALS:0 = 爱着的
ELSEIF !CFLAG:ARG:面識 && ARG != MASTER
	LOCALS:0 = 在这之前面都没有见过的
ELSEIF TALENT:ARG:路人 == 1
	LOCALS:0 = 初対面の
ENDIF

SIF !SHIRAHU(ARG)
	自己 = 0
SIF 対象 == ""
	対象 = 阴茎

IF FLAG:70
	状況 = 時間停止中に
ELSEIF TCVAR:ARG:烂醉
	状況 = 喝得酩酊大醉的时候に
ELSEIF CFLAG:ARG:睡眠
	状況 = 在沉睡中
ELSEIF ARG != MASTER && ARG != TARGET
	状況 = 就像%CALLNAME:MASTER%期望的一様
ELSE
	状況 =
ENDIF

SIF ARG == MASTER && TALENT:相手:無知
	状況 += "什么都不知道的"

IF 対象 == "阴茎"
	IF TALENT:相手:2 & 2
		LOCALS:1 = %CALLNAME:相手%の%TINKO_SIMPLE(相手)%に
	ELSE
		LOCALS:1 = %CALLNAME:相手%に穿戴式假陰茎で
	ENDIF
ELSEIF 対象 == "指"
	LOCALS:1 = %CALLNAME:相手%の指に
ELSE
	LOCALS:1 = %CALLNAME:相手%に%対象%で
ENDIF
SIF 場所 == ""
	場所 = %PLACE_NAME(ARG)%
IF 修飾語 == ""  && SHIRAHU(ARG)
	IF TCVAR:ARG:强行 && CFLAG:ARG:負け犬
		修飾語 = 作为敗北的代价
		自己 = 0
	ELSEIF TCVAR:ARG:强行
		修飾語 = 霸王硬上弓
		自己 = 0
	ELSEIF TALENT:ARG:無知
		修飾語 = 什么都不知道的
		自己 = 0
	ELSEIF ARG != MASTER && ARG != TARGET
		修飾語 = 在不満中
	ELSEIF TALENT:ARG:恋人
		修飾語 = 在幸福感中
	ELSEIF CFLAG:ARG:诶嘿嘿 == 2
		修飾語 = 自ら
		自己 = 1
	ENDIF
ENDIF

IF 自己
	LOCALS:3 = 献上了
ELSEIF ARG == MASTER || 修飾語 == "治療と称して"
	LOCALS:3 = 给与了
ELSEIF 修飾語 == "お金を払って"
	LOCALS:3 = 奪走了
ELSE
	LOCALS:3 = 被奪走了
ENDIF

LOCALS:10 = %PRINT_DATE_F(DAY)%、%状況%%場所%で%LOCALS:0%%LOCALS:1%%修飾語%%喪失したもの%を%LOCALS:3%

RETURNF LOCALS:10

;あなたの場合イベントでCSVで定義されていない相手と致すこともありうるのでより自由度の高いものを
@SET_FIRSTKISS_MASTER(ARGS)
IF TALENT:MASTER:無接吻経験
	CALL COLORMESSAGE(@"無接吻経験喪失(%CALLNAME:MASTER%)", C_D_PINK, 2)
	CFLAG:MASTER:初吻喪失記念日 = DAY
	TALENT:MASTER:無接吻経験 = 0
	CSTR:MASTER:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%ARGS%
ENDIF