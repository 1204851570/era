@強行开始新的游戏
;パンツ保管庫。200人分30種類まで対応の優れもの
#DIM 胖次收藏,200,40
#DIM PREV_MASTER
#DIM PREV_BASE,  2
#DIM BONUS_BASE, 2
#DIM RETAIN_CFLAG, 150

CALL BUFF_RESET

;周回前の成長分を記録
;毎周あなたカスタムして体力気力2500スタートすると周回のたびに+500になる気がするが
;バイツァ・ダストの解禁日からしてそんな大した効率にもならんだろ感
;こっちが悩んでてもデバッグコンソールやERBでちょちょっと書き換えられたら手ぇだせねぇし
PREV_MASTER = FLAG:扮演
PREV_BASE:0 = MAXBASE:MASTER:体力
PREV_BASE:1 = MAXBASE:MASTER:気力

BONUS_BASE:0 = MAX(0, MAXBASE:MASTER:体力 - CSVBASE(FLAG:扮演, GETNUM(BASE, "体力")))
BONUS_BASE:1 = MAX(0, MAXBASE:MASTER:気力 - CSVBASE(FLAG:扮演, GETNUM(BASE, "気力")))
VARSET JUEL
VARSET MOBGIRL_PLACE, 99
;使用LOCAL配列要素
;5：なりきりキャラの出禁状態
;6：今周回のキャラ人数（CHARANUM退避用）

;FLAGリセット
;残されるFLAG
;11：周回数
;560：願掛け確認用。確認後リセット
;561：精力強化回数
;563：重婚可能人数（未使用）
;564-567：恋慕／恋人特典（未使用）
;700：パンツコレクション（種類数）
;701-702：実績解除関係
;850：清掃中毒治療回数
;1007：生理周期可視化のON/OFF
;1028：察知モード
;1030：なりきり
;1068：引継ぎ設定保存済み


FOR LOCAL,0,1500
	SELECTCASE LOCAL
	CASE 11,560,561,563 TO 567,700 TO 709, 850,1007,1028,1030,1068
		CONTINUE
	CASEELSE
		FLAG:LOCAL = 0
	ENDSELECT
NEXT

;なりきりキャラの出禁状態をチェック、出禁フラグはLOCAL:5に保存
SIF FLAG:扮演
	LOCAL:5 = CFLAG:(FLAG:扮演):出禁

CFLAG:MASTER:招待 = 0
CFLAG:MASTER:育児人数 = 0
CFLAG:MASTER:兒童人数 = 0
FLAG:周回数 ++
FOR LOCAL,1,CHARANUM
	RETAIN_CFLAG:LOCAL = CFLAG:LOCAL:口上用引継ぎ枠
	IF TALENT:LOCAL:恋人 || GETBIT(CFLAG:LOCAL:继承, 1)
		SETBIT TFLAG:(LOCAL + 500), 1
	ELSEIF TALENT:LOCAL:恋慕 || GETBIT(CFLAG:LOCAL:继承, 0)
		SETBIT TFLAG:(LOCAL + 500), 0
	ENDIF
	SIF TALENT:LOCAL:自慰狂 || GETBIT(CFLAG:LOCAL:继承, 2)
		SETBIT TFLAG:(LOCAL + 500), 2
	SIF TALENT:LOCAL:淫壺 || GETBIT(CFLAG:LOCAL:继承, 3)
		SETBIT TFLAG:(LOCAL + 500), 3
	SIF TALENT:LOCAL:尻穴狂 || GETBIT(CFLAG:LOCAL:继承, 4)
		SETBIT TFLAG:(LOCAL + 500), 4
	SIF TALENT:LOCAL:淫乳 || GETBIT(CFLAG:LOCAL:继承, 5)
		SETBIT TFLAG:(LOCAL + 500), 5
	SIF TALENT:LOCAL:接吻魔 || GETBIT(CFLAG:LOCAL:继承, 6)
		SETBIT TFLAG:(LOCAL + 500), 6
	FOR LOCAL:1,0,40
		胖次收藏:LOCAL:(LOCAL:1) = CFLAG:LOCAL:(LOCAL:1 + 100)
	NEXT
NEXT
;リセット前のキャラ人数をLOCAL:6に控えておく。
LOCAL:6 = CHARANUM

;あなた以外全員リセット。
;ここでCHARANUMの値が変わるので注意
PICKUPCHARA 0

;リセット前のキャラ人数でFOR文を回す
FOR LOCAL, 1, LOCAL:6
	;CSVファイルの存在判定
	IF EXISTCSV(LOCAL)
		IF GETCHARA (LOCAL) == -1
			ADDCHARA LOCAL
			TARGET = LOCAL
			RESULT = 0
			TRYCALLFORM M_KOJO_K{NO:LOCAL}
			SIF RESULT
				CFLAG:LOCAL:口上実装状況 = 1
		ENDIF
		;効率化追求のため引き継ぎフラグと同時に回す
		;ファイルチェックはすでにしてるんだからその中で回せば済む話でした（反省）
		FOR LOCAL:1,0,40
			;引き継ぎフラグのセット
			SIF LOCAL:1 < 7 && GETBIT (TFLAG:(LOCAL + 500), LOCAL:1)
				SETBIT CFLAG:LOCAL:继承, LOCAL:1
			;パンツコレクションの引き継ぎ
			CFLAG:LOCAL:(LOCAL:1 + 100) = 胖次收藏:LOCAL:(LOCAL:1)
			CFLAG:LOCAL:口上用引継ぎ枠 = RETAIN_CFLAG:LOCAL
		NEXT
	ENDIF
	;キス未経験のリセット
	SIF EXP:LOCAL:接吻経験 == 0
		 TALENT:LOCAL:無接吻経験 = 1
NEXT
;ターゲット・アシスタント初期化、貸本関係リセット
TARGET = 0
ASSI = 0
PBAND = 12
;パッチにて追加部分
	ITEM:101 = 0
	ITEM:102 = 0
	ITEM:103 = 0
	ITEM:104 = 0
	ITEM:105 = 0
;デフォルトオプション
CALL DEFAULT_OPTION
;初期地点設定
FOR LOCAL, 0, CHARANUM
	;開始位置
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初始位置
	;服装
	CALL SELECT_CLOTHES(LOCAL, "便服")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_便装")
	CALL 内衣確認リセット(LOCAL)
	;これ多分問題なさそうだけど一応
	CFLAG:LOCAL:睡衣 = 0
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT

;MASTER起床時間セット（7時）
TIME:3 = 420

;MASTER睡眠中フラグON
CALL SET_SLEEP(1, MASTER, 0)

;日付１日目
DAY = 1
DAY:2 = 1
DAY:3 = 1

BASE:MASTER:体力 = MAXBASE:MASTER:体力
BASE:MASTER:気力 = MAXBASE:MASTER:気力
BASE:MASTER:精力 = MAXBASE:MASTER:精力


CFLAG:MASTER:就寝時間 = 0
CALL SET_NOUMIN
CALL SET_MAINHOME
STR:16 = 破舊小屋
FLAG:酒虫 = 1

;あなたの既成事実及び[恋人]をクリア
TRYCALLFORM IS_MORELOVEER
IF RESULT == 1
	SIF GETBIT(CFLAG:MASTER:既成事実 , 2)
		CLEARBIT CFLAG:MASTER:既成事実, 2
ELSE
	CLEARBIT CFLAG:MASTER:既成事実, 2
ENDIF
TALENT:MASTER:恋人 = 0

;なりきっているキャラの出禁をチェック
SIF FLAG:扮演
	CFLAG:(FLAG:扮演):出禁 = LOCAL:5

CALL VIRGIN_SET
CALL 特典
CALL SET_CHARISMA("RESET")

PRINTFORML 要重置%CALLNAME:MASTER%的経験・能力麼？
CALL COLORMESSAGE(@"在实绩取得的素质是无法取消的所以不推荐",C_YELLOW,1)
CALL ASK_YN
IF !RESULT
	FOR LOCAL,0, 150
		EXP:MASTER:LOCAL = 0
		SIF LOCAL < 100
			ABL:MASTER:LOCAL = 0
	NEXT
	PRINTFORMW 已经重置。
ENDIF

PRINTFORML 要做%CALLNAME:MASTER%的个性化么？
CALL ASK_YN
IF !RESULT
	CALL CUSTOM_TERMINAL(MASTER)
ENDIF
;BASEの成長値の加算処理
;一応処理は書いたけどなりきり先って変更できたっけ？
LOCAL = 0
IF BONUS_BASE:0 > 0 && (PREV_MASTER != FLAG:扮演 || MAXBASE:MASTER:体力 != PREV_BASE:0)
	PRINTFORML "之前的世界"的経験是%CALLNAME:MASTER%的程度
	PRINTFORML ……%CALLNAME:MASTER%的体力有{BONUS_BASE:0}程度的増加
	MAXBASE:MASTER:体力 += BONUS_BASE:0
	BASE:MASTER:体力 = MAXBASE:MASTER:体力
	LOCAL ++
ENDIF
IF BONUS_BASE:1 > 0 && (PREV_MASTER != FLAG:扮演 || MAXBASE:MASTER:気力 != PREV_BASE:1)
	SIF !LOCAL
		PRINTFORML "之前的世界"的経験是%CALLNAME:MASTER%的程度
	PRINTFORML ……%CALLNAME:MASTER%的気力有{BONUS_BASE:1}程度的増加
	MAXBASE:MASTER:気力 += BONUS_BASE:1
	BASE:MASTER:気力 = MAXBASE:MASTER:気力
	LOCAL ++
ENDIF
SIF LOCAL
	WAIT
CALL TINKO_DECIDE(MASTER)
;SYSTEMCALL to SHOW_SHOP
BEGIN SHOP

@特典
PRINTW ＊恋人特典＊
PRINTW 可以对曾经成为恋人的人物的素質进行变更
$LOOP
DRAWLINE
FOR LOCAL,1,CHARANUM
SIF GETBIT (CFLAG:LOCAL:继承, 1)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [997] 引継ぎ設定をセーブ
PRINTL [998] 引継ぎ設定をロード 
PRINTL [999] 继续
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:继承, 1)
	CALL CUSTOM_TERMINAL(RESULT)
	GOTO LOOP
ENDIF
IF RESULT == 997
	CALL SaveTalentSet
	GOTO LOOP
ENDIF
IF RESULT == 998
	CALL LoadTalentSet
	GOTO LOOP
ENDIF

PRINTW ＊恋慕特典＊
PRINTW 可以对曾经取得恋慕的人物的一部分素質进行变更
$LOOP2
DRAWLINE
FOR LOCAL,1,CHARANUM
SIF GETBIT (CFLAG:LOCAL:继承, 0)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [997] 引継ぎ設定をセーブ
PRINTL [998] 引継ぎ設定をロード 
PRINTL [999] 继续
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:继承, 0)
	CALL LIMITED_CUSTOM(RESULT)
	GOTO LOOP2
ENDIF
IF RESULT == 997
	CALL SaveTalentSet
	GOTO LOOP2
ENDIF
IF RESULT == 998
	CALL LoadTalentSet
	GOTO LOOP2
ENDIF
FOR LOCAL,1,CHARANUM
	SIF TCVAR:LOCAL:感度上昇部位
		TALENT:LOCAL:(TCVAR:LOCAL:感度上昇部位) ++
NEXT

@SaveTalentSet
#DIM C_ID
FOR C_ID,1,CHARANUM
	FOR LOCAL,0,160
		TalentSet:C_ID:LOCAL = TALENT:C_ID:LOCAL
	NEXT
NEXT
FLAG:引継ぎ設定保存済み = 1
PRINTFORMW 引継ぎ設定をセーブしました
@LoadTalentSet
#DIM C_ID
IF !FLAG:引継ぎ設定保存済み
	PRINTFORMW 引継ぎ設定がありません
	RETURN
ENDIF
FOR C_ID,1,CHARANUM
	FOR LOCAL,0,160
		TALENT:C_ID:LOCAL = TalentSet:C_ID:LOCAL
	NEXT
NEXT
PRINTFORMW 引継ぎ設定をロードしました