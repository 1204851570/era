@UPDATE
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力値
#DIM 最低経験数
#DIM 育児中の兒童
#DIM 兒童設定初回
#DIM 口上実装チェック, 人物数量上限
#DIM リセット画面
#DIM 技能素質更新用

PRINTL 旧版本不存在的人物・初期素質・FLAG等可能会产生混乱。

VARSET 口上実装チェック
リセット画面 = 0
FOR C_ID, 51, 人物数量上限
	IF C_ID < CHARANUM
		SIF TALENT:C_ID:路人
			DELCHARA C_ID
	ENDIF
	IF GETCHARA (C_ID) == -1
		ADDCHARA C_ID
		TARGET = C_ID
		RESULT = 0
		TRYCALLFORM M_KOJO_K{NO:C_ID}
		SIF RESULT
			CFLAG:C_ID:口上実装状況 = 1
	ENDIF
	IF TALENT:C_ID:路人
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT

IF !CFLAG:MASTER:兒童人数
	FOR C_ID, 1, CHARANUM
		SIF TALENT:C_ID:路人
			CONTINUE
		CFLAG:MASTER:兒童人数 += CFLAG:C_ID:兒童人数
		CFLAG:MASTER:育児人数 += CFLAG:C_ID:育児人数
	NEXT
ENDIF
SIF !CM_LIMIT
	CM_LIMIT = 100
YOGORE:348 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	;CSTR:仕事情報をCSTR:3に移行する手続き
	IF CSTR:C_ID:3 == "" && CSVCSTR(C_ID, 3) != ""
		CSTR:C_ID:3 = %CSVCSTR(C_ID, 3)%
		CSTR:C_ID:1 = 
	ENDIF
	TALENT:C_ID:186 = 0
	IF TALENT:C_ID:恋慕
		TALENT:C_ID:思慕 = 0
		TALENT:C_ID:炮友 = 0
		TALENT:C_ID:愛欲 = 0
	ENDIF
	CALL 履歴文字列変換(C_ID)
	IF TALENT:C_ID:育児中
		FOR LOCAL:1, 1, 11
			SIF (CHILD_BIRTHDAY:C_ID:(LOCAL:1) > CHILD_BIRTHDAY:C_ID:(LOCAL:1 - 1)) || (LOCAL:1 - 1) == 0
				育児中の兒童 = LOCAL:1
		NEXT
		TALENT:C_ID:育児中 = 育児中の兒童
	ENDIF
	SIF !TALENT:C_ID:妊娠
		CFLAG:C_ID:妊娠自覚状態 = 0
	SIF !CFLAG:C_ID:依頼状況
		CFLAG:C_ID:依頼内容 = 0
	
	FOR LOCAL:1, 0, 3
		SIF !EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1) = 1 + RAND:3
		SIF !EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1) = 1 + RAND:3
	NEXT

	;種族系素質を更新
	FOR LOCAL:1, 190, 198
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	;水棲、氷精、種族(未使用？)、半白澤、動物耳も変動しないと思われるので更新
	FOR LOCAL:1, 163, 167
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	
	;技能系素質
	技能素質更新用 = MAX(ABL:C_ID:釣魚技能, CSVTALENT(C_ID, 47))
	TALENT:C_ID:釣りLv = MAX(TALENT:C_ID:釣りLv, 技能素質更新用)
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "釣魚経験"), TALENT:C_ID:釣りLv)
	
	技能素質更新用 = MAX(ABL:C_ID:採集技能, CSVTALENT(C_ID, 48))
	TALENT:C_ID:採集Lv = MAX(TALENT:C_ID:採集Lv, 技能素質更新用)
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "採集経験"), TALENT:C_ID:採集Lv)
	
	技能素質更新用 = MAX(ABL:C_ID:調合技能, CSVTALENT(C_ID, 49))
	TALENT:C_ID:調合Lv = MAX(TALENT:C_ID:調合Lv, 技能素質更新用)
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "調合経験"), TALENT:C_ID:調合Lv)

	TALENT:C_ID:痛覚 = CSVTALENT(C_ID, GETNUM(TALENT, "痛覚"))
	TALENT:C_ID:動物耳 = CSVTALENT(C_ID, GETNUM(TALENT, "動物耳"))
	SIF TALENT:C_ID:回復速度 < CSVTALENT(C_ID, GETNUM(TALENT, "回復速度")) && !TALENT:C_ID:妊娠 && CFLAG:C_ID:出産経過日 > 20
		TALENT:C_ID:回復速度 = CSVTALENT(C_ID, GETNUM(TALENT, "回復速度"))
	;多分大丈夫だと思うが、後天的な取得処理を見込んで判定
	SIF TALENT:C_ID:掃除係 == 0
		TALENT:C_ID:掃除係 = CSVTALENT(C_ID, GETNUM(TALENT, "掃除係"))
	SIF TALENT:C_ID:音感 == 0
		TALENT:C_ID:音感 = CSVTALENT(C_ID, GETNUM(TALENT, "音感"))
	SIF TALENT:C_ID:音楽知識 == 0
		TALENT:C_ID:音楽知識 = CSVTALENT(C_ID, GETNUM(TALENT, "音楽知識"))
	SIF GETBIT(TALENT:C_ID:性別, 1) && !TALENT:C_ID:形状
		CALL TINKO_DECIDE(C_ID)

	;来訪確率補正を読み込み
	FOR LOCAL:1, 420, 432
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	;来訪位置を読み込み
	FOR LOCAL:1, 450, 462
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	FOR LOCAL:1, 1, CHARANUM
		RELATION:C_ID:(LOCAL:1) = CSVRELATION(C_ID, LOCAL:1)
	NEXT
	;大家フラグ
	CFLAG:C_ID:家主候補 = CSVCFLAG (C_ID, GETNUM(CFLAG, "家主候補"))
	CFLAG:C_ID:居住必要信頼度 = CSVCFLAG (C_ID, GETNUM(CFLAG, "居住必要信頼度"))
	CFLAG:C_ID:家賃 = CSVCFLAG (C_ID, GETNUM(CFLAG, "家賃"))

	CFLAG:C_ID:固有指令 = CSVCFLAG (C_ID, GETNUM(CFLAG, "固有指令"))
	CFLAG:C_ID:地位 = CSVCFLAG (C_ID, GETNUM(CFLAG, "地位"))
	CFLAG:C_ID:初始位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "初始位置"))
	SIF CFLAG:C_ID:神社在住
		CFLAG:C_ID:初始位置 = CFLAG:C_ID:神社在住
	CFLAG:C_ID:来訪時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "来訪時間"))
	CFLAG:C_ID:帰宅時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "帰宅時間"))
	CFLAG:C_ID:就寝時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "就寝時間"))
	CFLAG:C_ID:起床時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "起床時間"))
	CFLAG:C_ID:經常去的地方 = CSVCFLAG (C_ID, GETNUM(CFLAG, "經常去的地方"))
	CFLAG:C_ID:自宅位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "自宅位置"))
	CFLAG:C_ID:移動率補正 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移動率補正"))
	CFLAG:C_ID:移動節度 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移動節度"))
	CFLAG:C_ID:来訪抑制値 = CSVCFLAG(C_ID, GETNUM(CFLAG, "来訪抑制値"))
	CFLAG:C_ID:弾幕特能 = CSVCFLAG (C_ID, GETNUM(CFLAG, "弾幕特能"))
	SIF !CFLAG:C_ID:弾幕特能
		CALL 弾幕特殊能力設定(C_ID)
	;最大気力体力がCSV値より低い場合CSVに合わせる
	IF !CFLAG:C_ID:回復速度降低
		SIF MAXBASE:C_ID:0 < CSVBASE (C_ID, 0)
			MAXBASE:C_ID:0 = CSVBASE (C_ID, 0)
		SIF MAXBASE:C_ID:1 < CSVBASE (C_ID, 1)
			MAXBASE:C_ID:1 = CSVBASE (C_ID, 1)
	ENDIF
	;酒耐性
	MAXBASE:C_ID:酒気 = CSVBASE(C_ID, GETNUM(BASE, "酒気"))
	CALL SET_MINIMUM_EXP_ALL(C_ID)
	FOR 更新能力番号, 0, 111
		SIF EXP:C_ID:更新能力番号 < CSVEXP (C_ID,更新能力番号)
			EXP:C_ID:更新能力番号 = (EXP:C_ID:更新能力番号 + CSVEXP (C_ID,更新能力番号))
	NEXT
	;パンツ種類非存在時消去
	FOR LOCAL:1, 2, MAXPANTS
		SIF CFLAG:C_ID:(LOCAL:1 + 胖次管理) && GET_STR(C_ID, "下半身内衣_ずらし可能", LOCAL:1, "名前") == ""
			CFLAG:C_ID:(LOCAL:1 + 胖次管理) = 0
	NEXT
	;子供の性別と生年月日
	IF EXP:C_ID:出産経験
		FOR LOCAL:1, 1, 11
			IF CHILD_BIRTHDAY:C_ID:(LOCAL:1) && !CHILD_BIRTHDATE:C_ID:(LOCAL:1)
				;生年月日設定
				CHILD_BIRTHDATE:C_ID:(LOCAL:1) = DAY_TO_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
				;天気は遡れないので晴れってことで
				CHILD_WEATHER:C_ID:(LOCAL:1) = たしか晴天
				;エレンの子供は例外
				IF C_ID == [[艾倫]]
					CHILD_SEX:C_ID:(LOCAL:1) = 0
				ELSE
					;初回
					IF !兒童設定初回 && !FLAG:兒童の性別
						兒童設定初回 = 1
						PRINTL 請設定小孩的性別
						PRINTL 請進行選択
						CALL ASK_M("随机", 1, "女の子", 1, "男の子", 1)
						SIF RESULT == 1 || RESULT == 2
							FLAG:兒童の性別 = RESULT
					ENDIF
					IF !FLAG:兒童の性別
						CHILD_SEX:C_ID:(LOCAL:1) = RAND:2 + 1
					ELSE
						CHILD_SEX:C_ID:(LOCAL:1) = FLAG:兒童の性別
					ENDIF
					PRINTFORM %CHILDNAME:C_ID:(LOCAL:1), 15, LEFT% は \@ CHILD_SEX:C_ID:(LOCAL:1) == 1 ? 女 # 男 \@の子 で生年月日は 
					CALL PRINT_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
					PRINTFORML
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT
CFLAG:MASTER:神社在住 = CFLAG:MASTER:初始位置
IF FLAG:住宿人物 && SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):神社在住 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):初始位置 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):自宅位置 = SUMIKOMI_ROOM
ENDIF
SIF !FLAG:酒虫 
	FLAG:酒虫 = 1

;霊夢
NAME:[[霊夢]] = 博麗 霊夢

;る～こと

;カナ

;魅魔

;サニー

;ルナ

;スター

;ちゆり

;夢美

;萃香

;魔理沙

;ルーミア

;大ちゃん

;⑨

;メイド長

;おぜう

;アリス

;白百合

;黒百合

;リリカ

;メルラン

;ルナサ

;妖夢

;橙

;藍

;紫

;リグル

;ミスチー
;文

;映姫様

;早苗

;神奈子様

;諏訪子様

;てんこ

;衣玖さん

;おりんりん
;おくう


;こいし

;ナズ

;小傘

;ぬえ

;はたて

;華扇

;エレン

;理香子

;明羅

;里香

;ルイズ

;さとりん

;フラン

;にとり

;鈴仙
NAME:[[鈴仙]] = 鈴仙・優曇華院・因幡
;てゐ
;パチュリー

;白蓮
NAME:[[白蓮]] = 聖 白蓮

;神子様

;こころ
;CFLAG:57:神社在住 = CFLAG:57:初期位置

;美鈴

;こぁ

;パルスィ

;妹紅

;姫様

;影狼
;勇儀


;椛
;ゆゆさま

;慧音
NAME:[[慧音]] = 上白沢 慧音

;幽香
;TALENT:68:105 = 1

;マミゾウ

;小鈴

;針妙丸
NAME:[[針妙丸]] = 少名 針妙丸
;CFLAG:71:神社在住 = CFLAG:71:初期位置
;TALENT:71:100 = -5

;永琳
NAME:[[永琳]] = 八意 永琳

;蛮奇

;レティ

;メディスン

;小町

;静葉

;穣子

;雛

;阿求

;蓮子

;メリー

;キスメ

;ヤマメ

;一輪

;水蜜

;星


;響子
;芳香


;青娥

;屠自古


;布都


;わかさぎ姫

;弁々

;八橋

;雷鼓

;正邪

;依姫
NAME:[[依姫]] = 綿月 依姫


;豊姫
NAME:[[豊姫]] = 綿月 豊姫

;レイセン

;朱鷺子

;神綺

;夢子

;ユキ

;マイ

;菫子


;清蘭

;鈴瑚


;ドレミー

;サグメ

;クラウンピース
CALLNAME:[[克勞恩皮絲]] = 克勞恩皮絲

;純狐


;ヘカーティア
CALLNAME:[[赫喀提亞]] = 赫喀提亞

;隠岐奈

SIF !EXP:[[白蓮]]:Ｖ経験
	TALENT:[[白蓮]]:0 = 1
;初対面管理をFLAG:13からCFLAG:18に移行
IF FLAG:13 != 0
	FOR C_ID, 1, 63
		SIF GETBIT(FLAG:13, C_ID)
			CFLAG:C_ID:面識 = 1
	NEXT
	FLAG:13 = 0
ENDIF

;衣裳フラグの修正
CFLAG:[[霊夢]]:基本服装セット = 101
CFLAG:[[阿空]]:基本服装セット = 137
CFLAG:[[荷取]]:基本服装セット = 151
CFLAG:[[鈴仙]]:基本服装セット = 152
CFLAG:[[帝]]:基本服装セット = 153
CFLAG:[[帕秋莉]]:基本服装セット = 154
CFLAG:[[白蓮]]:基本服装セット = 155
CFLAG:[[神子]]:基本服装セット = 156
CFLAG:[[心]]:基本服装セット = 157
CFLAG:[[針妙丸]]:基本服装セット = 171


;あなた関連処理

MAXBASE:MASTER:酒気 = 1500
;MASTERが女、ふたなり時
IF GETBIT(TALENT:MASTER:性別, 0)
;MASTERが男
ELSE
	TALENT:MASTER:胸圍 = -2
ENDIF
SIF GETBIT(TALENT:MASTER:性別, 1) && !TALENT:MASTER:形状
	CALL TINKO_DECIDE(MASTER)
;小槌の代償が終了せずにずっと粗末な物になっている場合の修正
;要注意大きくした後に二本にした場合、及び二本にした後に大きくした場合、粗末な物がデフォルトになる可能性があります
;その場合、自分で元の形状を再設定しなければなりません
SIF TALENT:MASTER:形状 != CFLAG:MASTER:382 && CFLAG:MASTER:魔力回収 < 0
	TALENT:MASTER:形状 = CFLAG:MASTER:382
SIF CFLAG:MASTER:魔力回収 > 0 && CFLAG:MASTER:小槌 == 0
	CFLAG:MASTER:魔力回収 = 0
IF TALENT:MASTER:体型 == -3
	TALENT:MASTER:体型 = 0
	PRINTFORML ---重新設定%CALLNAME:MASTER%的体型---
	PRINTL 
ENDIF



;折り畳み傘
SIF ITEM:36 == 0 && ITEMSTOCK(36) == 2
	ITEMSALES:36 ++
;ローター
SIF ITEM:0 < 0
	ITEM:0 = 0
SIF ITEM:0 == 0 && ITEMSTOCK(0) == 2
	ITEM:0 = 1

FLAG:出禁人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	SIF CFLAG:C_ID:出禁
		FLAG:出禁人数 ++
NEXT

SIF FLAG:3 == 0
	PRINTL 宴会的有無设定改为了OFF

LOCAL = TARGET
FOR C_ID, 1, CHARANUM
	RESULT = 0
	TARGET = C_ID
	TRYCALLFORM M_KOJO_K{C_ID}
	IF RESULT
		IF !CFLAG:C_ID:口上実装状況
			口上実装チェック:C_ID = 1
			リセット画面 = 1
			CFLAG:C_ID:口上実装状況 = 1
		ENDIF
		PRINTFORML 请确认%CALLNAME:C_ID%的口上的UPDATE是否存在
		TRYCALLFORM M_KOJO_UPDATE_K{C_ID}
		PRINTL 
	ELSE
		CFLAG:C_ID:口上実装状況 = 0
	ENDIF
NEXT
TARGET = LOCAL

SIF FLAG:開始日 < DAY
	FLAG:開始日 = DAY + 1
VARSET FLAG, 0, 300, 500
VARSET LOCAL
;パンツをCFLAGに移行
;SAVESTR:10に2文字以上あれば実行
IF STRLENS(SAVESTR:10) > 1
	FLAG:700 = 0
	FOR LOCAL,1,CHARANUM
		FOR LOCAL:1,0,MAXPANTS
			LOCALS = /%CALLNAME:LOCAL%{LOCAL:1}/
			CFLAG:LOCAL:(LOCAL:1 + 胖次管理) = STRCOUNT(SAVESTR:10,LOCALS)
			SIF CFLAG:LOCAL:(LOCAL:1 + 胖次管理)
				FLAG:700 ++
		NEXT
	NEXT
	SAVESTR:10 =
	PRINTL 你重要的内裤藏品已移交完毕
ENDIF

;小作人あぷで
IF FLAG:地主 && !FLAG:農民難易度
	CALL MODE_NOUMIN
	CALL LOAN_NOUMIN
ENDIF

;紅魔カジノ用
SIF !FLAG:筹码変動状況
	CALL SET_CHARISMA("NEW")

PRINTFORML 如果使用了旧版存档、不重置委托的话、有非常高可能出现BUG。
PRINTFORML 要重置委托么？
CALL ASK_YN
SIF !RESULT
	CALL IRAI_RESET

;アイテム類の在庫管理
CALL ITEM_RANGE, "採購食材"
FOR LOCAL, RESULT:0, RESULT:1
	SIF ITEM_TYPES( LOCAL, "採購食材" ) && (ITEMSALES:LOCAL == 0 || ITEMSALES:LOCAL > 30 )
		ITEMSALES:LOCAL = LIMIT( ITEMSALES:LOCAL, 10, 30 )
NEXT
CALL ITEM_RANGE, "酒屋"
FOR LOCAL, RESULT:0, RESULT:1
	SIF ITEM_TYPES( LOCAL, "酒屋" ) && (ITEMSALES:LOCAL == 0 || ITEMSALES:LOCAL > 30 )
		ITEMSALES:LOCAL = LIMIT( ITEMSALES:LOCAL, 5, 30 )
NEXT
CALL ITEM_RANGE, "花屋"
FOR LOCAL, 1 + RESULT:0, RESULT:1
	SIF ITEM_TYPES( LOCAL, "花屋" ) && (ITEMSALES:LOCAL == 0 || ITEMSALES:LOCAL > 3 )
		ITEMSALES:LOCAL = 3
NEXT

IF TALENT:MASTER:体型 == -3
	TALENT:MASTER:体型 = 0
	PRINTFORML ---重新設定%CALLNAME:MASTER%的体型---
	PRINTL 
ENDIF
IF !FLAG:宴会開催FLAG
ELSEIF !FLAG:32
	;必要なのかわからんが念のため、です
	;暫定処理でとりあえず博麗神社境内を指定、主催者がズレたりしても気にするな
	FLAG:32 = 2
	PRINTFORML 修改宴会的举办位置
ELSEIF FLAG:32 == 230
	;バグ報告でのFLAG:32の内容はおそらくこっち
	FLAG:32 = 218
	PRINTFORML 修改宴会的举办位置
ENDIF

CALL SET_ENKAI_LOCATIONDISPLAY
SAVESTR:本体バージョン = %eraTW_Version%

IF リセット画面
	PRINTFORML 以下的角色的口上是新实施的。
	PRINTFORML 请选择想要重置好感度、经验、能力等的角色。
	$PRINT_RESETCHARA
	FOR LOCAL,1,CHARANUM
		SIF !口上実装チェック:LOCAL
			CONTINUE
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTFORML 
	DRAWLINE
	PRINTFORML [999] 終了
	INPUT
	SIF 人物数量上限 < RESULT
		GOTO UPDATE_END
	SIF !口上実装チェック:RESULT
		GOTO UPDATE_END
	IF 口上実装チェック:RESULT
		口上実装チェック:RESULT = 0
		CALL RESET_KOJO_CHARA(RESULT)
		GOTO PRINT_RESETCHARA
	ENDIF
ENDIF

$UPDATE_END

PRINTW 更新完毕

;アプデでCSV読み込みによる更新されない素質
@TALENT_NOT_UPDATED(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 15,28,29,35 TO 39, 42 TO 49, 58, 59, 63 TO 69, 72 TO 79, 87 TO 91, 95 TO 99, 107 TO 120, 122 TO 129
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@履歴文字列変換(ARG)
#DIM 日期
VARSET LOCALS
IF CFLAG:ARG:初吻喪失記念日 && CSTR:ARG:初吻喪失履歴 == ""
	日期 = CFLAG:ARG:初吻喪失記念日
	LOCALS = %PRINT_DATE_F(日期)%、%CALLNAME:(CFLAG:ARG:初吻喪失对手)%に
	SIF CSTR:ARG:初吻位置 != ""
		LOCALS += @"在%CSTR:ARG:初吻位置%"
	IF CFLAG:ARG:初吻喪失対象 & (喪失_自己 | 喪失_恋慕 | 喪失_思慕)
		LOCALS += "献上了初吻"
	ELSE
		SIF CFLAG:ARG:初吻喪失対象 & 喪失_なし崩し
			LOCALS += "就那様"
		LOCALS += "被夺走初吻了"
	ENDIF
	CSTR:ARG:初吻喪失履歴 = %LOCALS%
	PRINTFORML %CALLNAME:ARG%的初吻成为了历史
ENDIF

IF CFLAG:ARG:尻穴処女喪失記念日 && CSTR:ARG:尻穴処女喪失履歴 == ""
	日期 = CFLAG:ARG:尻穴処女喪失記念日
	LOCALS:1 = %PRINT_DATE_F(日期)%、\@ CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止? 時間停止中# \@
	SIF CSTR:ARG:Ａ処女喪失位置 != ""
		LOCALS:1 += @"在%CSTR:ARG:Ａ処女喪失位置%"
	IF GETBIT (CFLAG:ARG:尻穴処女喪失対象,3)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%用穿戴式假陰茎"
	ELSEIF GETBIT (CFLAG:ARG:尻穴処女喪失対象,2)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%用肛用振動棒"
	ELSEIF GETBIT (CFLAG:ARG:尻穴処女喪失対象,1)
		LOCALS:1 += @"被%CALLNAME:(CFLAG:ARG:尻穴処女喪失对手)%的阴茎"
	ENDIF
	IF CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止
		LOCALS:1 += "夺走了尻穴处女"
	ELSE
		LOCALS:1 += "奉献了尻穴処女"
	ENDIF
	CSTR:ARG:尻穴処女喪失履歴 = %LOCALS:1%
	PRINTFORML %CALLNAME:ARG%的尻穴処女成为了历史
ENDIF

IF CFLAG:ARG:処女喪失記念日 && CSTR:ARG:処女喪失履歴 == ""
	日期 = CFLAG:ARG:処女喪失記念日
	LOCAL = CFLAG:ARG:処女喪失対象
	LOCALS:2 = %PRINT_DATE_F(日期)%、
	SIF LOCAL & 喪失_時間停止
		LOCALS:2 += "時間停止中"
	SIF GETBIT (LOCAL,9)
		LOCALS:2 += "喝得酩酊大醉的时候烂醉如泥的时候"
	SIF GETBIT (LOCAL,16)
		LOCALS:2 += "在睡觉的时候"
	SIF CSTR:ARG:処女喪失位置 != ""
		LOCALS:2 += @"在%CSTR:ARG:処女喪失位置%\@ LOCAL & 喪失_恋慕 ? 心爱的# \@%CALLNAME:(CFLAG:ARG:処女喪失对手)%"
	IF GETBIT (LOCAL,3)
		LOCALS:2 += "用穿戴式假陰茎"
	ELSEIF GETBIT (LOCAL,2)
		LOCALS:2 += "用振動棒"
	ELSEIF GETBIT (LOCAL,1)
		LOCALS:2 += "的阴茎"
	ENDIF
	SIF LOCAL & 喪失_なし崩し
		LOCALS:2 += "破坏性地"
	IF (GETBIT (LOCAL, 11) || GETBIT (LOCAL, 12)) && ((!GETBIT (LOCAL,9) && !GETBIT (LOCAL,13) && !GETBIT (LOCAL,16)) || GETBIT (LOCAL,14))
		LOCALS:2 += "献上处女"
	ELSE
		SIF LOCAL & 喪失_强行
			LOCALS:2 += "用力地"
		LOCALS:2 += @"就那様\@ARG == MASTER ? 献上了处女# 被夺走了处女\@"
	ENDIF
	CSTR:ARG:処女喪失履歴 = %LOCALS:2%
	PRINTFORML %CALLNAME:ARG%的処女成为了历史
ENDIF

@RESET_KOJO_CHARA(ARG)
CFLAG:ARG:好感度 = 0
CFLAG:ARG:信頼度 = 0
CFLAG:ARG:掌握把柄 = 0
CFLAG:ARG:被掌握把柄 = 0
CFLAG:ARG:既成事実 = 0
CFLAG:ARG:允许无套 = 0
CFLAG:ARG:面識 = 0
CFLAG:ARG:指出缺点 = 0
CFLAG:ARG:オナバレ = 0
CFLAG:ARG:眠姦発覚 = 0
CFLAG:ARG:バレンタイン = 0
CFLAG:ARG:最後に会った日 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:由于妊娠而追加尺寸 = 0
CFLAG:ARG:清い交際 = 0
CFLAG:ARG:出産経過日 = 0
CFLAG:ARG:育児人数 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0
CFLAG:ARG:烂酔奸 = 0
CFLAG:ARG:不會喝酒 = 0
CFLAG:ARG:强行内射 = 0
CFLAG:ARG:妊娠 = 0
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:兒童人数 = 0
CFLAG:ARG:子宝補正 = 0
CFLAG:ARG:産休 = 0
CFLAG:ARG:累計膣内精液 = 0
CFLAG:ARG:累計肛門精液 = 0
CFLAG:ARG:累計精飲 = 0
CFLAG:ARG:累計精浴 = 0
MARK:ARG:時姦刻印 = 0
MARK:ARG:反発刻印 = 0
MARK:ARG:反発取得履歴 = 0
MARK:ARG:苦痛刻印 = 0
MARK:ARG:快楽刻印 = 0
MARK:ARG:不埒刻印 = 0

;CSTR:ARG:浮気バレ =
;CSTR:ARG:浮気公認 =
UWAKI_KNOWN:ARG:0 =
UWAKI_ACCEPTED:ARG:0 =

;陥落
TALENT:ARG:把柄 = 0
TALENT:ARG:恋慕 = 0
TALENT:ARG:思慕 = 0
TALENT:ARG:淫乱 = 0
TALENT:ARG:愛欲 = 0
TALENT:ARG:炮友 = 0
TALENT:ARG:妊娠 = 0
TALENT:ARG:育児中 = 0
TALENT:ARG:母性 = 0
TALENT:ARG:恋人 = 0
TALENT:ARG:初潮 = 0
TALENT:ARG:母乳体質 = 0
SIF TALENT:MASTER:恋人 == ARG
	TALENT:MASTER:恋人 = 0
;淫乱系
TALENT:ARG:受虐狂 = 0
TALENT:ARG:自慰狂 = 0
TALENT:ARG:淫壺 = 0
TALENT:ARG:尻穴狂 = 0
TALENT:ARG:淫乳 = 0
TALENT:ARG:接吻魔 = 0
TALENT:ARG:子宮口性感 = 0

IF CFLAG:ARG:回復速度降低
	MAXBASE:ARG:体力 += 500
	MAXBASE:ARG:気力 += 500
	TALENT:ARG:回復速度 = CFLAG:ARG:回復速度降低 - 2
	CFLAG:ARG:回復速度降低 = 0
ENDIF
IF CFLAG:ARG:乳品质提升 == 1 && TALENT:ARG:胸圍 > -2
	TALENT:ARG:胸圍 -= 1
	CFLAG:ARG:乳品质提升 = 0
ENDIF
TALENT:ARG:痛覚 = CSVTALENT(ARG,40)
CFLAG:ARG:尻穴処女喪失記念日 = 0
CFLAG:ARG:処女喪失記念日 = 0
CSTR:ARG:尻穴処女喪失履歴 =
CSTR:ARG:処女喪失履歴 =
CSTR:ARG:初吻喪失履歴 =

FOR LOCAL,0,100
	ABL:ARG:LOCAL = CSVABL(ARG,LOCAL)
	EXP:ARG:LOCAL = CSVEXP(ARG,LOCAL)
NEXT
VARSET JUEL:ARG:0, 0
VARSET CHILD_BIRTHDAY:ARG:0, 0
VARSET CHILD_WEATHER:ARG:0, ""
VARSET CHILD_SEX:ARG:0, 0
VARSET CHILDNAME:ARG:0, ""
SIF !EXP:ARG:接吻経験
	TALENT:ARG:無接吻経験 = 1
TALENT:ARG:0 = CSVTALENT(ARG,0)
PRINTFORMW %CALLNAME:ARG%重置了。



;CSV更新用
@SET_MINIMUM_EXP_ALL(C_ID)
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力値

FOR 更新能力番号,0,100
	最低能力値 = CSVABL(C_ID, 更新能力番号)
	SIF 最低能力値 == 0
		CONTINUE
	;戦闘能力
	IF ABLNAME:更新能力番号 == "戦闘能力"
		;戦闘能力修正
		SIF ABL:C_ID:更新能力番号 < 最低能力値 && EXP:C_ID:戦闘経験 == CSVEXP(C_ID, GETNUM(EXP, "戦闘経験"))
			ABL:C_ID:更新能力番号 = 最低能力値
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "戦闘経験"), 最低能力値)
		CONTINUE
	ENDIF
	ABL:C_ID:更新能力番号 = MAX(最低能力値, ABL:C_ID:更新能力番号)
	SELECTCASE ABLNAME:更新能力番号
	CASE "清掃技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "清掃経験"), 最低能力値)
	CASE "話術技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "会話経験"), 最低能力値)
	CASE "教養"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "学習経験"), 最低能力値)
	CASE "料理技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "料理経験"), 最低能力値)
	CASE "音楽技能"
		SELECTCASE TALENT:C_ID:音楽知識
		CASE 1
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "演奏経験"), 最低能力値)
		CASE 2
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "歌唱経験"), 最低能力値)
		CASE 3
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "舞踏経験"), 最低能力値)
		ENDSELECT
	CASE "釣魚技能"
		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 47))
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "釣魚経験"), 最低能力値)
		TALENT:C_ID:釣りLv = MAX(TALENT:C_ID:釣りLv, ABL:C_ID:釣魚技能)
	CASE "採集技能"
		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 48))
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "採集経験"), 最低能力値)
		TALENT:C_ID:採集Lv = MAX(TALENT:C_ID:採集Lv, ABL:C_ID:採集技能)
	CASE "調合技能"
		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 49))
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "調合経験"), 最低能力値)
		TALENT:C_ID:調合Lv = MAX(TALENT:C_ID:調合Lv, ABL:C_ID:調合技能)
	ENDSELECT
NEXT


;非エロ系ABL、40～49において
;CSV準拠で能力が上昇した場合に最低限のEXPを取得し、表示する
@SET_MINIMUM_EXP(C_ID, VAR_ID, MINIMUM_ABL)
#DIM C_ID
#DIM VAR_ID
#DIM MINIMUM_ABL
#DIM MINIMUM_EXP
MINIMUM_EXP = EXPLV:(MINIMUM_ABL + 2)
SIF EXP:C_ID:VAR_ID >= MINIMUM_EXP
	RETURN
EXP:C_ID:VAR_ID = MINIMUM_EXP
PRINTFORML %CALLNAME:C_ID%的%EXPNAME:VAR_ID%上升了{EXP:C_ID:VAR_ID}

;-------------------------------------------------
;CSVを使わず弾幕特殊能力を個別に設定する関数
;新しくキャラクターを追加する際にCSVだと手間なので増設したものです
;▼memo
;0	【言笑自若】: 相手のダイス数Down
;	何があっても決して慌てず、落ち着いていることのたとえ。
;1	【温柔敦厚】: 相手のダイス面Down
;	優しく穏やかで、思いやりがあること。
;2	【勇気凛々】: %CALLNAME:OPPONENT%のダイス数Up
;	失敗や危険をかえりみず、勇敢に物事に立ち向かっていこうとするさま。
;3	【豪放磊落】: %CALLNAME:OPPONENT%のダイス面Up
;	心が広く大らかで、些細なことは気にしないこと。または、そのような様子。
;4	【怪力乱神】: %CALLNAME:OPPONENT%のダイス値が相手のダイス値より小さくても、その差が一定数以下なら、残機が減らない
;	人の知識では理解することが出来ない、怪しく奇怪な現象や物事のこと。
;5	【洽覧深識】: %CALLNAME:OPPONENT%の初期残機が4になる
;	体験して得た経験や知識が多くあり、様々なことを知っていること。
;6	【残忍酷薄】: 相手の被弾からの復帰時のボム数が-1される
;	人のことを気遣うこともなく、むごいこと。
;7	【乾坤一擲】: %CALLNAME:OPPONENT%が一定確率でボムを使うようになる
;	自身の運命を賭けて、運まかせの大博打をすること。
;8	【狷介孤高】: %CALLNAME:OPPONENT%のダイス修正値Up
;	意志を曲げずに、他人と協力しないこと。
;9	【幽愁暗恨】: 相手のダイス修正値Down
;	誰にも知られることのない深い恨みや憂いのこと。
;10  【百載無窮】: 全ての特殊能力をあわせ持つ。最強
;	いつまでも無くなることがないこと。
;-------------------------------------------------
@弾幕特殊能力設定(ARG)
#DIM CONST SKL言笑自若 = 0
#DIM CONST SKL温柔敦厚 = 1
#DIM CONST SKL勇気凛々 = 2
#DIM CONST SKL豪放磊落 = 3
#DIM CONST SKL怪力乱神 = 4
#DIM CONST SKL洽覧深識 = 5
#DIM CONST SKL残忍酷薄 = 6
#DIM CONST SKL乾坤一擲 = 7
#DIM CONST SKL狷介孤高 = 8
#DIM CONST SKL幽愁暗恨 = 9
#DIM CONST SKL百載無窮 = 10

CFLAG:ARG:弾幕特能 = 0

SELECTCASE ARG
CASE [[瓔花]]
	SETBIT CFLAG:ARG:弾幕特能, SKL温柔敦厚
CASE [[潤美]]
	SETBIT CFLAG:ARG:弾幕特能, SKL豪放磊落
CASE [[久侘歌]]
	SETBIT CFLAG:ARG:弾幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弾幕特能, SKL勇気凛々
CASE [[八千慧]]
	SETBIT CFLAG:ARG:弾幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弾幕特能, SKL洽覧深識
	SETBIT CFLAG:ARG:弾幕特能, SKL残忍酷薄
CASE [[磨弓]]
	SETBIT CFLAG:ARG:弾幕特能, SKL勇気凛々
	SETBIT CFLAG:ARG:弾幕特能, SKL怪力乱神
CASE [[袿姫]]
	SETBIT CFLAG:ARG:弾幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弾幕特能, SKL温柔敦厚
	SETBIT CFLAG:ARG:弾幕特能, SKL洽覧深識
CASE [[早鬼]]
	SETBIT CFLAG:ARG:弾幕特能, SKL怪力乱神
	SETBIT CFLAG:ARG:弾幕特能, SKL豪放磊落
	SETBIT CFLAG:ARG:弾幕特能, SKL乾坤一擲
ENDSELECT

