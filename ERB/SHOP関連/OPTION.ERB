;オプション設定処理
@OPTION
LOCAL:1 = 0
DRAWLINEFORM =
FOR LOCAL, 0, 100
	SIF OPTION_SETNAME(LOCAL) == ""
		CONTINUE
	PRINTFORML [{LOCAL, 2}] - %OPTION_SETNAME(LOCAL), 30, RIGHT%：%RESULTS:1%
	LOCAL:1 ++
NEXT
DRAWLINEFORM －
;修改开始
PRINTFORML [93] - 各种补丁的启动设定
;修改结束
PRINTFORML [94] - 自動喘息一括設定
PRINTFORML [95] - 簡易過濾的總設定
PRINTFORML [96] - 料理操作的設定
PRINTFORML [97] - 画像表示設定
PRINTFORML [98] - 登場人物的切换
PRINTFORML [99] - 指令过滤器
DRAWLINE
IF DAY > 120
	PRINTFORML [200] - 败者・食尘
	LOCAL:1 ++
ENDIF
PRINTFORML [100] - 設定完了
INPUT
IF OPTION_SETNAME(RESULT) != ""
	CALL OPTION_SETTING(RESULT)
	CLEARLINE 11 + LOCAL:1
ELSEIF RESULT == 93
	CALL OPTION_DLC
ELSEIF RESULT == 94
	CALL OPTION_自動喘息一括設定
ELSEIF RESULT == 95
	CALL 簡易过滤器一括設定
ELSEIF RESULT == 96
	CALL SET_OPTION_COOKING
ELSEIF RESULT == 97
	CALL OPTION_画像表示詳細設定
ELSEIF RESULT == 98
	CALL 登場人物的切换
ELSEIF RESULT == 99
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 200; && DAY > 120
	PRINTFORML 要返回到第一天么？？
	PRINTL 
	PRINTL [0]不要按下「开关」啊ーー
	PRINTL [1]不！到极限了、按下它！就现在！
	INPUT
	IF RESULT == 1
		CALL 強行开始新的游戏
	ELSE
;		PRINTW 「スイッチ」を押すのは止められなかったが、幸運にもオプション選択までしか戻らなかった。
	ENDIF
ELSE
	RETURN
ENDIF
RESTART

@OPTION_SETNAME(ARG)
#FUNCTIONS
VARSET RESULTS
SELECTCASE ARG
CASE 0
	RESULTS:0 = 妊娠の有無 ※基本OFF
	IF FLAG:妊娠开关
		RESULTS:1 = 会怀孕 ※发生任何意外请自己负責
	ELSE
		RESULTS:1 = 不会怀孕
	ENDIF
CASE 1
	RESULTS:0 = 兒童の性別
	SELECTCASE FLAG:兒童の性別
	CASE 0
		RESULTS:1 = 随机
	CASE 1
		RESULTS:1 = 女孩子
	CASE 2
		RESULTS:1 = 男孩子
	ENDSELECT
CASE 2
	RESULTS:0 = 兒童の描写 
	IF FLAG:兒童描写
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 3
	RESULTS:0 = 住宿事件
	IF FLAG:住宿事件
		RESULTS:1 = 无
	ELSE
		RESULTS:1 = 有
	ENDIF
CASE 4
	RESULTS:0 = 宴会の発生
	IF FLAG:宴会的有無
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 5
	RESULTS:0 = 対方是否会告白
	IF FLAG:告白禁止
		RESULTS:1 = 不会告白
	ELSE
		RESULTS:1 = 会告白
	ENDIF
CASE 6
	RESULTS:0 = 從炮友系陥落向恋慕系陥落変化
	IF FLAG:陥落路線変化
		RESULTS:1 = 会変化
	ELSE
		RESULTS:1 = 不会変化
	ENDIF
CASE 7
	RESULTS:0 = 因事件而[無知]喪失
	IF FLAG:無知自動喪失禁止
		RESULTS:1 = 不会喪失
	ELSE
		RESULTS:1 = 会喪失
	ENDIF
CASE 8
	RESULTS:0 = 文本設定
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		RESULTS:1 = 口上＋情景文本
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		RESULTS:1 = 仅情景文本
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		RESULTS:1 = 不显示文本
	ENDIF
CASE 9
	RESULTS:0 = 口上顔色
	IF FLAG:口上色
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 10
	RESULTS:0 = 汚物程度表示
	IF FLAG:垃圾显示OFF
		RESULTS:1 = 否
	ELSE
		RESULTS:1 = 是
	ENDIF
CASE 11
	RESULTS:0 = counter的地文対素質的改変
	IF FLAG:counter地文控制
		RESULTS:1 = 会変化
	ELSE
		RESULTS:1 = 不会変化
	ENDIF
CASE 12
	RESULTS:0 = 描写胸部的文本
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 全部固定
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 仅固定乳房描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		RESULTS:1 = 仅固定乳头描写
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		RESULTS:1 = 不固定
	ENDIF
CASE 13
	RESULTS:0 = 好意/理性之壁の表示
	IF FLAG:好意理性短縮
		RESULTS:1 = 縮短
	ELSE
		RESULTS:1 = 不縮短
	ENDIF
CASE 14
	RESULTS:0 = 能力表示的標記
	IF FLAG:能力表記
		RESULTS:1 = 数字標記
	ELSE
		RESULTS:1 = 記号標記
	ENDIF
CASE 15
	RESULTS:0 = 毎回合のABLUP
	IF FLAG:ABLUP省略
		RESULTS:1 = 省略
	ELSE
		RESULTS:1 = 不省略
	ENDIF
CASE 16
	RESULTS:0 = 初吻の記録
	SELECTCASE FLAG:ＦＫ判定
	CASE 0
		RESULTS:1 = 僅在親吻時・僅在有意識時
	CASE 1
		RESULTS:1 = 僅在親吻時・無意識時也算
	CASE 2
		RESULTS:1 = 口交時也算・僅在有意識時
	CASE 3
		RESULTS:1 = 口交時也算・無意識時也算
	ENDSELECT
CASE 17
	RESULTS:0 = 是否会在移動時停下
	SELECTCASE FLAG:停下脚步
	CASE 0
		RESULTS:1 = 無条件停下脚步
	CASE 1
		RESULTS:1 = 只有在口上存在的情况下会停下
	CASE 2
		RESULTS:1 = 不会停下脚步
	ENDSELECT
CASE 18
	RESULTS:0 = 遭遇時の依頼提示
	IF FLAG:依頼スルー
		RESULTS:1 = 不显示
	ELSE
		RESULTS:1 = 显示
	ENDIF
CASE 19
	RESULTS:0 = 消耗品の自動購入
	IF FLAG:消耗品自動購入
		RESULTS:1 = 是
	ELSE
		RESULTS:1 = 否
	ENDIF
CASE 20
	RESULTS:0 = 自動時停移動
	SELECTCASE FLAG:瞬間移動
	CASE 0
		RESULTS:1 = ＯＦＦ
	CASE 1
		RESULTS:1 = 仅在外出散步时
	CASE 2
		RESULTS:1 = 仅在拠点内移動時
	CASE 3
		RESULTS:1 = 外出/拠点内均启用
	ENDSELECT
CASE 21
	RESULTS:0 = 進行能力比較來決定會不會被推倒
	IF FLAG:推倒
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 22
	RESULTS:0 = 警告牌使用
	IF FLAG:警告牌使用
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 23
	RESULTS:0 = 来訪模式
	SELECTCASE FLAG:交流来訪模式
	CASE 0
		RESULTS:1 = 正常
	CASE 1
		RESULTS:1 = 好感度50以下不会来访
	CASE 2
		RESULTS:1 = 不会来訪
	ENDSELECT
CASE 24
	RESULTS:0 = 脱衣指令的選択
	SELECTCASE FLAG:脱衣選択
	CASE 0
		RESULTS:1 = 毎回選択
	CASE 1
		RESULTS:1 = 全裸
	CASE 2
		RESULTS:1 = 半脱
	ENDSELECT
CASE 25
	RESULTS:0 = 時間停止時脱衣指令的選択
	SELECTCASE FLAG:胖次泥棒
	CASE 0
		RESULTS:1 = 毎回選択
	CASE 1
		RESULTS:1 = 总是占为己有
	CASE 2
		RESULTS:1 = 总是单单脱掉
	ENDSELECT
CASE 26
	RESULTS:0 = 一部分人物的長筒襪穿戴
	IF FLAG:長筒襪着用
		RESULTS:1 = 有
	ELSE
		RESULTS:1 = 无
	ENDIF
CASE 27
	RESULTS:0 = 增加色色的内衣
	IF FLAG:セクシー胖次追加
		RESULTS:1 = 增加
	ELSE
		RESULTS:1 = 不增加
	ENDIF
CASE 28
	RESULTS:0 = 胖次収集状況表示
	IF FLAG:胖次収集状況タイプB
		RESULTS:1 = 未入手的胖次也会显示
	ELSE
		RESULTS:1 = 仅显示入手的胖次
	ENDIF
ENDSELECT
RETURNF RESULTS:0

@OPTION_SETTING(ARG)
SELECTCASE OPTION_SETNAME(ARG)
CASE "妊娠の有無 ※基本OFF"
	FLAG:妊娠开关 = !FLAG:妊娠开关
CASE "文本設定"
	IF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == 2
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 1 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 0
		FLAG:口上文本設定 = -1
	ELSEIF FLAG:情景文本設定 == 0 && FLAG:口上文本設定 == -1
		FLAG:情景文本設定 = 1
		FLAG:口上文本設定 = 2
	ENDIF
CASE "消耗品の自動購入"
	FLAG:消耗品自動購入 = !FLAG:消耗品自動購入
CASE "汚物程度表示"
	FLAG:垃圾显示OFF = !FLAG:垃圾显示OFF
CASE "是否会在移動時停下"
	CALL オプション切り替え("停下脚步", 2)
CASE "進行能力比較來決定會不會被推倒"
	FLAG:推倒 = !FLAG:推倒
CASE "自動時停移動"
	CALL オプション切り替え("瞬間移動", 3)
CASE "住宿事件"
	FLAG:住宿事件 = !FLAG:住宿事件
CASE "警告牌使用"
	FLAG:警告牌使用 = !FLAG:警告牌使用
CASE "一部分人物的長筒襪穿戴"
	FLAG:長筒襪着用 = !FLAG:長筒襪着用
CASE "宴会の発生"
	FLAG:宴会的有無 = !FLAG:宴会的有無
CASE "来訪模式"
	CALL オプション切り替え("交流来訪模式", 2)
CASE "脱衣指令的選択"
	CALL オプション切り替え("脱衣選択", 2)
CASE "時間停止時脱衣指令的選択"
	CALL オプション切り替え("胖次泥棒", 2)
CASE "口上顔色"
	FLAG:口上色 = !FLAG:口上色
CASE "描写胸部的文本"
	IF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 1 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 1
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 1
		FLAG:Look表示用乳房 = 0
		FLAG:Look表示用乳頭 = 0
	ELSEIF FLAG:Look表示用乳房 == 0 && FLAG:Look表示用乳頭 == 0
		FLAG:Look表示用乳房 = 1
		FLAG:Look表示用乳頭 = 1
	ENDIF
CASE "能力表示的標記"
	FLAG:能力表記 = !FLAG:能力表記
CASE "counter的地文対素質的改変"
	FLAG:counter地文控制 = !FLAG:counter地文控制
CASE "增加色色的内衣"
	FLAG:セクシー胖次追加 = !FLAG:セクシー胖次追加
	IF !FLAG:セクシー胖次追加
		PRINTFORMW 席卷幻想郷的性感内衣热潮结束了
		PRINTL 
		PRINTFORML 要消除掉所拥有的性感内衣吗？
		PRINTFORML 削除対象：超短内裤、兜裆布、T型裤、透明女式短内裤、开放型女式短内裤、创可贴、衬裤（开档）
		PRINTL 
		PRINTL [0] 已经不需要了
		PRINTL [1] 珍藏起来
		INPUT
		IF RESULT == 0
			CALL DELETE_SEXYPANTS
			CLEARLINE 10
		ELSE
			CLEARLINE 8
		ENDIF
	ENDIF
CASE "從炮友系陥落向恋慕系陥落変化"
	FLAG:陥落路線変化 = !FLAG:陥落路線変化
CASE "対方是否会告白"
	FLAG:告白禁止 = !FLAG:告白禁止
CASE "毎回合のABLUP"
	FLAG:ABLUP省略 = !FLAG:ABLUP省略
CASE "因事件而[無知]喪失"
	FLAG:無知自動喪失禁止 = !FLAG:無知自動喪失禁止
CASE "好意/理性之壁の表示"
	FLAG:好意理性短縮 = !FLAG:好意理性短縮
CASE "兒童の描写"
	FLAG:兒童描写 = !FLAG:兒童描写
CASE "兒童の性別"
	CALL オプション切り替え("兒童の性別", 2)
CASE "胖次収集状況表示"
	FLAG:胖次収集状況タイプB = !FLAG:胖次収集状況タイプB
CASE "初吻の記録"
	CALL オプション切り替え("ＦＫ判定", 3)
CASE "遭遇時の依頼提示"
	FLAG:依頼スルー = !FLAG:依頼スルー
ENDSELECT

@オプション切り替え(ARGS, ARG)
FLAG:ARGS ++
SIF FLAG:ARGS > ARG
	FLAG:ARGS = 0

@OPTION_自動喘息一括設定
CALL ASK_M("全員無効",1,"全員有効（无下流）",1,"全員有効（有下流）",1, "中止",1)
IF INRANGE(RESULT, 0, 2)
	FOR LOCAL,1,CHARANUM
		CFLAG:LOCAL:自動喘息 = RESULT
	NEXT
	PRINTFORMW 统一設定了
ENDIF

@登場人物的切换
PRINTL 　被指定的人物将不会登場。（"*"为有口上的角色）
DRAWLINE
FOR LOCAL,1,CHARANUM
	SIF TALENT:LOCAL:路人
		CONTINUE
	SIF CFLAG:LOCAL:出禁
		SETCOLOR C_D_RED
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	PRINTFORMLC [{LOCAL, 3}] \@ RESULT ? * # \@%CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTFORML [999] 設定終了　出禁人物：{FLAG:出禁人数}人　[998]重置
$LOOP
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM
	IF CFLAG:RESULT:出禁 && FLAG:扮演 != RESULT
		CFLAG:RESULT:出禁 = 0
		SIF CFLAG:RESULT:現在位置 == CFLAG:RESULT:初始位置 && CFLAG:RESULT:睡眠
			LOCK:(CFLAG:RESULT:初始位置) = 1
		FLAG:出禁人数 --
	ELSEIF TALENT:RESULT:路人
		PRINTFORMW 这个角色不能解除出禁
		GOTO LOOP
	ELSEIF FLAG:扮演 == RESULT
		IF CFLAG:(FLAG:扮演):出禁 == 1
			PRINTFORML 要扮演現在这个人物的话、将会变成两个%CALLNAME:RESULT%同时存在的情况…
			PRINTL 即使这様也要登場吗？
			PRINTL [0]是 [1]否
			INPUT
			IF RESULT == 0
				CFLAG:(FLAG:扮演):出禁 = 0
				SIF CFLAG:(FLAG:扮演):現在位置 == CFLAG:(FLAG:扮演):初始位置 && CFLAG:(FLAG:扮演):睡眠
					LOCK:(CFLAG:(FLAG:扮演):初始位置) = 1
				FLAG:出禁人数 --
				PRINTFORMW %CALLNAME:(FLAG:扮演)%将登場
			ELSE
				GOTO LOOP
			ENDIF
		ELSE
			CFLAG:(FLAG:扮演):出禁 = 1
			SIF LOCK:(CFLAG:(FLAG:扮演):初始位置) && CFLAG:(FLAG:扮演):初始位置 < 100
				LOCK:(CFLAG:(FLAG:扮演):初始位置) = 0
			FLAG:出禁人数 ++
		ENDIF
	ELSE
		CFLAG:RESULT:出禁 = 1
		SIF LOCK:(CFLAG:RESULT:初始位置) && CFLAG:RESULT:初始位置 < 100
			LOCK:(CFLAG:RESULT:初始位置) = 0
		FLAG:出禁人数 ++
	ENDIF
	RESTART
ELSEIF RESULT == 999
	PRINTW 設定完毕。
ELSEIF RESULT == 998
	FOR LOCAL,1,CHARANUM
		SIF FLAG:扮演 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:路人
			CONTINUE
		CFLAG:LOCAL:出禁 = 0
	NEXT
	FLAG:出禁人数 = 0
	RESTART
ELSE
	GOTO LOOP
ENDIF

;簡易フィルタ制御関数
;仕様上の問題として、設定項目が増えすぎると設計をしなおす必要がある
@簡易过滤器一括設定
#DIM OP_CFLAG_Ｖ過濾器
#DIM OP_CFLAG_Ａ過濾器
#DIM OP_CFLAG_ＳＭ過濾器
#DIM OP_CFLAG_奉仕過濾器
#DIM OP_CFLAG_性騷擾過濾器
#DIM OP_SETTING
IF !LOCAL
	OP_CFLAG_Ｖ過濾器       = GETNUM(CFLAG, "Ｖ過濾器")
	OP_CFLAG_Ａ過濾器       = GETNUM(CFLAG, "Ａ過濾器")
	OP_CFLAG_ＳＭ過濾器     = GETNUM(CFLAG, "ＳＭ過濾器")
	OP_CFLAG_奉仕過濾器     = GETNUM(CFLAG, "奉仕過濾器")
	OP_CFLAG_性騷擾過濾器 = GETNUM(CFLAG, "性騷擾過濾器")
	LOCAL ++
ENDIF
PRINTFORML ～簡易過濾設定～
PRINTFORML ※這裡和單獨個別指令的設定是不一樣的
PRINTFORML 
PRINTFORML [29] 全角色的全簡易過濾OFF
PRINTFORML [28] 開放全角色的[Ｖ]
PRINTFORML [27] 開放全角色的[Ａ]
PRINTFORML [26] 開放全角色的[ＳＭ]
PRINTFORML [25] 開放全角色的[奉仕]
PRINTFORML [24] 開放全角色的[性騷擾]
PRINTFORML [23] 開放[家事]
PRINTFORML 
PRINTFORML [19] 全角色的全簡易過濾ON
PRINTFORML [18] 限制全角色的[Ｖ]
PRINTFORML [17] 限制全角色的[Ａ]
PRINTFORML [16] 限制全角色的[ＳＭ]
PRINTFORML [15] 限制全角色的[奉仕]
PRINTFORML [14] 限制全角色的[性騷擾]
PRINTFORML [13] 限制[家事]
PRINTFORML 
PRINTFORML [99] 設定終了
DO
	INPUT
	OP_SETTING = (RESULT / 10) - 1
	;20で1、10で0
	SELECTCASE RESULT
	CASE 29, 19
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的所有簡易过滤都设为＜OFF＞了
		ELSE
			PRINTFORMW 全角色的所有簡易过滤都设为＜ON＞了
		ENDIF
	CASE 28, 18
		CVARSET CFLAG, OP_CFLAG_Ｖ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ｖ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[Ｖ]指令设为＜限制＞了
		ENDIF
	CASE 27, 17
		CVARSET CFLAG, OP_CFLAG_Ａ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[Ａ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[Ａ]指令设为＜限制＞了
		ENDIF
	CASE 26, 16
		CVARSET CFLAG, OP_CFLAG_ＳＭ過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[ＳＭ]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[ＳＭ]指令设为＜限制＞了
		ENDIF
	CASE 25, 15
		CVARSET CFLAG, OP_CFLAG_奉仕過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[奉仕]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[奉仕]指令设为＜限制＞了
		ENDIF
	CASE 24, 14
		CVARSET CFLAG, OP_CFLAG_性騷擾過濾器, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 全角色的[性騷擾]指令设为＜開放＞了
		ELSE
			PRINTFORMW 全角色的[性騷擾]指令设为＜限制＞了
		ENDIF
	CASE 23, 13
		FLAG:家事過濾器 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 家事指令设为＜開放＞了
		ELSE
			PRINTFORMW 家事指令设为＜限制＞了
		ENDIF
	CASE 99
		PRINTFORMW 簡易过滤的設定完成了
		RETURN
	CASEELSE
		PRINTFORML 無效輸入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;---------------------------------------------------------------
;	画像詳細設定
;	設定が増えたので新設
;---------------------------------------------------------------
@OPTION_画像表示詳細設定
TARGET = [[霊夢]]
FOR LOCAL, 1, 11
	TARGET:LOCAL = LOCAL
NEXT
PRINTL ▼示例▼
CALL PRINT_TARGET_IMAGE(TARGET)
PRINTL 
PRINTFORML [ 0] - 画像\@ FLAG:画像表示 ? 显示 # 不显示 \@
IF FLAG:画像表示
	PRINTFORML [ *]┬ 現在的\@ FLAG:立絵種類 ? 顔絵 # 立絵 \@的顯示人数： {FLAG:画像表示}人 画像大小{100 + isModifier}％
	PRINTFORML [ 1]├ 增加
	PRINTFORML [ 2]└ 减少
	PRINTFORML [ 3] - 角色画像的类型 \@ FLAG:立絵種類 ? [顔] # [立絵] \@
	PRINTFORML [ 4]\@ FLAG:挿絵表示 ? ┬ # %" "%- \@ 挿絵的顯示 \@ FLAG:挿絵表示 ? ON # OFF \@
	IF FLAG:挿絵表示
		PRINT [ 5]└ 射精画像的种类
		SELECTCASE FLAG:射精画像種類
			CASE 0
				PRINTL  [断面＋効果音]
			CASE 1
				PRINTL  [只有断面]
			CASE 2
				PRINTL  [只有効果音]
		ENDSELECT
	ELSE
		PRINTL 
	ENDIF
	PRINTFORML [ 6]\@ FLAG:アニメーション ? ┬ # %" "%- \@動畫設定 \@ FLAG:アニメーション ? 重複回数 {FLAG:アニメーション}回 # OFF \@
	IF FLAG:アニメーション
		PRINTFORML [ 7]└挿入中每回合的动画显示：\@FLAG:挿入アニメ过滤器 & アニメ过滤器_挿入 ? [×]# [〇]\@
	ELSE
		PRINTL 
	ENDIF
	PRINTFORML [ 8] - \@ FLAG:立絵種類 ? 顔絵 # 立絵 \@的顯示位置 [\@ FLAG:画像表示位置 ? 靠左# 在中央 \@]
	PRINTFORML [ 9] - 图像有边框 \@ FLAG:画像枠表示 ? ON # OFF \@
	PRINTFORML [10] - 拡大
	PRINTFORML [11] - 縮小
ELSE
	FOR LOCAL, 1, 13
		PRINTL 
	NEXT
ENDIF
PRINTFORML [999] - 返回
INPUT
IF RESULT == 999
	VARSET TARGET
	RETURN 0
ELSEIF RESULT == 0
	FLAG:画像表示 = !FLAG:画像表示
ELSEIF RESULT == 1
	FLAG:画像表示++
	SIF FLAG:画像表示 > 10
		FLAG:画像表示 = 10
ELSEIF RESULT == 2
	FLAG:画像表示--
	SIF FLAG:画像表示 < 1
		FLAG:画像表示 = 1
ELSEIF RESULT == 3
	FLAG:立絵種類 = !FLAG:立絵種類
ELSEIF RESULT == 4
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 5 && FLAG:挿絵表示
	FLAG:射精画像種類++
	SIF FLAG:射精画像種類 >= 3
		FLAG:射精画像種類 = 0
ELSEIF RESULT == 6
	FLAG:アニメーション++
	SIF FLAG:アニメーション >= 6
		FLAG:アニメーション = 0
ELSEIF RESULT == 7
	INVERTBIT FLAG:挿入アニメ过滤器, 0
	;CALL ASK_M("挿入表示切替",1,"絶頂表示切替",1,"射精表示切替",1,"射精表示切替",1,"戻る",1)
	;SIF RESULT == 4
	;	RESTART
	;INVERTBIT FLAG:挿入アニメフィルタ, RESULT
	RESTART
ELSEIF RESULT == 8
	FLAG:画像表示位置 = !FLAG:画像表示位置
ELSEIF RESULT == 9
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 10
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 11
	isModifier = MAX(isModifier - 10, -50)
ENDIF
RESTART

;料理オプション設定関数
;0:手動決定,1:異物不可,2:薬物不可
@SET_OPTION_COOKING
#DIM CONST OP_BIT_手動決定 = 0
#DIM CONST OP_BIT_異物不可 = 1
#DIM CONST OP_BIT_薬物不可 = 2
#DIM CONST OP_BIT_採点オン = 3
PRINTFORML 設定料理関連的選項
PRINTFORML 現在的設定
IF GET_OPTION_COOKING("料理内容手動決定")
	PRINTFORML 　料理内容：手動決定
ELSE
	PRINTFORML 　料理内容：自動決定
ENDIF
IF GET_OPTION_COOKING("異物混入不可")
	PRINTFORML 　異物混入：否
ELSE
	PRINTFORML 　異物混入：是
ENDIF
IF GET_OPTION_COOKING("薬物混入不可")
	PRINTFORML 　薬物混入：否
ELSE
	PRINTFORML 　薬物混入：是
ENDIF
SELECTCASE GET_OPTION_COOKING("自動味付け設定")
CASE 0
	PRINTFORML 　自動味付：否
CASE 10
	PRINTFORML 　自動味付：自動设为普通口味
CASE 11
	PRINTFORML 　自動味付：自動设为浓厚口味
CASE 12
	PRINTFORML 　自動味付：自動设为清淡口味
ENDSELECT
IF GET_OPTION_COOKING("採点機能オフ")
	PRINTFORML 　採点機能：OFF
ELSE
	PRINTFORML 　採点機能：ON
ENDIF
DO
	PRINTFORML [9] 料理内容の設定変更
	PRINTFORML [8] 異物混入の設定変更
	PRINTFORML [7] 薬物混入の設定変更
	PRINTFORML [6] 自動味付の設定変更
	PRINTFORML [5] 採点機能の設定変更
	PRINTFORML 
	PRINTFORML 
	PRINTFORML [0] 決定
	INPUT
	SELECTCASE RESULT
	CASE 9
		INVERTBIT FLAG:料理オプション, OP_BIT_手動決定
	CASE 8
		INVERTBIT FLAG:料理オプション, OP_BIT_異物不可
	CASE 7
		INVERTBIT FLAG:料理オプション, OP_BIT_薬物不可
	CASE 6
		PRINTFORML 自動調味的機能設定
		PRINTFORML [12] 自動調味＜薄味＞
		PRINTFORML [11] 自動調味＜浓郁＞
		PRINTFORML [10] 自動調味＜普通の味＞
		PRINTFORML [ 0] 不使用自動調味
		DO
			INPUT
			SELECTCASE RESULT
			CASE 10, 11, 12
				CLEARBIT FLAG:料理オプション, 10, 11, 12
				SETBIT   FLAG:料理オプション, RESULT
			CASE 0
				CLEARBIT FLAG:料理オプション, 10, 11, 12
			CASEELSE
			PRINTFORML 無效輸入
			CONTINUE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 5
		INVERTBIT FLAG:料理オプション, OP_BIT_採点オン
	CASE 0
		RETURN 
	CASEELSE
		PRINTFORML 無效輸入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART


;料理オプションの内容取得関数
@GET_OPTION_COOKING(TYPE)
#FUNCTION
#DIMS DYNAMIC TYPE
#DIM CONST OP_BIT_手動決定 = 0
#DIM CONST OP_BIT_異物混入不可 = 1
#DIM CONST OP_BIT_薬物混入不可 = 2
#DIM CONST OP_BIT_採点機能オン = 3
#DIM CONST OP_BIT_自動味付け_普通 = 10
#DIM CONST OP_BIT_自動味付け_濃い = 11
#DIM CONST OP_BIT_自動味付け_单薄的 = 12
SELECTCASE TYPE
CASE "料理内容手動決定"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_手動決定)
		RETURNF 1
CASE "料理内容自動決定"
	RETURNF !GET_OPTION_COOKING("料理内容手動決定")

CASE "異物混入不可"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_異物混入不可)
		RETURNF 1
CASE "異物混入可能"
	RETURNF !GET_OPTION_COOKING("異物混入不可")

CASE "薬物混入不可"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_薬物混入不可)
		RETURNF 1
CASE "薬物混入可能"
	RETURNF !GET_OPTION_COOKING("薬物混入不可")
CASE "自動味付け設定"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_普通)
		RETURNF OP_BIT_自動味付け_普通
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_濃い)
		RETURNF OP_BIT_自動味付け_濃い
	SIF GETBIT(FLAG:料理オプション, OP_BIT_自動味付け_单薄的)
		RETURNF OP_BIT_自動味付け_单薄的
CASE "採点機能オン"
	SIF GETBIT(FLAG:料理オプション, OP_BIT_採点機能オン)
		RETURNF 1
CASE "採点機能オフ"
	RETURNF !GET_OPTION_COOKING("採点機能オン")
CASEELSE
	THROW 不明なTYPE%TYPE%
ENDSELECT
RETURNF 0


@SIMATTYAUOJISAN
;TW414_移動パッチにて右クリックやマクロによるスキップもしまっちゃうように修正
;PRINTでのウェイトをなくすかわりにFORCEWAITでスキップ解除するように
;
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子就在这吗？」
FORCEWAIT
PRINT 没收叔叔出現了！
FORCEWAIT
PRINT 「那么、输入画面上不可能显示的数字的孩子的钱、就这様没收了哦」
FORCEWAIT
PRINTFORM 洞窟叔叔把%CALLNAME:MASTER%的现金没收了
MONEY = 0
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的道具也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的道具被没收了
FOR LOCAL,0,500
	ITEM:LOCAL = 0
NEXT
FORCEWAIT
PRINT 「输入画面上不可能显示的数字的孩子的技巧也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的技巧被没收了
FORCEWAIT
FOR LOCAL,0,100
	ABL:0:LOCAL = 0
NEXT
PRINT 「输入画面上不可能显示的数字的孩子的経験也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%的経験被没收了
FORCEWAIT
FOR LOCAL,0,100
	EXP:0:LOCAL = 0
NEXT
;せっかくだしコレクションもしまっちゃいましょう（eraTW4.15パンツ対策パッチにて追加）
;
PRINT 「输入画面上不可能显示的数字的孩子的内裤藏品也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%殚精竭智収集到的宝贝般的内裤藏品被没收了
FORCEWAIT
FLAG:700 = 0
FOR LOCAL,1,CHARANUM
	FOR LOCAL:1,0,MAXPANTS
		CFLAG:LOCAL:(LOCAL:1 + 胖次管理) = 0
	NEXT
NEXT
PRINT 「输入画面上不可能显示的数字的孩子的信頼也、就这様没收了哦」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%获得的少女们的信頼被没收了
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:信頼度 = 0
NEXT
FORCEWAIT
;
;
;PRINT 「画面上に表示されていない数字を手打ちする子の基礎能力も、どんどんしまっちゃうからね」
;FORCEWAIT
;PRINTFORM %CALLNAME:MASTER%の基礎能力はしまわれてしまった
;FOR LOCAL,0,9
;	MAXBASE:0:LOCAL = 99
;	BASE:0:LOCAL = min(MAXBASE:0:LOCAL,BASE:0:LOCAL)
;NEXT
;さすがにやり過ぎは良くないと思ったのでクリック連打で許してみた
;作者様へ　勝手に当てたパッチですので不愉快でしたらこの部分だけパッチ前に戻してくださって構いません。
;
;
FORCEWAIT
PRINTW 不要再这様啦～～～～


;セクシーパンツ削除
@DELETE_SEXYPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,20,26
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT

;バイブパンツ削除
@DELETE_VIBEPANTS
#DIM 対象
#DIM 種類
#DIM 所有数

VARSET RESULT

FOR 対象,1,CHARANUM
	FOR 種類,27,30
		所有数 = CFLAG:対象:(種類 + 100)
		IF 所有数 >= 1
			RESULT:0 += (所有数)
			CFLAG:対象:(種類 + 100) = 0
		ENDIF
	NEXT
NEXT


