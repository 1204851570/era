;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;MASTER生成処理
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_CHARAMAKE(ARG)

;CALL COLORMESSAGE(@"このバリアントには画像表示機能がありますが、バリアント単品では画像が表示されません。",C_RED,1,1)
;CALL COLORMESSAGE(@"readmeに記載してあるURLからリソースをDLし、実行ファイルと同じ階層に置いてください。",C_RED,1,1)

;NOは適当に設定
NO:ARG = 0

;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:2 = 1
;MASTERは初期性別＝男
SIF ARG == 0
	TALENT:ARG:2 = 2
;気力、体力
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0
MAXBASE:ARG:1 = 2000
BASE:ARG:1 = MAXBASE:ARG:1

;モード選択
$INPUT_LOOP
PRINTL ★★请选择游戏模式★★
PRINTL [0]START [1]扮演模式
INPUT
IF RESULT == 0
	FLAG:5 = 0
ELSEIF RESULT == 1
	$LOOP_NARIKIRI
	FOR LOCAL,1,CHARANUM
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTL
	DRAWLINE
	PRINTL [900] 取消
	PRINTL 要变成誰呢？
	INPUT
	SELECTCASE RESULT
		CASE 1 TO CHARANUM
			NAME:MASTER = %NAME:RESULT%
			CALLNAME:MASTER = %CALLNAME:RESULT%
			FOR LOCAL,0,200
				TALENT:MASTER:LOCAL = TALENT:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,55
				ABL:MASTER:LOCAL = ABL:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,14
				MAXBASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
				BASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,100
				EXP:MASTER:LOCAL = EXP:RESULT:LOCAL
			NEXT
			CFLAG:MASTER:7 = RESULT
			CFLAG:MASTER:初始位置 = CFLAG:RESULT:初始位置
			CFLAG:RESULT:出禁 = 1
			FLAG:扮演 = RESULT
			PRINTFORMW 以%CALLNAME:RESULT%的身份来開始游戏
			IF !TALENT:MASTER:処女
				PRINTL 要变成処女吗？
				PRINTL [0]是 [1]否
				INPUT
				IF RESULT == 0
					TALENT:MASTER:処女 = 1
					EXP:MASTER:Ｖ経験 = 0
					ABL:MASTER:Ｖ感覚 = 0
					EXP:MASTER:Ｖ性交経験 = 0
					EXP:MASTER:Ｖ拡張経験 = 0
					EXP:MASTER:精液経験 = 0
				ENDIF
			ENDIF
			IF MAXBASE:MASTER:体力 < 2000
				PRINTFORML 這個角色的体力很低。要提高到2000麼？
				CALL ASK_YN("提高","就這樣")
				SIF RESULT == 0
					MAXBASE:MASTER:体力 = 2000
			ENDIF
			IF MAXBASE:MASTER:気力 < 2000
				PRINTFORML 這個角色的気力很低。要提高到2000麼？
				CALL ASK_YN("提高","就這樣")
				SIF RESULT == 0
					MAXBASE:MASTER:気力 = 2000
			ENDIF
			CALL CLOTHES_CUSTOM
			RETURN 0
		CASE 900
			GOTO INPUT_LOOP
		CASEELSE
			GOTO LOOP_NARIKIRI
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

;名前決定
CALL NAME_CUSTOM(ARG)

;ステータス設定
CALL CUSTOM_TERMINAL(ARG)



;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;キャラを追加したときにいちいちここをいじる必要の無いように
;キャラクタ数上限はERHファイルで定義
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS
FOR LOCAL,CHARANUM,人物数量上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	TARGET = LOCAL
	CALL SET_MINIMUM_EXP_ALL(LOCAL)
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	SIF RESULT
		CFLAG:LOCAL:口上実装状況 = 1
NEXT
CVARSET BASE, GETNUM(BASE, "酒気"), 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;MASTERステータス設定処理
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_TERMINAL(ARG)
;排他処理
;MASTERが女、ふたなり時の排他
IF TALENT:ARG:性別 & 1
	ABL:ARG:断袖属性 = 0
	ABL:ARG:断袖中毒 = 0
	EXP:ARG:断袖経験 = 0
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
	SIF TALENT:ARG:性別 != 1
		TALENT:ARG:具現 = 0
;MASTERが男時の排他
ELSE
	ABL:ARG:Ｖ感覚 = 0
	ABL:ARG:百合属性 = 0
	ABL:ARG:百合中毒 = 0
	EXP:ARG:百合経験 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:乳交経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:胸圍 = -2
	TALENT:ARG:風騷 = 0
	TALENT:ARG:初潮 = 0
	TALENT:ARG:具現 = 0
ENDIF
;チ○コ付いてりゃ精力セット
IF TALENT:ARG:2 & 2
	MAXBASE:ARG:精力 = 1000
	BASE:ARG:精力 = 1000
	MAXBASE:ARG:勃起 = 1500
	;実績ボーナス分
	SIF ARG == MASTER && GETBIT(FLAG:702,8)
		MAXBASE:MASTER:精力 += 100
ELSE
	TALENT:ARG:非童貞 = 0
	MAXBASE:ARG:精力 = 0
	BASE:ARG:精力 = 0
ENDIF
;処女ならV系経験無し
IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
;非処女はV経験１
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

;===========
;画面表示
;===========
DRAWLINE
PRINTFORML 名称：%NAME:ARG%　称呼：%CALLNAME:ARG%
CALL BASE_BAR(ARG, "体力", 28, "緑", "「改行」")
CALL BASE_BAR(ARG, "気力", 28, "青", "「改行」")
;能力・性技表示
CALL PRINT_STATE_ABL(ARG)
PRINTL 
;素質表示
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
;経験表示
CALL PRINT_STATE_EXP(ARG)
PRINTL
;------------------------------
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL 这様真的可以吗？
;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG,LOCAL:101)
;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ELSEIF LOCAL:100 == 1006
	CALL CLOTHES_CUSTOM
ENDIF
;------------------------------
DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ? 是 # 決定\@  [1001] - 名字的変更  [1002] - 体力、気力的設定
PRINTL [1003] - 素質的設定  [1004] - 能力的設定  [1005] - 経験的設定
INPUT
;最終決定
IF RESULT == 1000 && LOCAL:100 == 1000
	SIF NO:MASTER == NO:ARG
		CALL CLOTHES_CUSTOM
	RETURN 0
;設定系入力
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF

;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 2500
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 2500
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
;素質設定
ELSEIF LOCAL:100 == 1003
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;陥落素質ほか（1でも2でもダメ）
	SIF 不能选的素質(RESULT, NO:ARG)
		RESTART
	;性別
	IF RESULT == GETNUM(TALENT, "性別")
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;性別嗜好
	ELSEIF RESULT == GETNUM(TALENT, "性別嗜好")
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1 || RESULT == 6
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
		;オンオフ型で別途追加が必要な素質
		;母乳体質
		IF RESULT == 149
			IF TALENT:ARG:RESULT
				MAXBASE:ARG:母乳 = 2000
			ELSE
				MAXBASE:ARG:母乳 = 0
			ENDIF
		ENDIF
	;体型
	ELSEIF RESULT == GETNUM(TALENT, "体型")
		;体型が「小人(-5)」と「巨人(3)」の場合を独立処理
		IF TALENT:ARG:RESULT == -5
			TALENT:ARG:RESULT = -2
		ELSEIF TALENT:ARG:RESULT == 5
			TALENT:ARG:RESULT = -5
		ELSEIF TALENT:ARG:RESULT == 2
			TALENT:ARG:RESULT = 5
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;量的素質
	ELSE
		;端まで行くと-1を掛ける
		IF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;中毒関係
	SIF LOCAL >= 30
		RESTART
	;奇数(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
	;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		RESTART
	ENDIF
	PRINTFORM 請輸入%EXPNAME:LOCAL%      
	PRINTBUTTON "[中止]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT,30)
ELSEIF RESULT == 1006
ENDIF
RESTART

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;名前の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@NAME_CUSTOM(ARG)
PRINTL 请输入角色的名字（NAME）
IF STRLENS(NAME:ARG)
	PRINTFORM 現在的名字：%NAME:ARG%      
	PRINTBUTTON "[不変更]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 请输入角色的名字（CALLNAME）
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在的称呼：%CALLNAME:ARG%      
	PRINTBUTTON "[不変更]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;ベースの設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@BASE_CUSTOM(ARG)
CALL BASE_BAR(ARG, "体力", 28, "緑")
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 2500
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
CALL BASE_BAR(ARG, "気力", 28, "青")
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 2500
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;素質の設定
;CALL		USER
;ARG		0:MASTERキャラNO, 1:ページ数
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL, 0, 157
	SETCOLOR 150, 150, 150
	IF 不能选的素質(LOCAL, NO:ARG) == 1
		CONTINUE
	ELSEIF 不能选的素質(LOCAL, NO:ARG) == 2
		;表示はさせる
		SETCOLOR 50, 50, 50
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
		GOTO SKIP
	ENDIF
	SELECTCASE TALENTNAME:LOCAL
	;TALENT.CSVに登録されていない
	CASE ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	CASE GET_TALENTNAME(LOCAL,1)
		SIF TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
	CASE "非童貞"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]童貞
	CASE "無接吻経験"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
	;量的素質
	CASEELSE
		LOCALS =  [{LOCAL,3}]%TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 = 【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
			SETCOLOR C_AQUA
		ELSEIF TALENTNAME:LOCAL == "性別"
			LOCALS:1 = 【女性】
			SETCOLOR C_AQUA
		ELSE
			LOCALS:1 = 【－】
		ENDIF
		LOCALS += LOCALS:1
	ENDSELECT
	$SKIP
	SIF LOCAL:1 % 3 == 0
		PRINTL 
	IF 不能选的素質(LOCAL, NO:ARG) == 2
		PRINTPLAINFORM %LOCALS, 30, LEFT%
	ELSE
		PRINTFORM %LOCALS, 30, LEFT%
	ENDIF
	LOCAL:1 ++
NEXT
RESETCOLOR
PRINTL


;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;能力の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;経験の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	SIF LOCAL == 57 || (60 <= LOCAL && LOCAL <= 65)
		CONTINUE
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;服装の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CLOTHES_CUSTOM(ARG)
;あなた以外なら変更しない
SIF NO:ARG != NO:MASTER || FLAG:扮演
	RETURN 0
;MASTERの性別判定
IF TALENT:ARG:2 & 1
	CFLAG:ARG:7 = 2
ELSE
	CFLAG:ARG:7 = 1
ENDIF
PRINTFORML 請選擇平時的装扮。
DRAWLINE
$LOOP
PRINTL [0] - 全裸
PRINTL [1] - 一套男装
PRINTL [2] - 一套女装
PRINTL [3] - 一套執事服
PRINTL [4] - 一套女僕服
PRINTL [5] - 一套神官服
PRINTL [6] - 一套作務衣
PRINTL [7] - 一套道士服
PRINTL [8] - 一套学生服
PRINTL [9] - 一套女学生服
INPUT
SELECTCASE RESULT
	CASE 0
	PRINTFORMW 太过于暴露啦！
	GOTO LOOP
	CASE 1 TO 6
		CFLAG:MASTER:7 = RESULT
	CASE 7 TO 9
		CFLAG:MASTER:7 = RESULT + 1
	CASEELSE
		GOTO LOOP
ENDSELECT
RETURN 0

@不能选的素質(ARG, CHARA)
#FUNCTION
#DIM CHARA
;CHARAはNO:の形で渡されてくる
;RETURNF 2は表示はさせるもの
SELECTCASE ARG
;劣情察知～,ボーナス素質および特殊陥落,種族,孕ませ系
CASE IS >=157
	RETURNF 1
;あなたは選べない１
CASE GETNUM(TALENT, "無接吻経験"), GETNUM(TALENT, "習得速度") TO GETNUM(TALENT, "擅長用針"), GETNUM(TALENT, "薬毒耐性"), GETNUM(TALENT, "容易高潮"), GETNUM(TALENT, "盲信"), GETNUM(TALENT, "小悪魔")
	SIF CHARA == NO:MASTER
		RETURNF 1
;あなたは選べない２
CASE GETNUM(TALENT, "魅力") TO GETNUM(TALENT, "謎之魅力"), GETNUM(TALENT, "回復速度") TO GETNUM(TALENT, "不可思議的根"), GETNUM(TALENT, "狂気"), GETNUM(TALENT, "人気")
	SIF CHARA == NO:MASTER
		RETURNF 1
;あなたは選べず、女性器がないと選べない
CASE GETNUM(TALENT, "母乳体質")
	SIF CHARA == NO:MASTER
		RETURNF 1
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
;女性器がないと選べない
CASE GETNUM(TALENT, "処女"), GETNUM(TALENT, "風騷"), GETNUM(TALENT, "Ｖ感度")
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
;男性器がないと選べない
CASE GETNUM(TALENT, "非童貞")
	SIF !GETBIT(TALENT:CHARA:性別, 1)
		RETURNF 2
;男性器があると選べない
CASE GETNUM(TALENT, "具現")
	SIF GETBIT(TALENT:CHARA:性別, 1)
		RETURNF 2
;あなた以外は選べない
CASE GETNUM(TALENT, "年齢")
	SIF CHARA != NO:MASTER
		RETURNF 1
;全員選べない１
CASE GETNUM(TALENT, "恋慕") TO GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "心情"), GETNUM(TALENT, "容姿"), GETNUM(TALENT, "把柄"), GETNUM(TALENT, "調合知識"), GETNUM(TALENT, "調合Lv"), GETNUM(TALENT, "採集Lv"), GETNUM(TALENT, "釣りLv")
	RETURNF 1
;全員選べない２
CASE GETNUM(TALENT, "自慰狂") TO GETNUM(TALENT, "子宮口性感"), GETNUM(TALENT, "嫉妬"), GETNUM(TALENT, "濃厚精液"), GETNUM(TALENT, "精愛味覚"), GETNUM(TALENT, "禁断的知識"), GETNUM(TALENT, "精力超群") TO GETNUM(TALENT, "母性")
	RETURNF 1
;女性器があって幼児か幼稚がないと選べない
CASE GETNUM(TALENT, "初潮")
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
	SIF !TALENT:CHARA:幼児／幼児退行 && !TALENT:CHARA:幼稚
		RETURNF 2
ENDSELECT
RETURNF 0

;===========
;キャラカスタム
;===========
@CHARA_CUSTOM

	PRINTL 可以变更人物的設定
	FOR LOCAL, 0, CHARANUM
		IF FLAG:扮演
			SIF LOCAL == FLAG:扮演
				CONTINUE
		ENDIF
		PRINTFORMLC [{LOCAL}] - %CALLNAME:LOCAL%
	NEXT
	PRINTL [999] 設定完了
	INPUT

	IF RESULT >= 0 && RESULT < CHARANUM
		SIF RESULT != FLAG:扮演
			CALL CUSTOM_TERMINAL(RESULT)
	ELSEIF RESULT == 999
		PRINTL 可以吗？
		CALL ASK_YN
		IF !RESULT
			RETURN
		ENDIF
	ENDIF
	RESTART
RETURN 0

@LIMITED_CUSTOM(ARG)
$LOOP
PRINTFORML [0] 変更体型            現在的体型:%GET_TALENTNAME(100,TALENT:ARG:100)%\@ TALENT:ARG:100 == 0? 普通# \@
PRINTFORML [1] 変更胸围            現在的胸围:%GET_TALENTNAME(105,TALENT:ARG:105)%\@ TALENT:ARG:105 == 0? 普乳# \@
PRINTFORM [2] 感度上昇             
	SELECTCASE TCVAR:ARG:感度上昇部位
		CASE 101 TO 104,106
			PRINTFORML %TALENTNAME:(TCVAR:ARG:感度上昇部位)%選択中
		CASEELSE
			PRINTL
	ENDSELECT
DRAWLINE
PRINTL [100] 終了
INPUT
IF RESULT == 0
	PRINTLC [0] 小人
	PRINTLC [3] 幼児体型
	PRINTLC [4] 矮小体型
	PRINTLC [5] 普通
	PRINTLC [6] 長身
	PRINTLC [7] 巨躯
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	SELECTCASE RESULT
		CASE 0, 3 TO 7
			TALENT:ARG:100 = RESULT - 5
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 1
	PRINTLC [0] 絶壁
	PRINTLC [1] 貧乳
	PRINTLC [2] 普乳
	PRINTLC [3] 巨乳
	PRINTLC [4] 爆乳
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 4
			TALENT:ARG:105 = RESULT - 2
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 2
	PRINTL 可以提升某一个部位的感度
	FOR LOCAL,101,107
		SIF LOCAL == 105
			CONTINUE
		SIF TALENT:ARG:LOCAL > 0
			SETCOLOR C_AQUA
		PRINTFORMLC [{LOCAL}] %TALENTNAME:LOCAL% 
		RESETCOLOR
	NEXT
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	IF RESULT >= 101 && RESULT <= 104 || RESULT == 106
		IF TALENT:ARG:RESULT > 0
			PRINTW 已经是敏感了
		ELSE
			TCVAR:ARG:感度上昇部位 = RESULT
		ENDIF
	ENDIF
	GOTO LOOP
ENDIF