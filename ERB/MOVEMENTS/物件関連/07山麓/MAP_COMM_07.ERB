;--------------------------------------------------
;	マップ内移動処理拡張パッチ付属
;	命蓮寺MAP用関数
;--------------------------------------------------
@MAP_PLACENAME_7(ARG)
#FUNCTIONS

IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE 1 TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE 798
			RETURNF OMANEKI_PLACE()
		CASE 799
			RETURNF "隙間空間"
		CASE MAXROOM
			RETURNF "道路正前方"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "不明"
ENDIF

@MAP_VIEWING_7(AA)
#DIMS REF AA, 0
IF !TFLAG:地圖切換
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 720 TO 725,729,730
			TFLAG:地圖切換 = 3
		CASE 715 TO 719
			TFLAG:地圖切換 = 2
		CASE 726,727,728
			TFLAG:地圖切換 = 4
		CASEELSE
			TFLAG:地圖切換 = 1
	ENDSELECT
ENDIF
CALL DRAW_INFORMATIONLINE(@"%MAPNAME:(TFLAG:地圖切換)%")
PRINTFORML 
TRYCALLFORM MAP_SANROKU_{TFLAG:地圖切換}(AA)

@MAP_CAN_MOVE_7(ARG, ARG:1)
#FUNCTION
SELECTCASE ARG
	;華扇ハウス
	CASE 720 TO 730
		;本人は除外
		SIF ARG:1 == [[華扇]]
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:(ARG:1):現在位置,720,730)
			RETURNF 1
		;恋慕か住み込み
		IF ARG:1 == MASTER && (INRANGE(CFLAG:MASTER:初始位置,720,730) || TALENT:[[華扇]]:恋慕)
			RETURNF 1
		;デート相手が天子か自分が天子
		ELSEIF FLAG:约会的对象 == [[天子]] || ARG:1 == [[天子]]
			RETURNF 1
		;教養が足りないとたどり着けない
		ELSEIF ABL:(ARG:1):教養 < 3
			TFLAG:移動不能メッセージ = 1
			SIF ARG:1 == MASTER
				TIME += 30
			RETURNF 0
		ELSE
			;戦闘力3以上必要
			IF ABL:(ARG:1):戦闘能力 < 3 && !FLAG:時間停止
				TFLAG:移動不能メッセージ = 2
				;CFLAG:(ARG:1):現在位置 = 731
				SIF ARG:1 == MASTER
					TIME += 15
				RETURNF 0
			ELSE
				RETURNF 1
			ENDIF
		ENDIF
;正邪宅
	CASE 733
		;あなたは恋慕があれば入れてもらえる
		IF TALENT:[[正邪]]:恋慕 && ARG:1 == MASTER
			RETURNF 1
		;本人は入れる
		ELSEIF ARG:1 == [[正邪]]
			RETURNF 1
		ELSE
			TFLAG:移動不能メッセージ = ARG
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 1
ENDSELECT

@MAP_CANNOT_WORD_7(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTFORMW 從剛才開始和屋子的距離就完全沒有減少……
		PRINTFORMW 好像在同一個地方一直轉著圈
		;CALL TARGETSET_CHACK
	CASE 2
		PRINTFORMW 有危險的老虎蹲在在庭院中、看到%CALLNAME:MASTER%的樣子後提高了嗚嗚的吼聲
		PRINTFORMW 看來不要靠近比較好
		;CALL TARGETSET_CHACK
	CASE 733
		PRINTFORMW 不知道現在在哪了…
ENDSELECT
