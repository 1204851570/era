@OTHERREGIONS
#DIM 現在地点
#DIM 所要時間
#DIM 現在地MAPID
#DIM GOAL

CALL FIELDMAP
PRINTL 要去哪里呢？
現在地MAPID = GET_MAPID(CFLAG:MASTER:現在位置)
;	// 神社が表示されないのはちょっと寂しいので
PRINTFORM [ 0] - %"博麗神社", 18, LEFT%
IF 現在地MAPID == 0
	SETCOLOR 0 , 255, 0
	PRINTFORM 現在地\@ FLAG:70 ? %"   "% # %" "% \@
	RESETCOLOR
ELSEIF FLAG:70
	PRINTFORM 单程 {TIME_REQUIRED0(5000 + 現在地MAPID) * 3, 3, RIGHT}TSP {TIME_REQUIRED0(5000 + 現在地MAPID) * 3, 3, RIGHT}TSP  
ELSE
	PRINTFORM 单程 {TIME_REQUIRED0(5000 + 現在地MAPID) * 3, 3, RIGHT}TSP {TIME_REQUIRED0(5000 + 現在地MAPID), 3, RIGHT}分  
ENDIF
PRINTL
;	// ここまで神社表示
FOR LOCAL, 5001, 5011
	IF LOCAL == 現在地MAPID + 5000
		PRINTFORM [{現在地MAPID, 2, RIGHT}] - 
		PRINTFORM %NAME_FROM_PLACE(LOCAL), 21, LEFT%
		SETCOLOR 0 , 255, 0
		PRINTFORM 現在地\@ FLAG:70 ? %"    "% # %"  "% \@
		RESETCOLOR
		SIF !(LOCAL % 2)
			PRINTL
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL - 5000, 2, RIGHT}] - 
	PRINTFORM %NAME_FROM_PLACE(LOCAL), 18, LEFT%
	PRINTFORM 单程 {TIME_REQUIRED0(5000 + 現在地MAPID) * 3, 3, RIGHT}TSP \@ FLAG:70 ? {TIME_REQUIRED(現在地MAPID,LOCAL - 5000) * 3, 3, RIGHT}TSP # {TIME_REQUIRED(現在地MAPID,LOCAL - 5000), 3, RIGHT}分 \@ 
	SIF !(LOCAL % 2)
		PRINTL
NEXT
PRINT [100] - 返回
$LOOP
INPUT
;戻る
IF RESULT == 100
	RETURN RESULT
;時間停止発動
ELSEIF RESULT == 101
	
ELSEIF RESULT == 現在地MAPID
	;全体マップからも移動できるけど余計に時間かかる,おまけ機能なので
	LOCAL:1 = 1
	現在地点 = (CFLAG:MASTER:現在位置 - (CFLAG:MASTER:约会中 * 100)) / 10
	所要時間 = ABS(現在地点 - 0) * 3
	FOR LOCAL,0,CHARANUM
		SIF CHK_DATENOW(CFLAG:LOCAL:约会中) 
			CFLAG:LOCAL:現在位置 = (CFLAG:LOCAL:约会中 * 100) + (LOCAL:1 * 10)
	NEXT
	IF FLAG:70 == 1
		BASE:MASTER:TSP -= 所要時間 * 2
		IF TFLAG:運搬
			BASE:MASTER:TSP -= 所要時間 / 3
			CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
		ENDIF
	ELSEIF GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP > 10  && !FLAG:约会的对象
		BASE:MASTER:TSP -= 所要時間 * 2
	ELSE
		TIME += 所要時間
		IF CHK_DATENOW(CFLAG:93:约会中) && !GETBIT (FLAG:泳池, 0)
			TIME += 所要時間 / 2
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 50,500)
		ENDIF
	ENDIF
	RETURN RESULT
ELSEIF RESULT == MAIN_MAP
	IF FLAG:70 && FLAG:约会的对象
		PRINTFORMW 如果要回去的話請解除時間停止
		RETURN 100
	ENDIF
	CALL 从外面回家
	RETURN MAIN_MAP
ELSE
	GOAL = RESULT
	所要時間 = TIME_REQUIRED(現在地MAPID, GOAL)
	;100分の道中はないんです…
	IF FLAG:约会的对象 > 0 && 所要時間 >= 100
		PRINTW 太远了不能一下子过去
		CLEARLINE 2
		GOTO LOOP
	;自宅から出発する処理
	ELSEIF GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
		LOCAL:1 = 0
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:(LOCAL):同行
				LOCAL:1 ++
		NEXT
		IF LOCAL:1 == 1
			;100分の道中はないんです…
			IF 所要時間 >= 100
				PRINTW 太远了不能一下子过去
				CLEARLINE 2
				RETURN 100
			ENDIF
			PRINTL 外出约会
			FOR LOCAL,1,CHARANUM
				IF CFLAG:(LOCAL):同行
					CALL SET_DATE(GOAL,LOCAL)
				ENDIF
			NEXT
		;複数人デート処理（未設定）
		ELSEIF LOCAL:1 > 1
		;一人
		ELSE
			IF FLAG:70 && GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP >= 所要時間 * 3
				RESULT = 1
			ELSE		
				PRINTL 可以通过消費TSP、不消耗時間进行移動。
				PRINTL 要使用TSP吗？
				CALL ASK_M("否", 1, @"是（TSP{所要時間 * 3}消費）", BASE:MASTER:TSP >= 所要時間 * 3)
			ENDIF
			IF RESULT == 0
				FLAG:70 = 0
				TIME += 所要時間
			ELSEIF RESULT == 1
				BASE:MASTER:TSP -= 所要時間 * 3
			ENDIF
			CALL SET_DATE(GOAL,0)
			RETURN GOAL
		ENDIF
		RETURN GOAL
	ELSEIF FLAG:约会的对象 > 0
		PRINTFORML %GET_MAPNAME(GOAL)%へ向かいます
		CALL SET_DATE(GOAL,(FLAG:约会的对象))
		RETURN GOAL
	ELSEIF GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP > 所要時間 * 3 && !FLAG:约会的对象
		BASE:MASTER:TSP -= 所要時間 * 3
	ELSEIF FLAG:70
		IF BASE:MASTER:TSP < 所要時間 * 3
			PRINTW TSP不足
			CLEARLINE 2
			GOTO LOOP
		ELSE
			BASE:MASTER:TSP -= 所要時間 * 3
		ENDIF
	ELSE
		TIME += 所要時間
		IF CHK_DATENOW(CFLAG:93:约会中) && !GETBIT (FLAG:泳池, 0)
			TIME += 所要時間 / 2
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5 * (所要時間),500)
		ENDIF
	ENDIF
	FOR LOCAL,0,CHARANUM
		IF CHK_DATENOW(CFLAG:LOCAL:约会中)
			CFLAG:LOCAL:現在位置 = GET_ENTRANCE(GOAL)
			CFLAG:LOCAL:约会中 = GOAL
		ENDIF
	NEXT
	RETURN GOAL
ENDIF

@HappenToChild
#DIM YOME
#DIM CHILDNUM
SIF RAND:10 > 0
	RETURN
SIF FLAG:70
	RETURN
CALL PickRandGrownChild
YOME = RESULT
CHILDNUM = RESULT:1
VARSET RESULT
SIF YOME <= 0
	RETURN
LOCALS = %CHILDNAME:YOME:CHILDNUM%
PRINTFORMW 出门的时候、碰到了%LOCALS%
IF FLAG:约会的对象 != YOME && FLAG:约会的对象
	PRINTFORMW %CALLNAME:YOME%ではない女性を連れていたことに嫌味を言われた…
ELSE
	PRINTFORMW %LOCALS%%TEXTR("好像没有注意到的样子/点了点头/好像很精神的样子")%
ENDIF