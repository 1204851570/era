;--------------------------------------------------
;	マップ内移動処理拡張パッチ付属
;	竹林MAP用関数
;--------------------------------------------------

@MAP_PLACENAME_4(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE 401 TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE 498
			RETURNF OMANEKI_PLACE()
		CASE 499
			RETURNF "隙間空間"
		CASE MAXROOM
			RETURNF "竹林之外"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "不明"
ENDIF

;@MAP_VIEWING_4
;
;IF CHK_FOCUS(402,CFLAG:MASTER:現在位置,430)
;	CALL MAP_EIENTEI
;ELSEIF INRANGE(CFLAG:MASTER:現在位置,450,466)
;	CALL MAP_MUGENKAN
;ELSE
;	CALL MAP_EIEN_FRONT
;	CALL MAP_BAMBOO
;ENDIF
;RETURN 1

@MAP_VIEWING_4(AA)
#DIMS REF AA, 0
IF !TFLAG:地圖切換
	IF CHK_FOCUS(402,CFLAG:MASTER:現在位置,430)
		TFLAG:地圖切換 = 1
	ELSEIF CHK_FOCUS(450,CFLAG:MASTER:現在位置,466)
		TFLAG:地圖切換 = 2
	ELSE
		TFLAG:地圖切換 = 3
	ENDIF
ENDIF
CALL DRAW_INFORMATIONLINE(@"%MAPNAME:(TFLAG:地圖切換)%")
PRINTFORML 
TRYCALLFORM MAP_NEW_TIKURIN_{TFLAG:地圖切換}(AA)


@MAP_CAN_MOVE_4(ARG, ARG:1)
#FUNCTION
SELECTCASE ARG
	;奥ノ院判定
	CASE 407 TO 430
		;除外
		SIF GROUPMATCH(ARG:1, [[鈴仙]], [[帝]], [[輝夜]], [[永琳]])
			RETURNF 1
		;月の民も良いよね
		SIF GROUPMATCH(ARG:1, [[依姫]], [[豊姫]], [[鈴仙二號]], [[探女]])
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:(ARG:1):現在位置,407,430)
			RETURNF 1
		;あなた
		IF ARG:1 == MASTER
			IF 永遠亭侵入許可() || INRANGE(CFLAG:MASTER:初始位置,401,430) || FLAG:時間停止
				RETURNF 1
			ELSE
				TFLAG:移動不能メッセージ = 401
				RETURNF 0
			ENDIF
		;それ以外
		ELSE
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 1
ENDSELECT

@MAP_CANNOT_WORD_4(ARG)
IF TFLAG:移動不能メッセージ == 401
	PRINTW 沒有關係親密的人的話無法進入永遠亭裡面
	CLEARLINE 2
	REUSELASTLINE 
	RETURN
ENDIF

@永遠亭侵入許可()
#FUNCTION
SIF TALENT:52:恋慕 || TALENT:53:恋慕 || TALENT:62:恋慕 || TALENT:72:恋慕
	RETURNF 1
SIF GROUPMATCH(FLAG:约会的对象,52,53,62,72)
	RETURNF 1
SIF GROUPMATCH(FLAG:扮演,52,53,62,72)
	RETURNF 1
SIF CFLAG:52:同行 || CFLAG:53:同行 || CFLAG:62:同行 || CFLAG:72:同行
	RETURNF 1
SIF FLAG:宴会開催FLAG == 3 && TIME >= FLAG:33
	RETURNF 1
RETURNF 0