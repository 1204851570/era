;FileName_MOVEMENT.ERB ----------------------------- Rev1.00
;時間経過処理
;CALL		USER
;ARG		経過時間前
;RETURN		時間経過量
;COMMENT	
;-----------------------------------------------------------
@TIME_PROGRESS(ARG)
#FUNCTION
SIF TIME + DAY * 1440 < ARG
	RETURNF 0
RETURNF TIME + DAY * 1440 - ARG

;FileName_MOVEMENT.ERB ----------------------------- Rev1.01
;キャラ帰宅処理
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@KITAKU(ARG)
#DIMS 帰宅理由
IF TIME < 420 && TIME > 180
	帰宅理由 = 夜が明けてきたので、
ELSEIF TIME < 1140 && TIME > 900
	帰宅理由 = 日も落ちてきたので、
ELSE
	帰宅理由 = 夜ももう更けてきたので、
ENDIF

IF CFLAG:(ARG):現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:MASTER:睡眠
	CALL OneNightStay(ARG)
	SIF CFLAG:ARG:神社在住
		RETURN
	IF 睡眠時間(ARG)
		PRINTFORMW %CALLNAME:ARG%躺到了床上。
	;デート中
	ELSEIF CHK_DATENOW(CFLAG:(ARG):约会中)
		PRINTFORMW %帰宅理由%%CALLNAME:ARG%との约会はここまでだ。
		;自宅にいる時,お招き部屋にいる時はうふふしていない時
		IF (CFLAG:(ARG):現在位置 == CFLAG:(ARG):自宅位置 || (CFLAG:(ARG):現在位置 == OMANEKIBEYA() && !CFLAG:(ARG):诶嘿嘿))
			CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,3,0)
		;通常のデート中
		ELSE
			;お誘い
			IF (TALENT:ARG:恋慕 || TALENT:ARG:炮友 == 2) && CFLAG:ARG:积攒度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !TCVAR:ARG:连住禁止 && CFLAG:(ARG):現在位置 != OMANEKIBEYA() && CFLAG:ARG:自宅位置
				PRINTFORML 被%CALLNAME:ARG%邀請去房間
				CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,4,0)
				CALL ASK_YN("去","不去")
				IF !RESULT
					PRINTFORML %CALLNAME:ARG%に誘われるまま、%CALLNAME:MASTER%は%CALLNAME:ARG%的房間へと向かった……
					CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,5,0)
					PRINTFORMW +{TIME_REQUIRED(CFLAG:ARG:現在位置 / 100, CFLAG:ARG:自宅位置 / 100)}分
					TIME += TIME_REQUIRED(CFLAG:ARG:現在位置 / 100, CFLAG:ARG:自宅位置 / 100)
					CALL OMANEKI_ENTER(ARG)
					CALL TARGETSET_CHACK
				ELSE
					PRINTFORMW %CALLNAME:ARG%は心なしか寂しそうだ……
					CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,6,0)
					SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
						CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
				ENDIF
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,2,0)
				SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
					CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
			ENDIF
		ENDIF
	;神社内にいるか自宅でもお招き部屋でもない場所
	ELSEIF AT_HOME(ARG) || (CFLAG:(ARG):現在位置 != CFLAG:(ARG):自宅位置 && CFLAG:(ARG):現在位置 != OMANEKIBEYA()) && CFLAG:ARG:行動 != 5
		CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,1,0)
		PRINTFORMW %帰宅理由%%CALLNAME:ARG%は帰って行きました。
		CALL SET_TOGETHER(0,ARG,1)
		SIF (CFLAG:MASTER:現在位置 % 100) % 10 && CHK_DATENOW(CFLAG:MASTER:约会中) && FLAG:约会的对象 == ARG && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
			CFLAG:MASTER:現在位置 = CFLAG:MASTER:约会中 * 100
	ENDIF
	IF CFLAG:(ARG):诶嘿嘿 > 0 || CHK_DATENOW(CFLAG:(ARG):约会中)
		FOR LOCAL,1,CHARANUM
			;お招き中は継続
			SIF CFLAG:MASTER:招待 != LOCAL && CFLAG:LOCAL:诶嘿嘿
				CALL ENDUFUFU(LOCAL)
			;デートは消える
			CFLAG:LOCAL:约会中 = MAIN_MAP
			FLAG:约会的对象 = 0
			TFLAG:约会前好感度 = 0
		NEXT
		;お招きうふふ中は継続
		SIF !CFLAG:MASTER:招待
			CFLAG:MASTER:诶嘿嘿 = 0
		CFLAG:MASTER:同行 = 0
	ENDIF
ENDIF
CFLAG:ARG:同行 = 0

IF !TCVAR:ARG:休憩中 && !CFLAG:ARG:招待
	;活動時間外は自宅があれば寝るまで自宅にいる
	IF CFLAG:ARG:自宅位置 && !IN_HOME(CFLAG:ARG:自宅位置) && !睡眠時間(ARG)
		CFLAG:ARG:現在位置 = CFLAG:ARG:自宅位置
	;自宅がないか寝ると消える
	ELSE
		CFLAG:ARG:現在位置 = SUKIMA()
	ENDIF
ENDIF


@被从寝室赶了出来(ARG)
SIF !ARG
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF !睡眠時間(ARG)
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:ARG:初始位置
	RETURN
SIF !SHIRAHU(ARG)
	RETURN
;忍び込み中
SIF CFLAG:ARG:隠密中
	RETURN
;それ以外

SIF CFLAG:ARG:ブチギレ
	GOTO OIDASHI
;恋慕なら入室を許す
IF (TALENT:ARG:恋慕 || TEQUIP:ARG:縄 || CFLAG:诶嘿嘿 == 1 || EXP:ARG:睡眠姦経験 > 300 || CFLAG:ARG:恶作剧)
ELSEIF FLAG:一時滞在 == ARG
;願望 睡眠欲セット
ELSEIF CFLAG:ARG:初始位置 == 15 && (TALENT:1:恋慕 || TALENT:57:恋慕 || TALENT:71:恋慕)
;ここは誰か一人でも恋慕ならおｋ
;侵入成功なら入室
ELSEIF GETBIT(CFLAG:ARG:42,8)
;それ以外なら追い出し
ELSE
	$OIDASHI
	CLEARLINE 1
	;SIF !CFLAG:ARG:ブチギレ
		PRINTFORMW 被犯困的%CALLNAME:ARG%赶出来了…
	;忍び込みOFF
	CALL SET_SNEAK(0,ARG,2)
	;イタズラOFF
	CALL SET_PRANK(0,ARG,2)
	FOR LOCAL:1,0,CHARANUM
		CALL ENDUFUFU(LOCAL:1)
	NEXT
	;侵入成功フラグクリア
	CLEARBIT CFLAG:ARG:42,8
	TFLAG:MASTERのＰ挿入中 = 0
	;移動可能な場所へ移動
	CALL GETOUT(MASTER)
	;遠距離移動フラグOFF
	TFLAG:遠距離移動 = 0
ENDIF


@浴室から逐出(ARG)
SETCOLOR 150,150,250
PRINTL 【浴場的闖入者】
RESETCOLOR
IF TALENT:ARG:羞恥心 < 0
	IF ABL:ARG:親密 < 5
		PRINTFORML %CALLNAME:ARG%平靜地、說道你進來了的話那我現在就出去了
		CFLAG:ARG:好感度 -= (20 - ABL:ARG:親密 * 2)
		CFLAG:ARG:信頼度 -= (20 - ABL:ARG:親密 * 2)
		SOURCE:ARG:反感 += 300
		CFLAG:MASTER:現在位置 = KICKOUT()
	;ELSE
	;	PRINTFORML %CALLNAME:ARG%は平然として、一緒に入りたいのかと聞いてきた
	ENDIF
ELSE
	PRINTFORML %CALLNAME:ARG%察觉了自己所面对的状況、发出尖叫声飞奔出了浴場
	CFLAG:ARG:好感度 -= (50 - ABL:ARG:親密 * 2)
	CFLAG:ARG:信頼度 -= (50 - ABL:ARG:親密 * 2)
	SOURCE:ARG:反感 += 1000
	BASE:ARG:憤怒 += (1500 - ABL:ARG:親密 * 100)
	CFLAG:ARG:現在位置 = KICKOUT()
	;キャラを動かしたのでTARGET再設定
ENDIF
CFLAG:ARG:好感度 = MAX(CFLAG:ARG:好感度,0)
CFLAG:ARG:信頼度 = MAX(CFLAG:ARG:信頼度,0)
CALL TARGETSET_CHACK
CFLAG:ARG:同室 = 0
RETURN 1


@待客室処理(ARG)
SIF FLAG:時間停止 || !CFLAG:ARG:招待
	RETURN

;お招き部屋から出たら日常に戻る
;泊まる前か後かは連泊禁止フラグを参照してます
IF CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL 待客室から出る(ARG)

;翌日仕事に行く場合
ELSEIF 仕事中チェック(ARG) && TCVAR:ARG:连住禁止 && CFLAG:ARG:诶嘿嘿 != 1
	CFLAG:MASTER:現在位置 = CFLAG:ARG:自宅位置
	IF CFLAG:ARG:诶嘿嘿
		PRINTFORMW %CALLNAME:ARG%好像要開始工作了…
		CALL ENDUFUFU(LOCAL)
		PRINTFORMW %CALLNAME:ARG%雖然依依不捨但還是離開了
	ELSE
		PRINTFORMW %CALLNAME:ARG%因為工作而離開了
	ENDIF
	CALL 待客室から出る(ARG)

;デート中
ELSEIF CHK_DATENOW(CFLAG:ARG:约会中)
	IF CFLAG:ARG:神社在住
		;住み込みキャラは睡眠時間になったらデート終了
		IF 睡眠時間(ARG)
			CFLAG:ARG:约会中 = MAIN_MAP
			FLAG:约会的对象 = 0
		ENDIF
	ELSE
		;デート中に帰る時間が来たらメッセージ＆デートだけ消す
		IF !VISIT(ARG)
			CALL KITAKU(ARG)
		ENDIF
	ENDIF
ENDIF


@待客室から出る(ARG)
;遭遇フラグを自宅位置に設定（連動してると他のキャラが駆けつけてくるのは仕様）
CFLAG:ARG:遭遇位置 = CFLAG:ARG:自宅位置
;初期位置を元に戻す（参道の向こうや0もいるが99でもおｋ）
IF CFLAG:ARG:神社在住
	CFLAG:ARG:初始位置 = CFLAG:ARG:神社在住
ELSE
	CFLAG:ARG:初始位置 = SUKIMA()
ENDIF
;活動時間ならそのままデート。住み込みキャラは起きていればデート
;の予定だったがバランスブレイカーな気がする＆朝帰りは一人の方が雰囲気でるので住み込みキャラだけ
;活動前も配置するようになったので起床時間なら外に出るように
IF CFLAG:ARG:神社在住
	CFLAG:ARG:現在位置 = 97 + MAIN_MAP * 100
	CFLAG:ARG:约会中 = MAIN_MAP
	FLAG:约会的对象 = 0
	TFLAG:约会前好感度 = 0
ELSE
	IF !睡眠時間(ARG) && !CFLAG:ARG:睡眠
		;自宅位置に移動
		CFLAG:ARG:現在位置 = CFLAG:ARG:遭遇位置
		;泊まる前or住み込みキャラならデート継続,ワープ移動してたらダメ
		IF !TCVAR:ARG:连住禁止 && CHK_DATENOW(CFLAG:ARG:约会中) && !AT_HOME(MASTER)
			CFLAG:ARG:约会中 = (CFLAG:ARG:自宅位置) / 100
			CFLAG:ARG:同行 = 60
		ENDIF
	;寝ている場合は見えないところに移動
	;住み込みキャラは識別するために現在位置97を使ってるので要注意（MOVEMENT2のSLEEPへ続く）
	ELSE
		CFLAG:ARG:現在位置 = SUKIMA()
		CFLAG:ARG:约会中 = MAIN_MAP
		FLAG:约会的对象 = 0
		TFLAG:约会前好感度 = 0
	ENDIF
ENDIF

;お招きフラグを消して日常に戻す
CFLAG:ARG:招待 = 0
CFLAG:MASTER:招待 = 0
LOCK:(OMANEKIBEYA()) = 0


@OneNightStay(ARG)
#DIM YANUSHI
#DIM REQUIRED_DESIRE
YANUSHI = PRIVATEROOM:(CFLAG:MASTER:初始位置 % 100)
SIF CFLAG:ARG:积攒度 < 800
	RETURN
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF FLAG:一時滞在
	RETURN
SIF CFLAG:ARG:神社在住
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
REQUIRED_DESIRE = 10
SIF TALENT:ARG:恋慕 || TALENT:ARG:炮友
	REQUIRED_DESIRE -= 5
SIF TALENT:ARG:思慕
	REQUIRED_DESIRE -= 2
IF !CFLAG:ARG:既成事実 & 合意_诶嘿嘿
	REQUIRED_DESIRE += TALENT:ARG:難以越過的底線 * 2
	REQUIRED_DESIRE += TALENT:ARG:貞操 * 2
ENDIF
SIF TALENT:ARG:風騷
	REQUIRED_DESIRE = 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE
	RETURN

PRINTFORML %CALLNAME:ARG%は今夜は帰りたくないと言い出した…
CALL ASK_YN("泊める","泊めない")
IF !RESULT
	IF YANUSHI
		PRINTFORMW 泊めてあげたいのは山々だが、%CALLNAME:YANUSHI%が怖い…
		GOTO REFUSAL
	ENDIF
	FLAG:一時滞在 = ARG
	CFLAG:ARG:本来の初始位置 = CFLAG:ARG:初始位置
	CFLAG:ARG:初始位置 = CFLAG:MASTER:初始位置
	CFLAG:ARG:神社在住 = CFLAG:ARG:初始位置
	PRINTFORMW %CALLNAME:ARG%は%NAME_FROM_PLACE(CFLAG:MASTER:初始位置)%に泊まっていくこと成为了
ELSE
	$REFUSAL
	PRINTFORMW %CALLNAME:MASTER%は丁重に断った
ENDIF

@SET_OneNightStay()